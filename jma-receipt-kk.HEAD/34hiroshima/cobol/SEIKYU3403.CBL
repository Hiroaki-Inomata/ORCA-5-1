      *******************************************************************
      * Project code name "ORCA"
      * 日医標準レセプトソフト（JMA standard receipt software）
      * Copyright(C) 2002 JMA (Japan Medical Association)
      *
      * This program is part of "JMA standard receipt software".
      *
      *     This program is distributed in the hope that it will be useful
      * for further advancement in medical care, according to JMA Open
      * Source License, but WITHOUT ANY WARRANTY.
      *     Everyone is granted permission to use, copy, modify and
      * redistribute this program, but only under the conditions described
      * in the JMA Open Source License. You should have received a copy of
      * this license along with this program. If not, stop using this
      * program and contact JMA, 2-28-16 Honkomagome, Bunkyo-ku, Tokyo,
      * 113-8621, Japan.
      ********************************************************************
       IDENTIFICATION  DIVISION.
       PROGRAM-ID.     SEIKYU3403.
      *****************************************************************
      *  システム名        : ＯＲＣＡ
      *  サブシステム名    : 地方公費
      *  コンポーネント名  : 広島・福祉医療費請求書
      *                      (190,290,390,191,291,391,192,292,392)
      *  管理者            : 
      *  作成日付    作業者        記述
      *  05/04/15    有村          様式変更に伴う再作成
      *****************************************************************
      *  プログラム修正履歴
      * Maj/Min/Rev	修正者	日付		内容
      *  01.00.01    有村    05/05/06    医療機関情報取得条件修正
      *  01.00.02    有村    05/07/06    助成が無い場合も出力するよう修正
      *  01.00.03    中川    06/08/21    老人の場合備考欄へ項目追加
      *  01.00.04    内田    06/09/29    市町村公費保険番号も印字対象とする
      *  01.00.05    内田    06/11/22    帳票様式の変更に伴う修正
      *  01.00.06    内田    07/02/27    経過措置の印字の修正
      *                                  保険−一部負担金欄の印字
      *                                  訪問看護日数の印字
      *                                  長期上位は備考欄へ印字
      *  01.00.07    内田    07/06/15    訪問看護日数の判定修正
      *                                   (SRYACTからでは回数が分からない)
      *  01.00.08    内田    07/09/07    日レセver4.0対応
      *                                  システム管理マスタ「1901」の
      *                                  医療機関情報を参照
      *  01.00.09    内田    08/05/03    帳票様式の変更に伴う修正
      *  01.00.10    内田    08/09/09    SPA-AREA初期化
      *  01.00.11    内田    09/01/21    備考欄へ「高半」の印字対応
      *  01.00.12    吉川    10/03/04    前期高齢者９割（軽減措置）は
      *                                  ２割負担で記載する
      *  01.00.13    井上    19/01/09    修正履歴コメント削除
      *****************************************************************
      *
       ENVIRONMENT   DIVISION.
       CONFIGURATION SECTION.
       INPUT-OUTPUT  SECTION.
       FILE-CONTROL.
      *
      *    請求書用ファイル
           SELECT  MF100-FILE   ASSIGN    MF100PARA
                               ORGANIZATION    IS  INDEXED
                               ACCESS  MODE    IS  DYNAMIC
                               RECORD  KEY     IS  MF100-KEY
                               FILE    STATUS  IS  STS-MF100.
      *
006900*    エラーファイル
           SELECT  RECEERR-FILE ASSIGN  RECEERR
                                FILE    STATUS  IS  STS-RECEERR.
      *
       DATA  DIVISION.
       FILE  SECTION.
      *
      *
      *    集計ファイル
       FD  MF100-FILE.
       01  MF100-REC.
           COPY  "SEI3403.INC".
006900*    エラーファイル
       FD  RECEERR-FILE.
       01  RECEERR-REC  PIC X(200).
      *
       WORKING-STORAGE  SECTION.
      *
      *
           COPY  "CPCOMMONDAT2.INC"
                   REPLACING  //RECE01//
                   BY         //MF100//.
      *
      *    エラーファイル 名称領域
           COPY    "CPERRFL.INC"    REPLACING  //ERRFLPARA//
                                    BY         //RECEERR//.
      *
           COPY  "SC3403.INC".
      *
      *    スパ領域
       01  STS-AREA.
           03  STS-MF100      PIC X(02).
           03  STS-RECE02     PIC X(02).
           03  STS-RECEERR    PIC X(02).
      *
      *    フラグ領域
       01  FLG-AREA.
           03  FLG-END        PIC 9(01).
           03  FLG-SYUNOU     PIC 9(01).
           03  FLG-PTINF      PIC 9(01).
           03  FLG-MF100      PIC 9(01).
           03  FLG-TYOKI      PIC 9(01).
           03  FLG-PRINT      PIC 9(01).
           03  FLG-SYSKANRI   PIC 9(01).
           03  FLG-HKNJAINF   PIC 9(01).
           03  FLG-PTKOHINF   PIC 9(01).
           03  FLG-ERR        PIC 9(01).
           03  FLG-HKNCOMBI   PIC 9(01).
           03  FLG-SRYACT     PIC 9(01).
           03  FLG-SRYACCT    PIC 9(01).
           03  FLG-BTPARA     PIC 9(01).
           03  FLG-SYORI      PIC 9(01).
           03  FLG-RECE10     PIC 9(01).
           03  FLG-PTKOHFTN   PIC 9(01).
      *
      *    添字領域
       01  IDX-AREA.
           03  IDX            PIC 9(04).
           03  IDXX           PIC 9(04).
           03  IDX1           PIC 9(04).
           03  IDX2           PIC 9(04).
           03  IDX3           PIC 9(04).
           03  IDY            PIC 9(04).
           03  IDZ            PIC 9(04).
      *
      *    パラメタ領域
       01  WRK-PARA.
           COPY    "CPORCSPRTLNK.INC".
           03  WRK-PARA-JOBID      PIC 9(07).
           03  WRK-PARA-SHELLID    PIC X(08).
           03  WRK-PARA-HOSPNUM    PIC 9(02).
           03  WRK-PARA-PAGE       PIC 9(10).
           03  WRK-PARA-SYORIFLG   PIC X(01).
      *
      *    一時領域
       01  WRK-AREA.
           03  WRK-RECEERR         PIC  X(200).
           03  WRK-Z2              PIC ZZ.
           03  WRK-Z7              PIC ZZZ,ZZZ.
           03  WRK-Z79             PIC ZZZ,ZZ9.
           03  WRK-SRYYMWH         PIC X(16).
           03  WRK-SEIYMDWH        PIC X(22).
           03  WRK-HENYMDG         PIC X(22).
           03  WRK-HENYMDG1        PIC X(09).
           03  WRK-SYMD.
               05  WRK-SYY         PIC 9(04).
               05  WRK-SMM         PIC 9(02).
               05  WRK-SDD         PIC 9(02).
           03  WRK-LASTYMD         PIC 9(08).
           03  WRK-NYMD.
               05  WRK-NYY         PIC 9(04).
               05  WRK-NMM         PIC 9(02).
               05  WRK-NDD         PIC 9(02).
           03  WRK-TYMD.
               05  WRK-TYY         PIC 9(04).
               05  WRK-TMM         PIC 9(02).
               05  WRK-TDD         PIC 9(02).
           03  WRK-HKNJANUM        PIC X(06).
           03  WRK-KOHNUM          PIC X(03).
           03  WRK-KOHID           PIC 9(10).
           03  WRK-SEIKYUGK-G      PIC 9(09)V9(01).
           03  WRK-SEIKYUGK-G2     PIC 9(09).
           03  WRK-SEIKYUGK        PIC 9(09)V9(01).
           03  WRK-KEISAN          PIC 9(09)V9(01).
           03  WRK-JGNGK           PIC 9(09).
           03  WRK-PATH            PIC X(50).
      *    医療機関情報
           03  WRK-HOSPNAME          PIC X(100).
           03  WRK-HOSPNAME-R        REDEFINES   WRK-HOSPNAME.
               05  WRK-HOSPNAME1     PIC X(50).
               05  WRK-HOSPNAME2     PIC X(50).
           03  WRK-HOSPADRS          PIC X(100).
           03  WRK-HOSPADRS-R        REDEFINES   WRK-HOSPADRS.
               05  WRK-HOSPADRS1     PIC X(50).
               05  WRK-HOSPADRS2     PIC X(50).
           03  WRK-HOSPTEL           PIC X(15).
      *    訪問日
           03  WRK-HOUMON-TBL.
               05  WRK-HOUMON-DAY  PIC 9(01)  OCCURS  31.
           03  WRK-WRKYM           PIC 9(06).
      *
      *    文字変換
           03  WRK-MAE-INPUT       PIC X(200).
           03  WRK-OUT-INPUT       PIC X(200).
      *
      *    カウント領域
       01  CNT-AREA.
           03  CNT-RECE02  PIC 9(06).
           03  CNT-MF100   PIC 9(06).
           03  CNT-PAGE    PIC 9(03).
      *
       01  TABLE-AREA.
           03  TBL-PARA-OCC    OCCURS  100.
               05  TBL-PARA-HKNJANUM    PIC X(08).
               05  TBL-PARA-PTID        PIC 9(10).
      *
       01  WRK-CONS-PARA-MAX   PIC 9(03)   VALUE   100.
      *
      *    DB参照用
           COPY    "COMMON-SPA".
      *
      *    固定項目
       01  WRK-CONS-AREA.
           03  WRK-CONS-SRYYM-200804   PIC X(06)   VALUE   "200804".
      *****************************************************************
      *    ファイルレイアウト　領域
      *****************************************************************
      *
      *    システム管理マスタ
           COPY  "CPSK1001.INC".
      *
      *    医療機関情報−所在地、連絡先
           COPY  "CPSK1002.INC".
      *
      *    医療機関編集情報
           COPY    "CPSK1900.INC".
      *
      *    医療機関名称編集情報
           COPY    "CPSK1901.INC".
      *
      *    医療機関住所編集情報
           COPY    "CPSK1902.INC".
      *
      *    ジョブ管理マスタ
       01  JOBKANRI-REC.
           COPY    "CPJOBKANRI.INC".
      *    印刷管理マスタ
       01  PRTKANRI-REC.
           COPY    "CPPRTKANRI.INC".
      *    印刷マスタ
       01  PRTDATA-REC.
           COPY    "CPPRTDATA.INC".
      *
      *    収納情報
       01  SYUNOU-REC.
           COPY  "CPSYUNOU.INC".
      *
      *　　保険者情報
       01  HKNJAINF-REC.
           COPY    "CPHKNJAINF.INC".
      *
      *    患者公費情報マスタ
       01  PTKOHINF-REC.
           COPY  "CPPTKOHINF.INC".
      *
      *    患者公費負担
       01  PTKOHFTN-REC.
           COPY    "CPPTKOHFTN.INC".
      *
      *    患者情報
       01  PTINF-REC.
           COPY    "CPPTINF.INC".
      *
      *    公費請求書
           COPY    "CPKOHSKY.INC"
                   REPLACING  //KOHSKY//
                   BY         //RECE02//.
      *
      *    請求管理
           COPY    "CPRCF010.INC".
      *
      *    保険組合せ
       01  HKNCOMBI-REC.
           COPY    "CPHKNCOMBI.INC".
      *
      *    診療行為
       01  SRYACT-REC.
           COPY    "CPSRYACT.INC".
      *
      *    会計照会
       01  SRYACCT-REC.
           COPY    "CPSRYACCT.INC".
      *
      *    パラメタ
       01  BTPARA-REC.
           COPY    "CPBTPARA.INC".
      *
      *****************************************************************
      *    サブプロ用　領域
      *****************************************************************
      *
      *    ジョブ管理ＤＢ制御サブ
           COPY    "CPORCSJOBKANRI.INC".
      *    印刷ＤＢ制御サブ
           COPY    "CPORCSPRT.INC".
      *
      *    共通パラメタ
           COPY  "MCPAREA".
      *   日付変換サブ
           COPY    "CPORCSDAY.INC".
           COPY    "CPORCSLNK.INC".
      *
           COPY  "MCPDATA.INC".
      *
      *    全角チェックパラメタ
           COPY    "CPORCSKANACHK.INC".
      *
      ****************************************************************
       LINKAGE  SECTION.
       01  COMMAND-PARAM.
           02  FILLER  PIC X(256).
      *****************************************************************
      *
       PROCEDURE  DIVISION
               USING COMMAND-PARAM.
      *
      *****************************************************************
      *    主　　処理
      *****************************************************************
       000-PROC-SEC  SECTION.
      *
           PERFORM 100-INIT-SEC
      *
           PERFORM 200-MAIN-SEC
                   UNTIL  FLG-END = 1
      *
           PERFORM 300-END-SEC
      *
           STOP    RUN
           .
      *****************************************************************
      *    初期　処理
      *****************************************************************
       100-INIT-SEC  SECTION.
      *
           INITIALIZE  FLG-AREA
           INITIALIZE  STS-AREA
           INITIALIZE  WRK-AREA
           INITIALIZE  CNT-AREA
           INITIALIZE  IDX-AREA
           INITIALIZE  SPA-AREA
      *
           DISPLAY "START"
           PERFORM 100-DBOPEN-SEC
      *
           UNSTRING   COMMAND-PARAM    DELIMITED  BY  ","
                                       INTO    LNK-PRTKANRI-RENNUM
                                               LNK-PRTKANRI-TBL-KEY
                                               LNK-PRTKANRI-TBL-GROUP
                                               LNK-PRTKANRI-SHORI-RENNUM
                                               LNK-PRTKANRI-SRYYM
                                               LNK-PRTKANRI-SKYYMD
                                               LNK-PRTKANRI-SHELLID
                                               LNK-PRTKANRI-PRIORITY
                                               LNK-PRTKANRI-TERMID
                                               LNK-PRTKANRI-OPID
                                               LNK-PRTKANRI-PRTNM
                                               WRK-PARA-JOBID
                                               WRK-PARA-SHELLID
                                               WRK-PARA-HOSPNUM
                                               WRK-PARA-SYORIFLG
                                               RECEERR
           END-UNSTRING
           MOVE    WRK-PARA-HOSPNUM    TO  SPA-HOSPNUM
      *
      *    ステップ開始処理
           MOVE    "STS"           TO  SJOBKANRI-MODE
           INITIALIZE                  JOBKANRI-REC
           MOVE    SPA-HOSPNUM     TO  JOB-HOSPNUM
           MOVE    WRK-PARA-JOBID  TO  JOB-JOBID
           MOVE    WRK-PARA-SHELLID
                                   TO  JOB-SHELLID
           MOVE    "SEIKYU3403"    TO  JOB-PGID
           MOVE    "福祉医療費請求書" 
                                   TO  JOB-SHELLMSG
           CALL    "ORCSJOB"       USING
                                   ORCSJOBKANRIAREA
                                   JOBKANRI-REC
                                   SPA-AREA
      *
      *    対象年月最終日取得
           INITIALIZE  WRK-SYMD
           MOVE    LNK-PRTKANRI-SRYYM  TO    WRK-SYMD(1:6)
           PERFORM 31012-LASTDAY-GET-SEC
           MOVE    WRK-SYMD         TO    WRK-LASTYMD
      *
           MOVE    LNK-PRTKANRI-SRYYM  TO  WRK-SYMD (1:6)
           MOVE    "01"                TO  WRK-SYMD (7:2)
           PERFORM 31012-SEIWA-HEN-SEC
           MOVE    WRK-HENYMDG(1:16)   TO  WRK-SRYYMWH 
      *
           INITIALIZE                      STS-AREA-DAY
           INITIALIZE                      LNK-DAY2-AREA
           MOVE    "21"                TO  LNK-DAY2-IRAI
           MOVE    LNK-PRTKANRI-SKYYMD     TO  LNK-DAY2-YMD
           CALL    "ORCSDAY"           USING   STS-AREA-DAY
                                               LNK-DAY2-AREA
           IF      STS-DAY-RC1         =   ZERO 
                   MOVE    LNK-DAY2-EDTYMD3    TO  WRK-SEIYMDWH
           END-IF
      *
      *
           MOVE    "MF100ZZ"           TO  MF100PARA-FILE-ID
           MOVE    LNK-PRTKANRI-TERMID(1:16)
                                       TO  MF100PARA-TERMID
           MOVE  WRK-PARA-HOSPNUM      TO  MF100PARA-HOSPNUM
      *
      *    医療機関ＩＤ編集
           INITIALIZE  SYS-1001-REC
           MOVE    "1001"          TO  SYS-1001-KANRICD
           MOVE    "*"             TO  SYS-1001-KBNCD
           MOVE    LNK-PRTKANRI-SRYYM(1:6)
                                   TO  SYS-1001-STYUKYMD(1:6)
                                       SYS-1001-EDYUKYMD(1:6)
           MOVE    "01"            TO  SYS-1001-STYUKYMD(7:2)
                                       SYS-1001-EDYUKYMD(7:2)
           MOVE  SPA-HOSPNUM       TO  SYS-1001-HOSPNUM
           MOVE    SYS-1001-REC    TO  MCPDATA-REC
           PERFORM 800-SYSKANRI-READ-SEC
           IF      FLG-SYSKANRI    =  ZERO
               MOVE    MCPDATA-REC TO  SYS-1001-REC
           ELSE
               DISPLAY "*** SEIKYU3403 SYS 1001 ERR ***"
               MOVE  1             TO  FLG-END
           END-IF
      *
      *    医療機関情報−所在地、連絡先
           INITIALIZE  SYS-1002-REC
           MOVE    "1002"          TO  SYS-1002-KANRICD
           MOVE    "*"             TO  SYS-1002-KBNCD
           MOVE    LNK-PRTKANRI-SRYYM(1:6)
                                   TO  SYS-1002-STYUKYMD(1:6)
                                       SYS-1002-EDYUKYMD(1:6)
           MOVE    "01"            TO  SYS-1002-STYUKYMD(7:2)
                                       SYS-1002-EDYUKYMD(7:2)
           MOVE  SPA-HOSPNUM       TO  SYS-1002-HOSPNUM
           MOVE    SYS-1002-REC    TO  MCPDATA-REC
           PERFORM 800-SYSKANRI-READ-SEC
           IF      FLG-SYSKANRI    =   ZERO
               MOVE    MCPDATA-REC TO  SYS-1002-REC
           ELSE
               DISPLAY "*** SEIKYU3403 SYS 1002 ERR ***"
               MOVE    1           TO  FLG-END
           END-IF
      *
           MOVE    SYS-1001-HOSPNAME   TO  WRK-HOSPNAME
           MOVE    SYS-1002-ADRS       TO  WRK-HOSPADRS
           MOVE    SYS-1002-TEL        TO  WRK-HOSPTEL
      *    医療機関編集情報
           PERFORM 900-1900-READ-SEC
      *
      *    パラメタ編集処理
           PERFORM 1001-PARA-HENSYU-SEC
      *
           PERFORM 900-KOHSKY-START-SEC
           .
       100-INIT-EXT.
           EXIT.
      *
      *****************************************************************
      *    パラメタ編集処理
      *****************************************************************
       1001-PARA-HENSYU-SEC        SECTION.
      *
           MOVE    ZERO                TO  IDXX
                                           FLG-SYORI
           INITIALIZE                      TABLE-AREA
      *
           IF      WRK-PARA-SYORIFLG   =   "1" 
               INITIALIZE                      BTPARA-REC
               MOVE    SPA-HOSPNUM         TO  BTPARA-HOSPNUM
               MOVE    WRK-PARA-SHELLID    TO  BTPARA-SHELLID
               MOVE    WRK-PARA-JOBID      TO  BTPARA-JOBID
               MOVE    BTPARA-REC          TO  MCPDATA-REC
               MOVE    "DBSELECT"          TO  MCP-FUNC
               MOVE    "tbl_btpara"        TO  MCP-TABLE
               MOVE    "key5"              TO  MCP-PATHNAME
               CALL    "ORCDBMAIN"         USING
                                           MCPAREA
                                           MCPDATA-REC
                                           SPA-AREA
               IF      MCP-RC              =   ZERO
                   MOVE    "tbl_btpara"    TO  MCP-TABLE
                   MOVE    "key5"          TO  MCP-PATHNAME
                   PERFORM 900-BTPARA-READ-N-SEC
               ELSE
                   INITIALIZE                  BTPARA-REC
                   MOVE    1               TO  FLG-BTPARA
               END-IF
      *
               PERFORM     UNTIL   FLG-BTPARA  =   1
                   ADD     1                   TO  IDXX
                   IF      BTPARA-INFO-KBN     =   "1"
                     MOVE    1                 TO  FLG-SYORI
                     MOVE    BTPARA-INFO-PARA
                                       TO TBL-PARA-HKNJANUM(IDXX)
                   ELSE
                     MOVE    2                 TO  FLG-SYORI
                     MOVE    BTPARA-INFO-PTID
                                       TO TBL-PARA-PTID(IDXX)
                   END-IF
                   DISPLAY "パラメタ区分=" BTPARA-INFO-KBN
                           "パラメタ=" BTPARA-INFO-PARA
                           "保険者番号="   TBL-PARA-HKNJANUM(IDXX)
                           "患者番号="     TBL-PARA-PTID(IDXX)
      *
                   MOVE    "tbl_btpara"    TO  MCP-TABLE
                   MOVE    "key5"          TO  MCP-PATHNAME
                   PERFORM 900-BTPARA-READ-N-SEC
               END-PERFORM
      *
               MOVE    "tbl_btpara"        TO  MCP-TABLE
               MOVE    "key5"              TO  MCP-PATHNAME
               MOVE    "DBCLOSECURSOR"     TO  MCP-FUNC
               CALL    "ORCDBMAIN"         USING
                                           MCPAREA
                                           MCPDATA-REC
                                           SPA-AREA
      *
               IF      IDXX            =   ZERO
                   MOVE    ZERO            TO  WRK-PARA-SYORIFLG
               END-IF
           END-IF 
           .
       1001-PARA-HENSYU-EXT.
           EXIT.
      *
      *****************************************************************
      *    主　　処理
      *****************************************************************
       200-MAIN-SEC  SECTION.
      *
           OPEN     OUTPUT  MF100-FILE
           CLOSE            MF100-FILE
           OPEN     I-O     MF100-FILE
      *
           MOVE     ZERO    TO      FLG-END
      *
           PERFORM  2001-SYUKEI-SEC
                    UNTIL   FLG-END   =   1
      *
           CLOSE   MF100-FILE
      *
           IF       CNT-RECE02   >       0
                    PERFORM   2002-PRINT-SEC
           END-IF
           MOVE   1   TO   FLG-END
      *
           .
       200-MAIN-EXT.
           EXIT.
      *
      *****************************************************************
      *    集計処理
      *****************************************************************
       2001-SYUKEI-SEC  SECTION.
      *
           PERFORM  VARYING  IDZ  FROM  1  BY  1
                    UNTIL    IDZ  >     4
             IF  RECE02-KOHNUM (IDZ) =  "190" OR "290" OR "390"
                                     OR "191" OR "291" OR "391"
                                     OR "192" OR "292" OR "392"
               PERFORM 20011-SYUKEI-SEC
             END-IF
           END-PERFORM
      *
           PERFORM  900-KOHSKY-READ-SEC
      *
           .
       2001-SYUKEI-EXT.
           EXIT.
      **
      *****************************************************************
      *    集計処理２
      *****************************************************************
       20011-SYUKEI-SEC  SECTION.
      *
      *    TMP-FILE キーセット
      *
           INITIALIZE  MF100-REC
      *
           MOVE    SPA-HOSPNUM             TO  MF100-HOSPNUM
           MOVE    RECE02-PTNUM            TO  MF100-PTNUM
           MOVE    RECE02-RECEKA           TO  MF100-SRYKA
           MOVE    RECE02-HKNJANUM-KEY     TO  MF100-HKNJANUM-KEY
           MOVE    RECE02-TEKSTYMD         TO  MF100-TEKSTYMD
           MOVE    RECE02-NYUGAIKBN        TO  MF100-NYUGAIKBN
           MOVE    RECE02-SRYYM            TO  MF100-SRYYM
           MOVE    RECE02-KIGO             TO  MF100-KIGO-KEY
           MOVE    RECE02-NUM              TO  MF100-NUM-KEY
      * 
           MOVE    RECE02-PTID             TO  MF100-PTID
           MOVE    RECE02-NAME             TO  MF100-NAME
           MOVE    RECE02-KANANAME         TO  MF100-KANANAME
           MOVE    RECE02-BIRTHDAY         TO  MF100-BIRTHDAY
           MOVE    RECE02-SEX              TO  MF100-SEX
           MOVE    RECE02-AGE              TO  MF100-AGE
           MOVE    RECE02-HKN              TO  MF100-HKN
           MOVE    RECE02-RJNHKN           TO  MF100-RJNHKN
           MOVE    RECE02-SHOKUJIFTNKBN    TO  MF100-SYOTOKU
           MOVE    RECE02-RECESYUBETU      TO  MF100-RECESYUBETU
      *
           MOVE    RECE02-KOHINF(IDZ)      TO  MF100-KOHINF
           MOVE    MF100-KOHFTNJANUM       TO  MF100-FTNJANUM
           MOVE    MF100-KOHJKYSNUM        TO  MF100-JKYSNUM
           MOVE    MF100-FTNJANUM(5:3)     TO  MF100-CITYNUM
           COMPUTE IDY     =   IDZ +   1
           MOVE    RECE02-JNISSU(IDY)      TO  MF100-JNISSU
           MOVE    RECE02-TOTALTEN(IDY)    TO  MF100-TOTALTEN
           MOVE    RECE02-FTNMONEY(IDY)    TO  MF100-FTNMONEY
      *
      *    経過措置判定のため
           MOVE    RECE02-FTNMONEY(1)    TO  MF100-KEIKAITBFTN
      *
      *    広島市でまとめる(負担者番号は区毎)
           IF      MF100-CITYNUM(1:1)      =   "4"
               MOVE    "400"               TO  MF100-CITYNUM
           END-IF
      *
           MOVE     MF100-KOHNUM      TO    WRK-KOHNUM
           MOVE     MF100-KOHID       TO    WRK-KOHID
           MOVE     ZERO              TO    WRK-SEIKYUGK-G
      *
      *    患者に負担金が発生する公費は助成判定を行う
           MOVE    ZERO        TO  FLG-ERR
      *
           MOVE     ZERO           TO  FLG-TYOKI
      *
           IF      RECE02-NYUGAIKBN    =   "1"
               MOVE    "key33"                 TO  WRK-PATH
           ELSE
               MOVE    "key25"                 TO  WRK-PATH
           END-IF
      *
           PERFORM  920-SYUNOU-SET-SEC
      *
           IF   RECE02-FLG(2)      =  1
           AND  MF100-T-KOHNUM     =  "051"
      *      公費の上限額を取得
             PERFORM  950-PTKOHFTN-READ-SEC
      *      上限額を超えているかどうか
             IF  MF100-T-FTNMONEY  >  WRK-JGNGK
               MOVE  WRK-JGNGK         TO  MF100-T-FTNMONEY
             END-IF
           END-IF
      *
           IF      MF100-T-KOHNUM      NOT = SPACE
               PERFORM 930-PTKOHINF-SET-SEC
           END-IF
      *
           COMPUTE  WRK-SEIKYUGK-G2  ROUNDED  =  WRK-SEIKYUGK-G 
      *
      *    福祉点数と公費点数が同じ場合、公費点数は記載しない
           IF      MF100-TOTALTEN      =   MF100-T-TOTALTEN
               MOVE    ZERO            TO  MF100-T-TOTALTEN
           END-IF
      *    保険限度額設定
           IF      RECE02-FTNMONEY(1)  >   ZERO  AND
                   RECE02-FTNMONEY(1)  <   WRK-SEIKYUGK-G2
               MOVE    RECE02-FTNMONEY(1)
                                       TO  WRK-SEIKYUGK-G2
                                           MF100-ETC-FTNMONEY
           END-IF
      *    長期編集
           IF       FLG-TYOKI         =     1 
             MOVE  1                  TO    MF100-TYOKI
      *
      *    老人のみ長期併用時の金額を記載する
             IF  MF100-RJNHKNNUM   NOT  =  SPACE
               IF  WRK-SEIKYUGK-G2      >     10000
                 MOVE  10000            TO    MF100-ETC-FTNMONEY
               ELSE
                 IF  MF100-ETC-FTNMONEY   >     10000
                   MOVE  10000            TO    MF100-ETC-FTNMONEY
                 ELSE
                   MOVE  WRK-SEIKYUGK-G2  TO    MF100-ETC-FTNMONEY
                 END-IF
               END-IF
             END-IF
           END-IF
      *    長期編集（長期上位）
           IF       FLG-TYOKI         =     2
             MOVE  2                  TO    MF100-TYOKI
      *
      *    老人のみ長期併用時の金額を記載する
             IF  MF100-RJNHKNNUM   NOT  =  SPACE
               IF  WRK-SEIKYUGK-G2      >     20000
                 MOVE  20000            TO    MF100-ETC-FTNMONEY
               ELSE
                 IF  MF100-ETC-FTNMONEY   >     20000
                   MOVE  20000            TO    MF100-ETC-FTNMONEY
                 ELSE
                   MOVE  WRK-SEIKYUGK-G2  TO    MF100-ETC-FTNMONEY
                 END-IF
               END-IF
             END-IF
           END-IF
      *
           IF  RECE02-ZAITAKU  NOT =  ZERO
      *      経過措置チェック
             PERFORM  900-PTKOHINF-READ-SEC
           END-IF
      *    保険者番号チェック処理
           PERFORM  200111-HKNJANUM-CHK-SEC
      *
      *    訪問看護チェック処理
           PERFORM  200112-HOUMON-CHK-SEC
      *
      *    高半チェック処理
      *   （公費請求から取得したいが不具合があり請求管理より取得）
           PERFORM  900-SEIKYU-SELECT-SEC
           IF  RECE10-FLG (3)  =  "1" OR "2" OR "3"
               MOVE  1           TO  MF100-KOUHAN
           END-IF
      *
           IF      FLG-ERR            =     ZERO
             WRITE    MF100-REC 
             ADD      1                 TO    CNT-MF100
             DISPLAY "ETC-FTNMONEY=" MF100-ETC-FTNMONEY
           ELSE
             DISPLAY "助成対象外=" MF100-PTNUM
                     "点数="       MF100-TOTALTEN
                     "負担金="     MF100-FTNMONEY
           END-IF
      *
           .
       20011-SYUKEI-EXT.
           EXIT.
      *
      *****************************************************************
      *    個別発行時の保険者番号(市町村番号)チェック処理
      *****************************************************************
       200111-HKNJANUM-CHK-SEC                SECTION.
      *
           INITIALIZE      WRK-HKNJANUM
           MOVE    "34"                TO  WRK-HKNJANUM(1:2)
           MOVE    MF100-FTNJANUM(5:3) TO  WRK-HKNJANUM(3:3)
      *
           IF      WRK-PARA-SYORIFLG   =   "1"
               AND FLG-SYORI           =    1
               AND FLG-ERR             =   ZERO
      *
               DISPLAY "チェック用保険者番号="  WRK-HKNJANUM
               MOVE    1       TO      FLG-ERR
               PERFORM VARYING IDXX    FROM    1   BY  1
                       UNTIL   IDXX    >   WRK-CONS-PARA-MAX
                       OR      TBL-PARA-HKNJANUM(IDXX)  =   SPACE
                   IF      WRK-HKNJANUM(1:5)
                                   =   TBL-PARA-HKNJANUM(IDXX)(1:5)
                       MOVE    ZERO                TO  FLG-ERR
                       MOVE    WRK-CONS-PARA-MAX   TO  IDXX
                   END-IF
               END-PERFORM 
           END-IF
      *
           .
       200111-HKNJANUM-CHK-EXT.
           EXIT.
      *****************************************************************
      *    訪問看護チェック処理（SRYACTには該当コードが複数でも１つのみ）
      *****************************************************************
       200112-HOUMON-CHK-SEC                SECTION.
      *
           INITIALIZE  SRYACCT-REC
           INITIALIZE  WRK-HOUMON-TBL
           MOVE  SPA-HOSPNUM           TO  ACCT-HOSPNUM
           MOVE  RECE02-NYUGAIKBN      TO  ACCT-NYUGAIKBN
           MOVE  RECE02-PTID           TO  ACCT-PTID
           MOVE  RECE02-SRYYM          TO  ACCT-SRYYM
           MOVE  SRYACCT-REC           TO  MCPDATA-REC
           MOVE  "DBSELECT"            TO  MCP-FUNC
           MOVE  "tbl_sryacct"         TO  MCP-TABLE
           MOVE  "key7"                TO  MCP-PATHNAME
           CALL  "ORCDBMAIN"           USING
                                       MCPAREA
                                       MCPDATA-REC
                                       SPA-AREA
           IF      MCP-RC              =   ZERO
      *
               PERFORM  945-SRYACCT-READ-SEC
      *
               PERFORM  UNTIL  FLG-SRYACCT  =  1
      *    会計照会から取得
                 IF  ACCT-SRYCDTOTAL  =  "00000114004510"
                 OR  ACCT-SRYCDTOTAL  =  "00000114004610"
                 OR  ACCT-SRYCDTOTAL  =  "00000114010610"
                 OR  ACCT-SRYCDTOTAL  =  "00000114010710"
                   PERFORM  VARYING  IDX1  FROM  1  BY  1
                            UNTIL    IDX1  >    31
                     IF  ACCT-DAY (1 IDX1)  >  0
                       IF  WRK-HOUMON-DAY (IDX1)  =  0
                         ADD   1  TO  MF100-HOUMON
                         MOVE  1  TO  WRK-HOUMON-DAY(IDX1)
                       END-IF
                     END-IF
                   END-PERFORM
                 END-IF
      *
                 PERFORM  945-SRYACCT-READ-SEC
               END-PERFORM
           ELSE
               INITIALIZE                  SRYACCT-REC
               MOVE    1               TO  FLG-SRYACCT
           END-IF
      *
           MOVE  "DBCLOSECURSOR"         TO  MCP-FUNC
           MOVE  "tbl_sryacct"         TO  MCP-TABLE
           MOVE  "key7"                TO  MCP-PATHNAME
           CALL  "ORCDBMAIN"           USING
                                       MCPAREA
                                       MCPDATA-REC
                                       SPA-AREA
      *
           .
       200112-HOUMON-CHK-EXT.
           EXIT.
      *
      *****************************************************************
      *    帳票ファイル出力処理
      *****************************************************************
       2002-PRINT-SEC                SECTION.
      *
      *
           OPEN     INPUT       MF100-FILE
      *
           MOVE     ZERO        TO       FLG-PRINT
      *
      *    一時ファイル読込
           PERFORM  900-TMP-READ-SEC
      *
           PERFORM  UNTIL     FLG-PRINT   =  1
      *
      *    帳票編集<見出し>処理
             PERFORM  310-HEAD-HEN-SEC
      *    帳票編集<明細>処理
             PERFORM  320-BODY-HEN-SEC
      *    帳票印刷処理
             PERFORM  330-PRINT-OUT-SEC
      *    一時ファイル読込
             PERFORM  900-TMP-READ-SEC
           END-PERFORM
      *
           CLOSE    MF100-FILE
      *
           .
       2002-PRINT-EXT.
           EXIT.
      *
      *****************************************************************
      *    帳票編集<ヘッダー部>処理
      *****************************************************************
       310-HEAD-HEN-SEC          SECTION.
      *
      *
           ADD     1   TO  CNT-PAGE
      *
           INITIALIZE  SC03
      *
      *    カルテＮｏ(患者番号)
           MOVE    MF100-PTNUM             TO  SC03-PTNUM
      *
      *    本人家族区分
           EVALUATE    MF100-RECESYUBETU(4:1)
               WHEN    "1"
                   MOVE    "◯"            TO  SC03-HONKZKKBN(1)
               WHEN    "2"
                   MOVE    "◯"            TO  SC03-HONKZKKBN(2)
               WHEN    "3"
                   MOVE    "◯"            TO  SC03-HONKZKKBN(3)
               WHEN    "4"
                   MOVE    "◯"            TO  SC03-HONKZKKBN(4)
               WHEN    "5"
                   MOVE    "◯"            TO  SC03-HONKZKKBN(5)
               WHEN    "6"
                   MOVE    "◯"            TO  SC03-HONKZKKBN(6)
               WHEN    "7"
                   MOVE    "◯"            TO  SC03-HONKZKKBN(7)
               WHEN    "8"
                   MOVE    "◯"            TO  SC03-HONKZKKBN(8)
               WHEN    "9"
                   MOVE    "◯"            TO  SC03-HONKZKKBN(9)
               WHEN    "0"
                   MOVE    "◯"            TO  SC03-HONKZKKBN(10)
           END-EVALUATE
      *
      *    請求日付
           MOVE    WRK-SEIYMDWH            TO  SC03-SEIYMD
      *
      *    診療年月
           MOVE    MF100-SRYYM             TO  WRK-WRKYM
           MOVE    MF100-SRYYM             TO  WRK-SYMD (1:6)
           MOVE    "01"                    TO  WRK-SYMD (7:2)
           PERFORM 31012-SEIWA-HEN-SEC
           MOVE    WRK-HENYMDG             TO  SC03-SRYYM
      *    
      *    公費区分
           EVALUATE  MF100-KOHNUM
               WHEN "190"
               WHEN "290"
               WHEN "390"
                   MOVE  "○"              TO  SC03-KOHKBN(1)
               WHEN "191"
               WHEN "291"
               WHEN "391"
                   MOVE  "○"              TO  SC03-KOHKBN(2)
               WHEN "192"
               WHEN "292"
               WHEN "392"
                   MOVE  "○"              TO  SC03-KOHKBN(3)
           END-EVALUATE
      *
      *    市町村名
           MOVE    "34"                    TO  WRK-HKNJANUM(1:2)
           MOVE    MF100-CITYNUM           TO  WRK-HKNJANUM(3:3)
           PERFORM 920-HKNJAINF-SET-SEC
           STRING  HKNJA-HKNJANAME         DELIMITED   BY  SPACE
                   "市町村長　殿"          DELIMITED   BY  SIZE
                                           INTO    SC03-CITYNAME
           END-STRING
      *
      *    医療機関コード
           MOVE    SYS-1001-HOSPCDN        TO  SC03-HOSPCDN
      *
      *    住所
           MOVE    WRK-HOSPADRS            TO  SC03-ADRS
      *
      *    医療機関名
           MOVE    WRK-HOSPNAME            TO  SC03-HOSPNAME
      *
      *    管理者名
           MOVE    SYS-1001-KAISETUNAME    TO  SC03-KAISETU
      *
           .
       310-HEAD-HEN-EXT.
           EXIT.
      *
      *****************************************************************
      *    帳票編集<明細行>処理
      *****************************************************************
       320-BODY-HEN-SEC      SECTION.
      *
      *    老人保健負担者番号
           MOVE    MF100-RJNFTNJANUM       TO  WRK-MAE-INPUT
           PERFORM 2009-HENKAN-SEC
           MOVE    WRK-OUT-INPUT(9:8)      TO  SC03-RJNFTNJANUM-TBL
      *
      *    老人保健受給者番号
           MOVE    MF100-RJNJKYSNUM        TO  WRK-MAE-INPUT
           PERFORM 2009-HENKAN-SEC
           MOVE    WRK-OUT-INPUT           TO  SC03-RJNJKYSNUM-TBL
      *
      *    他公費負担者番号
           MOVE    MF100-T-FTNJANUM        TO  WRK-MAE-INPUT
           PERFORM 2009-HENKAN-SEC
           MOVE    WRK-OUT-INPUT(1:4)      TO  SC03-FTNJANUM2-TBL(1)
           MOVE    WRK-OUT-INPUT(9:8)      TO  SC03-FTNJANUM4-TBL(1)
      *
      *    他公費受給者番号
           MOVE    MF100-T-JKYSNUM         TO  WRK-MAE-INPUT
           PERFORM 2009-HENKAN-SEC
           MOVE    WRK-OUT-INPUT           TO  SC03-JKYSNUM-TBL(1)
      *
      *    福祉負担者番号
           MOVE    MF100-FTNJANUM          TO  WRK-MAE-INPUT
           PERFORM 2009-HENKAN-SEC
           MOVE    WRK-OUT-INPUT(1:4)      TO  SC03-FTNJANUM2-TBL(2)
           MOVE    WRK-OUT-INPUT(9:8)      TO  SC03-FTNJANUM4-TBL(2)
      *
      *    福祉受給者番号
           MOVE    MF100-JKYSNUM           TO  WRK-MAE-INPUT
           PERFORM 2009-HENKAN-SEC
           MOVE    WRK-OUT-INPUT           TO  SC03-JKYSNUM-TBL(2)
      *
      *    被保険者証記号
           MOVE    MF100-KIGO              TO  SC03-KIGO
      *
      *    被保険者証番号
           MOVE    MF100-NUM               TO  SC03-NUM 
      *
      *    氏名
           MOVE    MF100-NAME              TO  SC03-NAME
      *
      *    性別
           IF      MF100-SEX               =   1
               MOVE    "○"                TO  SC03-SEX(1)
           ELSE
               MOVE    "○"                TO  SC03-SEX(2)
           END-IF 
      *
      *    生年月日
           MOVE    MF100-BIRTHDAY          TO  WRK-SYMD
           PERFORM 31012-SEIWA-HEN-SEC
           EVALUATE  WRK-HENYMDG1(1:1)
               WHEN    "M"
                   MOVE    "○"            TO  SC03-GENGO(1)
               WHEN    "T"
                   MOVE    "○"            TO  SC03-GENGO(2)
               WHEN    "S"
                   MOVE    "○"            TO  SC03-GENGO(3)
               WHEN    "H"
                   MOVE    "○"            TO  SC03-GENGO(4)
           END-EVALUATE
           MOVE    WRK-HENYMDG(5:4)        TO  SC03-BIRTHDAYY
           MOVE    WRK-HENYMDG(11:4)       TO  SC03-BIRTHDAYM
           MOVE    WRK-HENYMDG(17:4)       TO  SC03-BIRTHDAYD 
      * 
      *    保険者番号
           MOVE    MF100-HKNJANUM          TO  WRK-MAE-INPUT
           PERFORM 2009-HENKAN-SEC
           IF  MF100-HKNNUM  =  "001"
               MOVE    WRK-OUT-INPUT(1:8)  TO  SC03-HKNJANUM-TBL(9:8)
           ELSE
               MOVE    WRK-OUT-INPUT       TO  SC03-HKNJANUM-TBL 
           END-IF
      *
      *    給付割合
           IF      MF100-RJNHKNNUM     NOT =   SPACE
               OR  MF100-AGE               >=  70
               IF      MF100-KYURATE           =  10
                   IF      MF100-NYUGAIKBN     =  "1"
                       EVALUATE    MF100-SYOTOKU
                           WHEN    "1"
                           WHEN    "2"
                               MOVE    6       TO      IDX
                           WHEN    "3"
                               MOVE    5       TO      IDX
                           WHEN    OTHER
                               MOVE    7       TO      IDX
                       END-EVALUATE
                   ELSE
                       MOVE    8       TO      IDX
                   END-IF
               ELSE
                   MOVE    4       TO      IDX
               END-IF
               IF      MF100-RJNHKNNUM     NOT =   SPACE
                   IF      MF100-SRYYM(1:6)    <=  "200209"
                       MOVE    14      TO      IDX  
                   ELSE
                       ADD     5       TO      IDX
                   END-IF
               END-IF
           ELSE
               EVALUATE  MF100-KYURATE
                   WHEN 10
                       MOVE    3       TO      IDX
                   WHEN 20
                       MOVE    2       TO      IDX
                   WHEN OTHER
                       MOVE    1       TO      IDX
               END-EVALUATE
           END-IF
           MOVE    "◯"                    TO  SC03-KYURATE(IDX)
      *
      *    日数
           MOVE    MF100-JNISSU           TO  WRK-Z2
           MOVE    WRK-Z2                 TO  SC03-NISSU
      *
      *    点数
           MOVE    MF100-TOTALTEN         TO  WRK-Z7
           MOVE    WRK-Z7                 TO  SC03-TENSU(1)
           MOVE    MF100-T-TOTALTEN       TO  WRK-Z7
           MOVE    WRK-Z7                 TO  SC03-TENSU(2)
      *
      *    一部負担金
           IF      MF100-KOHNUM    =   "190" OR "290" OR "390"
                                   OR  "191" OR "291" OR "391"
                                   OR  "192" OR "292" OR "392"
               IF      MF100-T-KOHNUM  =   SPACE
                   MOVE    MF100-FTNMONEY      TO  WRK-Z79
                   MOVE    WRK-Z79             TO  SC03-ITBFTN(2)
               ELSE
                   MOVE    MF100-T-FTNMONEY    TO  WRK-Z7
                   MOVE    WRK-Z7              TO  SC03-ITBFTN(2)
                   MOVE    MF100-FTNMONEY      TO  WRK-Z79
                   MOVE    WRK-Z79             TO  SC03-ITBFTN(3)
               END-IF
           ELSE
               MOVE    MF100-ETC-FTNMONEY   TO  WRK-Z7
               MOVE    WRK-Z7               TO  SC03-ITBFTN(1)
               MOVE    MF100-T-FTNMONEY     TO  WRK-Z7
               MOVE    WRK-Z7               TO  SC03-ITBFTN(2)
           END-IF
      *
      *    データがあれば保険−一部負担金欄印字（入院、在総診等）
           IF  MF100-KEIKAITBFTN  NOT =  ZERO
               MOVE  MF100-KEIKAITBFTN      TO  WRK-Z7
               MOVE  WRK-Z7                 TO  SC03-ITBFTN(1)
           END-IF
      *
      *    長期
           IF      MF100-TYOKI     =   1
               MOVE    "○"        TO  SC03-TYOKI
           END-IF
      *
      *    経過措置
           IF  MF100-KEIKA  =  1  AND  MF100-KEIKAITBFTN  >=  12000
               MOVE    "○"        TO  SC03-KEIKA
           END-IF
      *
      *    備考欄
           IF  MF100-TYOKI  =  2
               MOVE    "16"        TO  SC03-BIKO2
               MOVE    "○"        TO  SC03-BIKO3
           END-IF
           IF  MF100-HOUMON  >=  1
               MOVE  "訪問看護等"  TO  SC03-BIKO1
               MOVE  MF100-HOUMON  TO  WRK-Z2
               MOVE  WRK-Z2        TO  SC03-BIKO1(11:)
               MOVE  "日"          TO  SC03-BIKO1(13:)
           END-IF
      *
      *    高半
           IF  MF100-KOUHAN  =  1
               MOVE  "高半21"      TO  SC03-KOUHAN
               MOVE  "○"          TO  SC03-BIKO4
           END-IF
      *
           .
       320-BODY-HEN-EXT.
           EXIT.
      *
      *****************************************************************
      *    帳票印刷処理
      *****************************************************************
       330-PRINT-OUT-SEC                SECTION.
      *
      *
           INITIALIZE                  ORCSPRTAREA
           MOVE    "INS"               TO  SPRT-MODE
           MOVE    LNK-PRTKANRI-RENNUM TO  SPRT-RENNUM
           MOVE    LNK-PRTKANRI-TBL-KEY
                                       TO  SPRT-TBL-KEY
           MOVE    LNK-PRTKANRI-TBL-GROUP
                                       TO  SPRT-TBL-GROUP
           MOVE    LNK-PRTKANRI-SRYYM  TO  SPRT-SRYYM
           MOVE    LNK-PRTKANRI-SKYYMD TO  SPRT-SKYYMD
           MOVE    LNK-PRTKANRI-SHELLID 
                                       TO  SPRT-SHELLID
           MOVE    LNK-PRTKANRI-SHORI-RENNUM
                                       TO  SPRT-SHORI-RENNUM
           MOVE    LNK-PRTKANRI-PRIORITY
                                       TO  SPRT-PRIORITY
           IF  WRK-WRKYM   >=  WRK-CONS-SRYYM-200804
               MOVE   "SC3403.red"     TO  SPRT-PRTID
           ELSE
               MOVE   "SC3403o.red"    TO  SPRT-PRTID
           END-IF
           MOVE    "福祉医療費請求書"
                                       TO  SPRT-TITLE
           MOVE    SC03                TO  SPRT-PRTDATA
           MOVE    LNK-PRTKANRI-TERMID TO  SPRT-TERMID
           MOVE    LNK-PRTKANRI-OPID   TO  SPRT-OPID
           MOVE    LNK-PRTKANRI-PRTNM  TO  SPRT-PRTNM
           CALL    "ORCSPRT"           USING
                                       ORCSPRTAREA
                                       SPA-AREA
           IF      SPRT-RETURN         =   ZERO
               CONTINUE
           ELSE
               MOVE    "印刷ＤＢに更新できませんでした"
                                          TO  WRK-RECEERR
               PERFORM 500-ERR-HENSYU-SEC
           END-IF
           .
       330-PRINT-OUT-EXT.
           EXIT.
      *****************************************************************
      *    エラー出力処理
      *****************************************************************
       500-ERR-HENSYU-SEC  SECTION.
      *
           OPEN  INPUT  RECEERR-FILE
           IF  STS-RECEERR  =  ZERO
             CONTINUE
           ELSE
             OPEN  OUTPUT  RECEERR-FILE
      *
             MOVE  WRK-RECEERR  TO  RECEERR-REC
             WRITE RECEERR-REC
           END-IF
      *
           CLOSE  RECEERR-FILE
      *
           .
       500-ERR-HENSYU-EXT.
           EXIT.
      *
      *****************************************************************
      *    終了　　処理
      *****************************************************************
       300-END-SEC   SECTION.
      *
      *
           CLOSE  MF100-FILE
           MOVE  CNT-PAGE  TO  WRK-PARA-PAGE
      *
           DISPLAY "*** SEIKYU3403 IN  "  CNT-RECE02
           DISPLAY "*** SEIKYU3403 OUT "  CNT-MF100
           DISPLAY "*** SEIKYU3403 END ***"
      *
           IF  CNT-MF100  =  0
             DISPLAY "DATA NOT FOUND. PRINT JOB CANCEL!!"
           END-IF
      *
      *    ステップ終了処理
           MOVE    "STE"           TO  SJOBKANRI-MODE
           INITIALIZE                  JOBKANRI-REC
           MOVE    SPA-HOSPNUM     TO  JOB-HOSPNUM
           MOVE    WRK-PARA-PAGE   TO  JOB-UPDCNT
           MOVE    WRK-PARA-JOBID  TO  JOB-JOBID
           MOVE    WRK-PARA-SHELLID
                                   TO  JOB-SHELLID
           CALL    "ORCSJOB"       USING
                                   ORCSJOBKANRIAREA
                                   JOBKANRI-REC
                                   SPA-AREA
      *
           PERFORM 900-DBDISCONNECT-SEC
           .
       300-END-EXT.
           EXIT.
      *
      *****************************************************************
      *    一時ファイル読込
      *****************************************************************
       900-TMP-READ-SEC                SECTION.
      *
           READ  MF100-FILE   NEXT
               AT  END
                   MOVE  1  TO  FLG-PRINT
           END-READ
           .
       900-TMP-READ-EXT.
           EXIT.
      *      
      *****************************************************************
      *    公費請求書マスタ読込
      *****************************************************************
       900-KOHSKY-START-SEC            SECTION.
      *
           INITIALIZE                  RECE02-REC
           MOVE    SPA-HOSPNUM         TO  RECE02-HOSPNUM
           MOVE    LNK-PRTKANRI-SRYYM  TO  RECE02-SKYYM
           MOVE    RECE02-REC          TO  MCPDATA-REC
      *
           MOVE    "DBSELECT"              TO  MCP-FUNC
           MOVE    "tbl_kohsky"        TO  MCP-TABLE
           MOVE    "key2"              TO  MCP-PATHNAME
           CALL    "ORCDBMAIN"         USING
                                       MCPAREA
                                       MCPDATA-REC
                                       SPA-AREA
           IF      MCP-RC              =   ZERO
               PERFORM 900-KOHSKY-READ-SEC
           ELSE
               MOVE    1                   TO  FLG-END
               PERFORM  900-KOHSKY-CLOSE-SEC
           END-IF
           .
       900-KOHSKY-START-EXT.
           EXIT.
      *      
      *****************************************************************
      *    公費請求書マスタＲＥＡＤ
      *****************************************************************
       900-KOHSKY-READ-SEC         SECTION.
      *
           MOVE    "DBFETCH"               TO  MCP-FUNC
           MOVE    "tbl_kohsky"        TO  MCP-TABLE
           MOVE    "key2"              TO  MCP-PATHNAME
           CALL    "ORCDBMAIN"         USING
                                       MCPAREA
                                       MCPDATA-REC
                                       SPA-AREA
           IF      MCP-RC              =   ZERO
               MOVE    ZERO            TO  FLG-END
               MOVE    MCPDATA-REC     TO  RECE02-REC
               IF  RECE02-KOHNUM (1)  =  "190" OR "290" OR "390"
                                      OR "191" OR "291" OR "391"
                                      OR "192" OR "292" OR "392" OR
                   RECE02-KOHNUM (2)  =  "190" OR "290" OR "390"
                                      OR "191" OR "291" OR "391"
                                      OR "192" OR "292" OR "392" OR
                   RECE02-KOHNUM (3)  =  "190" OR "290" OR "390"
                                      OR "191" OR "291" OR "391"
                                      OR "192" OR "292" OR "392" OR
                   RECE02-KOHNUM (4)  =  "190" OR "290" OR "390"
                                      OR "191" OR "291" OR "391"
                                      OR "192" OR "292" OR "392"
      *        テスト患者のときは読み飛ばす
                   MOVE    SPACE               TO  PTINF-REC
                   INITIALIZE                      PTINF-REC
                   MOVE    SPA-HOSPNUM         TO  PTINF-HOSPNUM
                   MOVE    RECE02-PTID         TO  PTINF-PTID
                   MOVE    PTINF-REC           TO  MCPDATA-REC
                   PERFORM 800-PTINF-READ-SEC
                   IF      FLG-PTINF           =   ZERO
                   AND     PTINF-TSTPTNUMKBN   =   "1"
                       GO  TO  900-KOHSKY-READ-SEC
                   END-IF   
      *        個別発行で患者番号指定の場合、対象判定を行う
                   MOVE  ZERO                TO  FLG-ERR
                   IF      WRK-PARA-SYORIFLG   =   "1"
                   AND     FLG-SYORI           =    2
                     MOVE  1                   TO  FLG-ERR
                     PERFORM VARYING IDXX    FROM    1   BY  1
                             UNTIL   IDXX    >   WRK-CONS-PARA-MAX
                             OR      TBL-PARA-PTID(IDXX) =   ZERO
                         IF      RECE02-PTID =   TBL-PARA-PTID(IDXX)
                           MOVE    ZERO                TO  FLG-ERR
                           MOVE    WRK-CONS-PARA-MAX   TO  IDXX
                         END-IF
                     END-PERFORM 
                   END-IF
      *
                   IF      FLG-ERR           =   1
                     GO  TO  900-KOHSKY-READ-SEC
                   END-IF
      *
                   ADD  1  TO  CNT-RECE02
               ELSE
                 GO  TO  900-KOHSKY-READ-SEC
               END-IF
           ELSE
               MOVE    1               TO  FLG-END
               PERFORM  900-KOHSKY-CLOSE-SEC
           END-IF
           .
       900-KOHSKY-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    公費請求書マスタCLOSE
      *****************************************************************
       900-KOHSKY-CLOSE-SEC  SECTION.
      *
           MOVE    "DBCLOSECURSOR"         TO  MCP-FUNC
           MOVE    "tbl_kohsky"          TO  MCP-TABLE
           MOVE    "key2"                TO  MCP-PATHNAME
           CALL    "ORCDBMAIN"           USING
                                         MCPAREA
                                         MCPDATA-REC
                                         SPA-AREA
      *
           .
       900-KOHSKY-CLOSE-EXT.
           EXIT.
      *
      *****************************************************************
      *    患者マスタ読込
      *****************************************************************
       800-PTINF-READ-SEC         SECTION.
      *
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "tbl_ptinf"           TO  MCP-TABLE
           MOVE    "key"                 TO  MCP-PATHNAME
           CALL    "ORCDBMAIN"           USING
                                         MCPAREA
                                         MCPDATA-REC
                                         SPA-AREA
           IF      MCP-RC              =   ZERO
               MOVE    "DBFETCH"           TO  MCP-FUNC
               MOVE    "tbl_ptinf"         TO  MCP-TABLE
               MOVE    "key"               TO  MCP-PATHNAME
               CALL    "ORCDBMAIN"         USING
                                           MCPAREA
                                           MCPDATA-REC
                                           SPA-AREA
               IF      MCP-RC              =   ZERO
                   MOVE    ZERO                TO  FLG-PTINF
                   MOVE    MCPDATA-REC         TO  PTINF-REC
               ELSE
                   MOVE    1                   TO  FLG-PTINF
                   INITIALIZE                  PTINF-REC
               END-IF
           ELSE
               MOVE    1                   TO  FLG-PTINF
               INITIALIZE                  PTINF-REC
           END-IF
      *
           MOVE    "DBCLOSECURSOR"         TO  MCP-FUNC
           MOVE    "tbl_ptinf"            TO  MCP-TABLE
           MOVE    "key"                  TO  MCP-PATHNAME
           CALL    "ORCDBMAIN"            USING
                                          MCPAREA
                                          MCPDATA-REC
                                          SPA-AREA
      *
           .
       800-PTINF-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    保険組合せマスター読み込み
      *****************************************************************
       910-HKNCOMBI-READ-SEC         SECTION.
      *
           INITIALIZE                      HKNCOMBI-REC
           MOVE    SPA-HOSPNUM             TO  COMB-HOSPNUM
           MOVE    SYU-PTID                TO  COMB-PTID
           MOVE    SYU-HKNCOMBINUM         TO  COMB-HKNCOMBINUM
           MOVE    HKNCOMBI-REC            TO  MCPDATA-REC
           MOVE    "DBSELECT"              TO  MCP-FUNC
           MOVE    "tbl_hkncombi"        TO  MCP-TABLE
           MOVE    "key"                 TO  MCP-PATHNAME
           CALL    "ORCDBMAIN"           USING
                                         MCPAREA
                                         MCPDATA-REC
                                         SPA-AREA
           IF      MCP-RC              =   ZERO
               MOVE    "DBFETCH"               TO  MCP-FUNC
               MOVE    "tbl_hkncombi"        TO  MCP-TABLE
               MOVE    "key"                 TO  MCP-PATHNAME
               CALL    "ORCDBMAIN"           USING
                                             MCPAREA
                                             MCPDATA-REC
                                             SPA-AREA
               IF      MCP-RC              =   ZERO
                   MOVE    MCPDATA-REC     TO  HKNCOMBI-REC
                   MOVE    ZERO            TO  FLG-HKNCOMBI
               ELSE
                   INITIALIZE                  HKNCOMBI-REC
                   MOVE    1               TO  FLG-HKNCOMBI
               END-IF
           ELSE
               INITIALIZE                  HKNCOMBI-REC
               MOVE    1               TO  FLG-HKNCOMBI
           END-IF
      *
           MOVE    "DBCLOSECURSOR"         TO  MCP-FUNC
           MOVE    "tbl_hkncombi"        TO  MCP-TABLE
           MOVE    "key"                 TO  MCP-PATHNAME
           CALL    "ORCDBMAIN"           USING
                                         MCPAREA
                                         MCPDATA-REC
                                         SPA-AREA
      *
           .
       910-HKNCOMBI-READ-EXT.
           EXIT.
      *****************************************************************
      *    請求点数集計処理
      *****************************************************************
       800-SEIKYU-SEC  SECTION.
      *
           IF  RECE02-RJNHKNNUM  =  SPACE
             IF  RECE02-FTNMONEY(1)  >  ZERO
               MOVE  SYU-SYUCOMPFTN         TO  WRK-SEIKYUGK
               ADD   SYU-SYUCOMPFTN         TO  MF100-ETC-FTNMONEY
             ELSE
               MOVE  SYU-SYUCOMPFTN-ENTANI  TO  WRK-SEIKYUGK
             END-IF
           ELSE
             IF  RECE02-FTNMONEY(1)  >  ZERO
               MOVE  SYU-KOH-COMPFTN(1)         TO  WRK-SEIKYUGK
               ADD   SYU-KOH-COMPFTN(1)         TO  MF100-ETC-FTNMONEY
             ELSE
               MOVE  SYU-KOH-COMPFTN-ENTANI(1)  TO  WRK-SEIKYUGK
             END-IF
           END-IF
      *
           PERFORM  VARYING  IDX  FROM  1  BY  1
                    UNTIL    IDX  >     4  OR
                    SYU-KOH-HKNNUM(IDX) =  SPACE
      *
             IF  SYU-KOH-HKNNUM(IDX)(1:1) =  "0" AND
                 SYU-KOH-HKNNUM(IDX) NOT  =  "027"
      *
      *    全国公費点数と全国公費一部負担金を集計する
      *    (一部負担金はレセプトに記載する場合のみ集計する)
               MOVE  SYU-KOH-HKNNUM(IDX)  TO  MF100-T-KOHNUM
               MOVE  COMB-KOHID(IDX)      TO  MF100-T-KOHID
               ADD  SYU-TOTAL-HKNTEN      TO  MF100-T-TOTALTEN
               IF (RECE02-FTNMONEY(1)      >  ZERO)  OR
                  (SYU-KOH-HKNNUM(IDX) NOT  =  "010" AND "011" AND
                                               "020" AND "021")
      *    特定疾患は負担割合が定められていないので、負担額を手動計算
                 IF  RECE02-FLG(2)         =  1
                 AND SYU-KOH-HKNNUM(IDX)   =  "051"
      *            保険点数の８割で計算する
                   COMPUTE WRK-KEISAN  =  SYU-TOTAL-HKNTEN * 10 * 0.2
                   ADD     WRK-KEISAN     TO  MF100-T-FTNMONEY
                 ELSE
                   ADD     SYU-KOH-COMPFTN(IDX)
                                            TO  MF100-T-FTNMONEY
                 END-IF
               END-IF
             END-IF
      *
      *    計算負担金に差が有る場合、地方公費の助成があったと判定する
             IF     FLG-ERR                 =  1
               IF   SYU-KOH-HKNNUM(IDX)     =  WRK-KOHNUM
                 IF     IDX                 =  1
                   IF  SYU-SYUCOMPFTN-ENTANI
                                        >  SYU-KOH-COMPFTN-ENTANI(IDX)
                     MOVE     ZERO         TO  FLG-ERR
                   END-IF
                 ELSE
                   IF  SYU-KOH-COMPFTN-ENTANI(IDX - 1)
                                        >  SYU-KOH-COMPFTN-ENTANI(IDX)
                     MOVE     ZERO         TO  FLG-ERR
                   END-IF
                 END-IF
               END-IF
             END-IF
      *
             IF  SYU-KOH-HKNNUM(IDX)  =   "972"
               MOVE     1            TO   FLG-TYOKI
             END-IF
             IF  SYU-KOH-HKNNUM(IDX)  =   "974"
               MOVE     2            TO   FLG-TYOKI
             END-IF
      *
           END-PERFORM
      *
           ADD     WRK-SEIKYUGK                     TO  WRK-SEIKYUGK-G
      *
           .
       800-SEIKYU-SEC-EXT.
           EXIT.
      *
      *****************************************************************
      *  収納情報取得
      *****************************************************************
       920-SYUNOU-SET-SEC  SECTION.
      *
           INITIALIZE  SYUNOU-REC
           MOVE    SPA-HOSPNUM         TO  SYU-HOSPNUM
           MOVE    RECE02-NYUGAIKBN    TO  SYU-NYUGAIKBN
           MOVE    RECE02-PTID         TO  SYU-PTID
           MOVE    RECE02-SRYYM(1:6)   TO  SYU-SRYYMD(1:6)
           MOVE    "__"                TO  SYU-SRYYMD(7:2)
           MOVE    WRK-KOHNUM          TO  SYU-KOH-HKNNUM(1)
                                           SYU-KOH-HKNNUM(2)
                                           SYU-KOH-HKNNUM(3)
                                           SYU-KOH-HKNNUM(4)
           MOVE    SYUNOU-REC          TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "tbl_syunou"       TO  MCP-TABLE
           MOVE    WRK-PATH           TO  MCP-PATHNAME
           CALL    "ORCDBMAIN"        USING
                                      MCPAREA
                                      MCPDATA-REC
                                      SPA-AREA
           IF  MCP-RC  =  ZERO
               MOVE  "tbl_syunou"       TO  MCP-TABLE
               MOVE  WRK-PATH           TO  MCP-PATHNAME
               PERFORM  925-SYUNOU-READ-SEC
      *
               PERFORM  UNTIL  FLG-SYUNOU  =  1
                 IF  SYU-DENPJTIKBN   NOT = "3"
                   IF    SYU-HKNCOMBINUM  =  RECE02-HKNCOMBI(1) OR
                                             RECE02-HKNCOMBI(2) OR
                                             RECE02-HKNCOMBI(3) OR
                                             RECE02-HKNCOMBI(4) OR
                                             RECE02-HKNCOMBI(5) OR
                                             RECE02-HKNCOMBI(6) OR
                                             RECE02-HKNCOMBI(7) OR
                                             RECE02-HKNCOMBI(8) OR
                                             RECE02-HKNCOMBI(9) OR
                                             RECE02-HKNCOMBI(10) 
                     PERFORM  910-HKNCOMBI-READ-SEC
                     IF  COMB-KOHID(1)    =  WRK-KOHID   OR
                         COMB-KOHID(2)    =  WRK-KOHID   OR
                         COMB-KOHID(3)    =  WRK-KOHID   OR
                         COMB-KOHID(4)    =  WRK-KOHID
                       PERFORM  800-SEIKYU-SEC
                     END-IF
                   END-IF
                 END-IF
      *
                 MOVE  "tbl_syunou"     TO  MCP-TABLE
                 MOVE  WRK-PATH         TO  MCP-PATHNAME
                 PERFORM   925-SYUNOU-READ-SEC
               END-PERFORM
           ELSE
               DISPLAY  "SYUNOU SELECT ERR"
           END-IF
      *
           INITIALIZE  MCPAREA
           MOVE    "DBCLOSECURSOR"         TO  MCP-FUNC
           MOVE  "tbl_syunou"         TO  MCP-TABLE
           MOVE  WRK-PATH             TO  MCP-PATHNAME
           CALL  "ORCDBMAIN"          USING
                                      MCPAREA
                                      MCPDATA-REC
                                      SPA-AREA
      *
           .
       920-SYUNOU-SET-EXT.
           EXIT.
      *
      *****************************************************************
      *    収納読込
      *****************************************************************
       925-SYUNOU-READ-SEC  SECTION.
      *
           MOVE    "DBFETCH"               TO  MCP-FUNC
           CALL  "ORCDBMAIN"    USING
                                MCPAREA
                                MCPDATA-REC
                                SPA-AREA
           IF      MCP-RC      =   ZERO
               MOVE    MCPDATA-REC     TO  SYUNOU-REC
               MOVE    ZERO            TO  FLG-SYUNOU
           ELSE
               INITIALIZE      SYUNOU-REC
               MOVE    1   TO  FLG-SYUNOU
           END-IF
      *
           .
       925-SYUNOU-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    患者公費情報SELECT
      *****************************************************************
       900-PTKOHINF-READ-SEC         SECTION.
      *
           INITIALIZE                             PTKOHINF-REC
           MOVE    SPA-HOSPNUM                TO  PTKOH-HOSPNUM
           MOVE    RECE02-PTID                TO  PTKOH-PTID
           MOVE    "DBSELECT"                 TO  MCP-FUNC
           MOVE  "tbl_ptkohinf"        TO  MCP-TABLE
           MOVE  "key2"                TO  MCP-PATHNAME
           CALL  "ORCDBMAIN"           USING
                                       MCPAREA
                                       MCPDATA-REC
                                       SPA-AREA
           IF      MCP-RC              =   ZERO
               PERFORM  900-PTKOHINF-FETCH-SEC
      *
               PERFORM  UNTIL  FLG-PTKOHINF  =  1
                 IF  PTKOH-KOHNUM  =  "978"  OR  "979"
                   IF   PTKOH-TEKSTYMD(1:6)  <=  RECE02-SRYYM
                   AND  PTKOH-TEKEDYMD(1:6)  >=  RECE02-SRYYM
                     MOVE  1     TO  MF100-KEIKA
                     MOVE  1     TO  FLG-PTKOHINF
                   END-IF
                 END-IF
                 PERFORM  900-PTKOHINF-FETCH-SEC
               END-PERFORM
           ELSE
               MOVE    1                   TO  FLG-PTKOHINF
               INITIALIZE                  PTKOHINF-REC
           END-IF
      *
           MOVE    "DBCLOSECURSOR"            TO  MCP-FUNC
           MOVE  "tbl_ptkohinf"        TO  MCP-TABLE
           MOVE  "key2"                TO  MCP-PATHNAME
           CALL  "ORCDBMAIN"           USING
                                       MCPAREA
                                       MCPDATA-REC
                                       SPA-AREA
      *
           .
       900-PTKOHINF-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    患者公費情報FETCH
      *****************************************************************
       900-PTKOHINF-FETCH-SEC         SECTION.
      *
           MOVE    "DBFETCH"                  TO  MCP-FUNC
           MOVE  "tbl_ptkohinf"        TO  MCP-TABLE
           MOVE  "key2"                TO  MCP-PATHNAME
           CALL  "ORCDBMAIN"           USING
                                       MCPAREA
                                       MCPDATA-REC
                                       SPA-AREA
           IF      MCP-RC              =   ZERO
               MOVE    ZERO                TO  FLG-PTKOHINF
               MOVE    MCPDATA-REC         TO  PTKOHINF-REC
           ELSE
               MOVE    1                   TO  FLG-PTKOHINF
               INITIALIZE                  PTKOHINF-REC
           END-IF
      *
           .
       900-PTKOHINF-FETCH-EXT.
           EXIT.
      *
      *****************************************************************
      *   保険者情報セット
      *****************************************************************
       920-HKNJAINF-SET-SEC  SECTION.
      *
           INITIALIZE  HKNJAINF-REC
           MOVE    SPA-HOSPNUM             TO  HKNJA-HOSPNUM
           MOVE    WRK-HKNJANUM            TO  HKNJA-HKNJANUM
           MOVE    HKNJAINF-REC            TO  MCPDATA-REC
           MOVE    "DBSELECT"              TO  MCP-FUNC
           MOVE  "tbl_hknjainf"     TO  MCP-TABLE
           MOVE  "key2"             TO  MCP-PATHNAME
           CALL  "ORCDBMAIN"        USING
                                    MCPAREA
                                    MCPDATA-REC
                                    SPA-AREA
           IF      MCP-RC  =   ZERO
               PERFORM  925-HKNJAINF-READ-SEC
           ELSE
               INITIALIZE  HKNJAINF-REC
           END-IF
      *
           INITIALIZE  MCPAREA
           MOVE    "DBCLOSECURSOR"         TO  MCP-FUNC
           MOVE  "tbl_hknjainf"     TO  MCP-TABLE
           MOVE  "key2"             TO  MCP-PATHNAME
           CALL  "ORCDBMAIN"        USING
                                    MCPAREA
                                    MCPDATA-REC
                                    SPA-AREA
      *
           .
       920-HKNJAINF-SET-EXT.
           EXIT.
      *
      *****************************************************************
      *    保険者情報読込
      *****************************************************************
       925-HKNJAINF-READ-SEC  SECTION.
      *
           MOVE  "DBFETCH"          TO  MCP-FUNC
           MOVE  "tbl_hknjainf"     TO  MCP-TABLE
           MOVE  "key2"             TO  MCP-PATHNAME
           CALL  "ORCDBMAIN"        USING
                                    MCPAREA
                                    MCPDATA-REC
                                    SPA-AREA
           IF      MCP-RC      =   ZERO
               MOVE    MCPDATA-REC         TO  HKNJAINF-REC
               MOVE    ZERO                TO  FLG-HKNJAINF
      *
      *    国保でない時は、読み直す
               IF      HKNJA-HKNNUM    NOT =   "060" AND "067"
                   GO  TO  925-HKNJAINF-READ-SEC
               END-IF
           ELSE
               INITIALIZE  HKNJAINF-REC
               MOVE    1   TO  FLG-HKNJAINF
           END-IF
      *
           .
       925-HKNJAINF-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    公費の負担者番号取得
      *****************************************************************
       930-PTKOHINF-SET-SEC  SECTION.
      *
           INITIALIZE  PTKOHINF-REC
           MOVE    SPA-HOSPNUM             TO  PTKOH-HOSPNUM
           MOVE    RECE02-PTID             TO  PTKOH-PTID
           MOVE    MF100-T-KOHID           TO  PTKOH-KOHID
      *
           MOVE    PTKOHINF-REC            TO  MCPDATA-REC
           MOVE    "DBSELECT"              TO  MCP-FUNC
           MOVE  "tbl_ptkohinf"     TO  MCP-TABLE
           MOVE  "key"              TO  MCP-PATHNAME
           CALL  "ORCDBMAIN"        USING
                                    MCPAREA
                                    MCPDATA-REC
                                    SPA-AREA
           IF  MCP-RC  =  ZERO
               MOVE  "tbl_ptkohinf"     TO  MCP-TABLE
               MOVE  "key"              TO  MCP-PATHNAME
               PERFORM 935-PTKOHINF-READ-SEC
               MOVE    PTKOH-FTNJANUM  TO  MF100-T-FTNJANUM
               MOVE    PTKOH-JKYSNUM   TO  MF100-T-JKYSNUM
           END-IF
      *
           INITIALIZE  MCPAREA
           MOVE    "DBCLOSECURSOR"         TO  MCP-FUNC
           MOVE  "tbl_ptkohinf"     TO  MCP-TABLE
           MOVE  "key"              TO  MCP-PATHNAME
           CALL  "ORCDBMAIN"        USING
                                    MCPAREA
                                    MCPDATA-REC
                                    SPA-AREA
      *
           .
       930-PTKOHINF-SET-EXT.
           EXIT.
      *
      *****************************************************************
      *    公費の負担者番号取得
      *****************************************************************
       935-PTKOHINF-READ-SEC  SECTION.
      *
           MOVE    "DBFETCH"               TO  MCP-FUNC
           CALL  "ORCDBMAIN"        USING
                                    MCPAREA
                                    MCPDATA-REC
                                    SPA-AREA
           IF  MCP-RC  =  ZERO
             MOVE  MCPDATA-REC  TO  PTKOHINF-REC
             MOVE  ZERO         TO  FLG-PTKOHINF
           ELSE
             INITIALIZE  PTKOHINF-REC
             MOVE  1  TO  FLG-PTKOHINF
           END-IF
      *
           .
       935-PTKOHINF-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    診療行為読込
      *****************************************************************
       945-SRYACCT-READ-SEC  SECTION.
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
           MOVE  "tbl_sryacct"        TO  MCP-TABLE
           MOVE  "key7"               TO  MCP-PATHNAME
           CALL  "ORCDBMAIN"          USING
                                      MCPAREA
                                      MCPDATA-REC
                                      SPA-AREA
           IF      MCP-RC      =   ZERO
               MOVE    MCPDATA-REC     TO  SRYACCT-REC
               MOVE    ZERO            TO  FLG-SRYACCT
           ELSE
               INITIALIZE                  SRYACCT-REC
               MOVE    1               TO  FLG-SRYACCT
           END-IF
      *
           .
       945-SRYACCT-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    患者公費負担情報読込処理
      *****************************************************************
       950-PTKOHFTN-READ-SEC         SECTION.
      *
           INITIALIZE                          PTKOHFTN-REC
      *
           MOVE    SPA-HOSPNUM             TO  PTKOHFTN-HOSPNUM
           MOVE    MF100-PTID              TO  PTKOHFTN-PTID
           MOVE    PTKOHFTN-REC            TO  MCPDATA-REC
           MOVE    "DBSELECT"              TO  MCP-FUNC
           MOVE    "tbl_ptkohftn"          TO  MCP-TABLE
           MOVE    "key2"                  TO  MCP-PATHNAME
           CALL    "ORCDBMAIN"             USING
                                           MCPAREA
                                           MCPDATA-REC
                                           SPA-AREA
           IF      MCP-RC              =   ZERO
               MOVE    "DBFETCH"           TO  MCP-FUNC
               MOVE    "tbl_ptkohftn"      TO  MCP-TABLE
               MOVE    "key2"              TO  MCP-PATHNAME
               CALL    "ORCDBMAIN"         USING
                                           MCPAREA
                                           MCPDATA-REC
                                           SPA-AREA
               IF      MCP-RC              =   ZERO
                   MOVE    MCPDATA-REC         TO  PTKOHFTN-REC
                   MOVE    ZERO                TO  FLG-PTKOHFTN
      *            上限額
                   IF  MF100-NYUGAIKBN  =  "1"
                     MOVE  PTKOHFTN-NYUJGNGAK    TO  WRK-JGNGK
                   ELSE
                     MOVE  PTKOHFTN-GAIJGNGAK    TO  WRK-JGNGK
                   END-IF
               ELSE
                   MOVE    1                   TO  FLG-PTKOHFTN
                   INITIALIZE                  PTKOHFTN-REC
               END-IF
           ELSE
               MOVE    1                   TO  FLG-PTKOHFTN
               INITIALIZE                  PTKOHFTN-REC
           END-IF
      *
           MOVE    "DBCLOSECURSOR"         TO  MCP-FUNC
           MOVE    "tbl_ptkohftn"          TO  MCP-TABLE
           MOVE    "key2"                  TO  MCP-PATHNAME
           CALL    "ORCDBMAIN"             USING
                                           MCPAREA
                                           MCPDATA-REC
                                           SPA-AREA
      *
           .
       950-PTKOHFTN-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    請求管理読込
      *****************************************************************
       900-SEIKYU-SELECT-SEC             SECTION.
      *
      *    請求管理ＤＢ検索
           INITIALIZE                  RECE10-REC
      *
           MOVE   RECE02-HOSPNUM       TO  RECE10-HOSPNUM
           MOVE   RECE02-SRYYM         TO  RECE10-SRYYM
           MOVE   RECE02-NYUGAIKBN     TO  RECE10-NYUGAIKBN
           MOVE   RECE02-PTID          TO  RECE10-PTID
           MOVE   RECE02-RECEKA        TO  RECE10-RECEKA
           MOVE   RECE02-TEISYUTUSAKI  TO  RECE10-TEISYUTUSAKI
           MOVE   RECE02-RECESYUBETU   TO  RECE10-RECESYUBETU
           MOVE   RECE02-HKNJANUM      TO  RECE10-HKNJANUM
           MOVE   RECE02-HOJOKBN-KEY   TO  RECE10-HOJOKBN-KEY
           MOVE   RECE02-TEKSTYMD      TO  RECE10-TEKSTYMD
      *
           MOVE    RECE10-REC          TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "tbl_seikyu"        TO  MCP-TABLE
           MOVE    "key"               TO  MCP-PATHNAME
           CALL    "ORCDBMAIN"             USING
                                           MCPAREA
                                           MCPDATA-REC
                                           SPA-AREA
           IF      MCP-RC              =   ZERO
               PERFORM 900-SEIKYU-READ-SEC
           ELSE
               MOVE    1               TO  FLG-RECE10
           END-IF
      *
           MOVE    "DBCLOSECURSOR"     TO  MCP-FUNC
           MOVE    "tbl_seikyu"        TO  MCP-TABLE
           MOVE    "key"               TO  MCP-PATHNAME
           CALL    "ORCDBMAIN"             USING
                                           MCPAREA
                                           MCPDATA-REC
                                           SPA-AREA
           .
       900-SEIKYU-SELECT-EXT.
           EXIT.
      *
      *****************************************************************
      *    請求マスター読込
      *****************************************************************
       900-SEIKYU-READ-SEC           SECTION.
      *
           MOVE    "tbl_seikyu"            TO  MCP-TABLE
           MOVE    "key"                   TO  MCP-PATHNAME
           MOVE    "DBFETCH"               TO  MCP-FUNC
           CALL    "ORCDBMAIN"             USING
                                           MCPAREA
                                           MCPDATA-REC
                                           SPA-AREA
           IF      MCP-RC              =   ZERO
               MOVE    ZERO                TO  FLG-RECE10
               MOVE    MCPDATA-REC         TO  RECE10-REC
           ELSE
               MOVE    1                   TO  FLG-RECE10
           END-IF
      *
           .
       900-SEIKYU-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    月末日算出処理
      *****************************************************************
       31012-LASTDAY-GET-SEC        SECTION.
      *
           INITIALIZE                      STS-AREA-DAY
           INITIALIZE                      LNK-DAY7-AREA
           MOVE    "71"                   TO  LNK-DAY7-IRAI
           MOVE    WRK-SYMD     TO  LNK-DAY7-YM
           CALL    "ORCSDAY"       USING   STS-AREA-DAY
                                                  LNK-DAY7-AREA
           MOVE LNK-DAY7-KOYOMI  TO  WRK-SYMD
           .
       31012-LASTDAY-GET-EXT.
           EXIT.
      *
      *****************************************************************
      *    管理マスタ読み込み
      *****************************************************************
       800-SYSKANRI-READ-SEC  SECTION.
      *
           MOVE    "DBSELECT"              TO  MCP-FUNC
           MOVE  "tbl_syskanri"        TO  MCP-TABLE
           MOVE  "key10"               TO  MCP-PATHNAME
           CALL  "ORCDBMAIN"           USING
                                       MCPAREA
                                       MCPDATA-REC
                                       SPA-AREA
           IF  MCP-RC  =  ZERO
               MOVE    "DBFETCH"               TO  MCP-FUNC
               MOVE  "tbl_syskanri"        TO  MCP-TABLE
               MOVE  "key10"               TO  MCP-PATHNAME
               CALL  "ORCDBMAIN"           USING
                                           MCPAREA
                                           MCPDATA-REC
                                           SPA-AREA
               IF  MCP-RC  =  ZERO
                   MOVE  ZERO  TO  FLG-SYSKANRI
               ELSE
                   MOVE  1  TO  FLG-SYSKANRI
               END-IF
           ELSE
               MOVE  1  TO  FLG-SYSKANRI
           END-IF
      *
           MOVE    "DBCLOSECURSOR"         TO  MCP-FUNC
           MOVE  "tbl_syskanri"           TO  MCP-TABLE
           MOVE  "key10"                  TO  MCP-PATHNAME
           CALL  "ORCDBMAIN"              USING
                                          MCPAREA
                                          MCPDATA-REC
                                          SPA-AREA
      *
           .
       800-SYSKANRI-READ-EXT.
           EXIT.
      *      
      *****************************************************************
      *    パラメタ読込
      *****************************************************************
       900-BTPARA-READ-N-SEC          SECTION.
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
           CALL  "ORCDBMAIN"           USING
                                       MCPAREA
                                       MCPDATA-REC
                                       SPA-AREA
           IF      MCP-RC              =   ZERO
               MOVE    MCPDATA-REC         TO  BTPARA-REC
               MOVE    ZERO                TO  FLG-BTPARA
           ELSE
               MOVE    1                   TO  FLG-BTPARA
               INITIALIZE                      BTPARA-REC
           END-IF
      *
           .
       900-BTPARA-READ-N-EXT.
           EXIT.
      *
      *****************************************************************
      *    全角変換処理
      *****************************************************************
       2009-HENKAN-SEC                 SECTION.
      *
           INITIALIZE                  WRK-OUT-INPUT
      *
           INITIALIZE                  ORCSKANACHKAREA
           MOVE    "2"                 TO  KANACHK-SYORI
           MOVE    WRK-MAE-INPUT       TO  KANACHK-MAE-INPUT
           CALL    "ORCSKANACHK"       USING
                                       ORCSKANACHKAREA
           IF      KANACHK-RC          =   ZERO
               MOVE    KANACHK-OUT-INPUT
                                           TO  WRK-OUT-INPUT
           ELSE
               MOVE    WRK-MAE-INPUT       TO  WRK-OUT-INPUT
           END-IF
      *
           .
       2009-HENKAN-EXT.
           EXIT.
      *****************************************************************
      *    西暦日本語変換処理
      *****************************************************************
       31012-SEIWA-HEN-SEC        SECTION.
      *
           INITIALIZE                      STS-AREA-DAY
           INITIALIZE                      LNK-DAY2-AREA
           MOVE    "21"                TO  LNK-DAY2-IRAI
           MOVE    WRK-SYMD            TO  LNK-DAY2-YMD
           CALL    "ORCSDAY"           USING   STS-AREA-DAY
                                               LNK-DAY2-AREA
           MOVE    LNK-DAY2-EDTYMD3    TO  WRK-HENYMDG
           MOVE    LNK-DAY2-EDTYMD1    TO  WRK-HENYMDG1
           INSPECT WRK-HENYMDG    REPLACING  ALL "  "  BY  "　"
           .
       31012-SEIWA-HEN-EXT.
           EXIT.
      *****************************************************************
      *    医療機関編集情報読み込み
      *****************************************************************
       900-1900-READ-SEC           SECTION.
      *
      *    医療機関編集情報読み込み
           MOVE    SPACE                   TO  SYS-1900-REC
           INITIALIZE                          SYS-1900-REC
           MOVE    "1900"                  TO  SYS-1900-KANRICD
           MOVE    "*"                     TO  SYS-1900-KBNCD
           MOVE    SPA-HOSPNUM             TO  SYS-1900-HOSPNUM
           MOVE    LNK-PRTKANRI-SRYYM      TO  SYS-1900-STYUKYMD(1:6)
           MOVE    "01"                    TO  SYS-1900-STYUKYMD(7:2)
           MOVE    LNK-PRTKANRI-SRYYM      TO  SYS-1900-EDYUKYMD(1:6)
           MOVE    "01"                    TO  SYS-1900-EDYUKYMD(7:2)
           MOVE    SYS-1900-REC            TO  MCPDATA-REC
           PERFORM 800-SYSKANRI-READ-SEC
           IF      FLG-SYSKANRI        =   ZERO
               MOVE    MCPDATA-REC         TO  SYS-1900-REC
               IF      SYS-1900-PRTKBN(8) NOT =   SPACE
      *            医療機関名称編集情報
                   PERFORM 900-1901-READ-SEC
      *            医療機関住所編集情報
                   PERFORM 900-1902-READ-SEC
               END-IF
           ELSE
               INITIALIZE                  SYS-1900-REC
           END-IF
           .
      *
       900-1900-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    医療機関名称編集情報読み込み
      *****************************************************************
       900-1901-READ-SEC           SECTION.
      *
      *    医療機関名称編集情報読み込み
           MOVE    SPACE               TO  SYS-1901-REC
           INITIALIZE                      SYS-1901-REC
           MOVE    "1901"              TO  SYS-1901-KANRICD
           MOVE    SYS-1900-PRTKBN(8)  TO  SYS-1901-KBNCD
           MOVE    SPA-HOSPNUM         TO  SYS-1901-HOSPNUM
           MOVE    LNK-PRTKANRI-SRYYM  TO  SYS-1901-STYUKYMD(1:6)
           MOVE    "01"                TO  SYS-1901-STYUKYMD(7:2)
           MOVE    LNK-PRTKANRI-SRYYM  TO  SYS-1901-EDYUKYMD(1:6)
           MOVE    "01"                TO  SYS-1901-EDYUKYMD(7:2)
           MOVE    SYS-1901-REC        TO  MCPDATA-REC
           PERFORM 800-SYSKANRI-READ-SEC
           IF      FLG-SYSKANRI        =   ZERO
               MOVE    MCPDATA-REC         TO  SYS-1901-REC
               MOVE    SYS-1901-HOSPNAME1  TO  WRK-HOSPNAME1
               MOVE    SYS-1901-HOSPNAME2  TO  WRK-HOSPNAME2
           ELSE
               INITIALIZE                  SYS-1901-REC
           END-IF
           .
      *
       900-1901-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    医療機関住所編集情報読み込み
      *****************************************************************
       900-1902-READ-SEC           SECTION.
      *
      *    医療機関住所編集情報読み込み
           MOVE    SPACE               TO  SYS-1902-REC
           INITIALIZE                      SYS-1902-REC
           MOVE    "1902"              TO  SYS-1902-KANRICD
           MOVE    SYS-1900-PRTKBN(8)  TO  SYS-1902-KBNCD
           MOVE    SPA-HOSPNUM         TO  SYS-1902-HOSPNUM
           MOVE    LNK-PRTKANRI-SRYYM  TO  SYS-1902-STYUKYMD(1:6)
           MOVE    "01"                TO  SYS-1902-STYUKYMD(7:2)
           MOVE    LNK-PRTKANRI-SRYYM  TO  SYS-1902-EDYUKYMD(1:6)
           MOVE    "01"                TO  SYS-1902-EDYUKYMD(7:2)
           MOVE    SYS-1902-REC        TO  MCPDATA-REC
           PERFORM 800-SYSKANRI-READ-SEC
           IF      FLG-SYSKANRI        =   ZERO
               MOVE    MCPDATA-REC     TO  SYS-1902-REC
               MOVE    SYS-1902-ADRS1  TO  WRK-HOSPADRS1
               MOVE    SYS-1902-ADRS2  TO  WRK-HOSPADRS2
               MOVE    SYS-1902-TEL    TO  WRK-HOSPTEL
           ELSE
               INITIALIZE                  SYS-1902-REC
           END-IF
           .
      *
       900-1902-READ-EXT.
           EXIT.
      *****************************************************************
      *    ＤＢ　オープン処理
      *****************************************************************
       100-DBOPEN-SEC  SECTION.
      *
           MOVE    LOW-VALUE           TO  MCP-TABLE
                                           MCP-PATHNAME
           MOVE    "DBOPEN"        TO  MCP-FUNC.
           CALL    "ORCDBMAIN"         USING
                                       MCPAREA
                                       MCPDATA-REC
                                       SPA-AREA
      *
           MOVE    LOW-VALUE           TO  MCP-TABLE
                                           MCP-PATHNAME
           MOVE    "DBSTART"       TO  MCP-FUNC.
           CALL    "ORCDBMAIN"         USING
                                       MCPAREA
                                       MCPDATA-REC
                                       SPA-AREA
           .
       100-DBOPEN-EXT.
           EXIT.
      *
      *****************************************************************
      *    ＤＢ　クローズ処理
      *****************************************************************
       900-DBDISCONNECT-SEC  SECTION.
      *
           MOVE    LOW-VALUE           TO  MCP-TABLE
                                           MCP-PATHNAME
           MOVE    "DBCOMMIT"      TO  MCP-FUNC.
           CALL    "ORCDBMAIN"         USING
                                       MCPAREA
                                       MCPDATA-REC
                                       SPA-AREA
      *
           MOVE    LOW-VALUE           TO  MCP-TABLE
                                           MCP-PATHNAME
           MOVE    "DBDISCONNECT"  TO  MCP-FUNC.
           CALL    "ORCDBMAIN"         USING
                                       MCPAREA
                                       MCPDATA-REC
                                       SPA-AREA
           .
       900-DBDISCONNECT-EXT.
           EXIT.
      *
