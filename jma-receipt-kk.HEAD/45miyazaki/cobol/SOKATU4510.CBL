      *******************************************************************
      * Project code name "ORCA"
      * 日医標準レセプトソフト（JMA standard receipt software）
      * Copyright(C) 2002 JMA (Japan Medical Association)
      *
      * This program is part of "JMA standard receipt software".
      *
      *     This program is distributed in the hope that it will be useful
      * for further advancement in medical care, according to JMA Open
      * Source License, but WITHOUT ANY WARRANTY.
      *     Everyone is granted permission to use, copy, modify and
      * redistribute this program, but only under the conditions described
      * in the JMA Open Source License. You should have received a copy of
      * this licence along with this program. If not, stop using this
      * program and contact JMA, 2-28-16 Honkomagome, Bunkyo-ku, Tokyo,
      * 113-8621, Japan.
      ********************************************************************
       IDENTIFICATION          DIVISION.
       PROGRAM-ID.             SOKATU4510.
      *****************************************************************
      *  システム名        : ＯＲＣＡ
      *  サブシステム名    : 月次帳票
      *  コンポーネント名  : 国保・請求書集計処理
      *  管理者            :
      *  作成日付    作業者        記述
      *  05/07/11    有村          再作成(2.5.0対応)
      *****************************************************************
      *  プログラム修正履歴
      * Maj/Min/Rev 	修正者	日付		内容
      *  01.00.01    有村    05/09/09    乳幼児対象公費番号追加(381)
      *  01.00.02    内田    06/11/18    食事がない場合でも生活療養費を印字
      *  01.00.03    内田    07/10/06    日レセver4.0対応
      *                                  システム管理マスタ「1901」の
      *                                  医療機関情報を参照
      *  01.00.04    内田    07/10/23    保険番号190の公費再掲欄は
      *                                  一部負担金を印字しない
      *  01.00.05    内田    08/05/03    帳票様式の変更に伴う修正
      *  01.00.06    吉川    08/08/21    保険は県内でも公費が県外の場合
      *                                  県外として集計する
      *  01.00.07    吉川    08/10/27    乳幼児対象公費番号追加(481,581)
      *                                  シス管「2007」公費まとめ対応
      *                                  SPA-AREA初期化
      *  01.00.08    吉川    09/10/28    乳幼児対象公費番号追加(681)
      *  01.00.09    吉川    10/01/07    シス管「2007」のまとめ公費で
      *                                  6つ目以降がまとまらない不具合を修正
      *  01.00.10    吉川    10/06/01    オンライン返戻対応
      *  01.00.11    吉川    11/02/10    特別療養費国保対応
      *                                  open-cobol1.0対応
      *  01.00.12    吉川    12/12/20    医療機関情報編集対応
      *  01.00.13    吉川    13/02/08    プラグイン対応
      *  01.00.14    吉川    13/06/18    プログラムオプション対応
      *  01.00.15    吉川    14/05/15    前期高齢者２割対応
      *  01.00.16    吉川    14/10/28    ver4.8.0対応（一時ディレクトリ変更）
      *  01.00.17    萩野    15/06/10    返戻時、再印刷画面のタイトル修正
      *  01.00.18    門間    17/06/06    医療機関編集情報への開設者名追加対応
      *                                  公費番号まとめ情報の公費番号の追加
      *                                  医療機関コード変更時、
      *                                  医療機関コード別の作成処理対応
      *  01.00.19    門間    17/11/01    法別番号の記載修正
      *                                  SITEKBN変更（動作に変更なし）
      *  01.00.20    門間    19/02/01    修正履歴コメント削除
      *                                  保険番号情報の取得処理を修正
      *****************************************************************
      *
       ENVIRONMENT                 DIVISION.
       CONFIGURATION               SECTION.
       INPUT-OUTPUT                SECTION.
       FILE-CONTROL.

      *    請求書一時ファイル
           SELECT  SKT4510TMP-FILE ASSIGN    SKT4510TP
                                   ORGANIZATION    IS  INDEXED
                                   ACCESS  MODE    IS  DYNAMIC
                                   RECORD  KEY     IS  SKT4510TMP-KEY
                                   FILE    STATUS  IS  STS-SKT4510.
      *
006900*    請求ＤＢ更新用ファイル
           SELECT  UPD-FILE        ASSIGN  RECEUPD
                                   ORGANIZATION    IS  LINE
                                                       SEQUENTIAL
                                   FILE    STATUS  IS  STS-RECEUPD.
      *
006900*    エラーファイル
           SELECT  RECEERR-FILE    ASSIGN  RECEERR
                                   FILE    STATUS  IS  STS-RECEERR.
      *
       DATA                        DIVISION.
       FILE                        SECTION.
      *
      *    請求書一時ファイル
       FD  SKT4510TMP-FILE.
           COPY    "CPSKT4510TMP.INC".
      *
006900*    請求ＤＢ更新用ファイル
       FD  UPD-FILE.
           COPY    "CPRCF010.INC"  REPLACING  //RECE10//
                                   BY         //RECE10X//.
      *
006900*    エラーファイル
       FD  RECEERR-FILE.
       01  RECEERR-REC             PIC X(200).
      *
       WORKING-STORAGE             SECTION.
      *
      *    請求書一時ファイル 名称領域
           COPY    "CPCOMMONDAT2.INC"
                                   REPLACING  //RECE01PARA//
                                   BY         //SKT4510TP//.
      *
      *
006900*    請求ＤＢ更新用ファイル 名称領域
           COPY    "CPCOMMONDAT2.INC"
                                   REPLACING  //RECE01PARA//
                                   BY         //RECEUPD//.
      *
      *    エラーファイル 名称領域
           COPY    "CPRECEERR.INC".
      *
      *    シェル用領域
           COPY    "CPCOMMONSHELL.INC".
      *
           COPY    "SKT4510.INC".
           COPY    "SKT4511.INC".
      *
      *    スパ領域
       01  STS-AREA.
           03  STS-SKT4510         PIC X(02).
           03  STS-RECEERR         PIC X(02).
           03  STS-RECEUPD         PIC X(02).
           03  STS-YUSEN           PIC X(02).
      *
      *    フラグ領域
       01  FLG-AREA.
           03  FLG-END             PIC 9(01).
           03  FLG-SKT4510TMP      PIC 9(01).
           03  FLG-RECE10          PIC 9(01).
           03  FLG-SYSKANRI        PIC 9(01).
           03  FLG-SYSKANRI3       PIC 9(01).
           03  FLG-HKNNUM          PIC 9(01).
           03  FLG-HKNJAINF        PIC 9(01).
           03  FLG-UPD             PIC 9(01).
           03  FLG-UPDFILE         PIC 9(01).
           03  FLG-SEIKYU          PIC 9(01).
           03  FLG-PTINF           PIC 9(01).
           03  FLG-PRINT           PIC 9(01).
           03  FLG-PASS            PIC 9(01).
           03  FLG-PTKOHINF        PIC 9(01).
           03  FLG-TAKOH           PIC 9(01).
           03  FLG-PRGOPTION       PIC 9(01).
           03  FLG-OK              PIC 9(01).
      *
      *    添字領域
       01  IDX-AREA.
           03  IDX1                PIC 9(05).
           03  IDX2                PIC 9(04).
           03  IDX3                PIC 9(04).
           03  IDX4                PIC 9(04).
           03  IDX5                PIC 9(04).
           03  IDX-MAX             PIC 9(04).
           03  IDZ1                PIC 9(04).
           03  IDZ2                PIC 9(04).
           03  IDX                 PIC 9(04).
           03  IDY                 PIC 9(04).
           03  IDZ                 PIC 9(04).
      *
       01  WRK-PARA.
           COPY    "CPORCSPRTLNK.INC".
           03  WRK-PARA-JOBID      PIC 9(07).
           03  WRK-PARA-SHELLID    PIC X(08).
           03  WRK-PARA-HOSPNUM    PIC 9(02).
           03  WRK-PARA-HKNJANUM   PIC X(08).
           03  WRK-PARA-PAGE       PIC 9(10).
           03  WRK-PARA-SYOKBN     PIC X(01).
           03  WRK-PARA-TOKUBETU   PIC X(01).
           03  WRK-PARA-STSRYYM    PIC 9(06).
           03  WRK-PARA-EDSRYYM    PIC 9(06).
      *
      *    一時領域
       01  WRK-AREA.
           03  WRK-SEIYMD          PIC X(08).
           03  WRK-SEIYMDWH        PIC X(22).
           03  WRK-SRYYMWH         PIC X(16).
           03  WRK-SRYYMD          PIC X(08).
      *
           03  WRK-SYMD.
               05  WRK-SYY         PIC 9(04).
               05  WRK-SMM         PIC 9(02).
               05  WRK-SDD         PIC 9(02).
           03  WRK-HENYMDG         PIC X(22).
      *
           03  WRK-CNT             PIC  9(02).
           03  WRK-HKNJANUM        PIC  X(06).
           03  WRK-KOH-KEY.
               05  WRK-KOHNUM      PIC  X(03).
               05  WRK-FTNRATE     PIC  9(01).
           03  WRK-KENNAME         PIC  X(10).
           03  WRK-PTKOHNUM        PIC X(03).
           03  WRK-FTNJANUM        PIC X(08).
           03  WRK-KOHNAME         PIC X(16).
      *
           03  WRK-Z1              PIC Z.
           03  WRK-Z2              PIC ZZ.
           03  WRK-Z4              PIC ZZZZ.
           03  WRK-Z5              PIC Z,ZZZ.
           03  WRK-Z9              PIC Z,ZZZ,ZZZ.
      *
           03  WRK-RECEERR         PIC X(200).
      *    文字変換
           03  WRK-MAE-INPUT           PIC X(200).
           03  WRK-OUT-INPUT           PIC X(200).
      *
           03  WRK-PATH            PIC X(50).
           03  WRK-PATH2           PIC X(50).
      *
           03  WRK-OPTION-TBL.
               05  WRK-OPTION      PIC X(100)  OCCURS 100.
           03  WRK-PRGID           PIC X(25).
           03  WRK-KBNCD           PIC X(16).
           03  WRK-YUSEN           PIC X(06).
      *
      *    医療機関情報
           03  WRK-HOSPNAME        PIC X(100).
           03  WRK-HOSPNAME-R      REDEFINES   WRK-HOSPNAME.
               05  WRK-HOSPNAME1   PIC X(50).
               05  WRK-HOSPNAME2   PIC X(50).
           03  WRK-HOSPADRS        PIC X(100).
           03  WRK-HOSPADRS-R      REDEFINES   WRK-HOSPADRS.
               05  WRK-HOSPADRS1   PIC X(50).
               05  WRK-HOSPADRS2   PIC X(50).
           03  WRK-HOSPTEL         PIC X(15).
           03  WRK-KAISETUNAME     PIC X(80).
      *
      *公費ソート用エリア
       01  SORT-AREA.
           03  SORT-TBL.
               05  SORT-KOH-TBL  OCCURS  20.
                   07  SORT-KOHNUM-KEY.
                       09  SORT-KOHNUM          PIC  X(03).
                       09  SORT-KOH-FTNRATE     PIC  9(01).
                   07  SORT-KOH-NYUGAI-TBL OCCURS 2.
                       09  SORT-K-KENSU         PIC  9(05).
                       09  SORT-K-NISSU         PIC  9(05).
                       09  SORT-K-TENSU         PIC  9(09).
                       09  SORT-K-ITBFTN        PIC  9(09).
                   07  SORT-K-SKENSU            PIC  9(05).
                   07  SORT-K-SNISSU            PIC  9(05).
                   07  SORT-K-SKINGK            PIC  9(09).
                   07  SORT-K-SFTNGK            PIC  9(09).
      *
       01  WRK-2007-AREA.
           03  WRK-2007-TBL        OCCURS  100.
               05  WRK-2007-MEISYO PIC X(44).
               05  WRK-2007-KOH-AREA.
                   07  WRK-2007-KOHNUM
                                   PIC X(03)   OCCURS  30.
           03  WRK-2007-MAX        PIC 9(03).
      *
      *    カウント領域
       01  CNT-AREA.
           03  CNT-PAGE            PIC 9(03).
           03  CNT-RECE10          PIC 9(06).
           03  CNT-UPDFILE         PIC 9(06).
           03  CNT-IN              PIC 9(02).
           03  CNT-KOH             PIC 9(02).
      *
           COPY    "COMMON-SPA".
      *
      *    固定項目
       01  WRK-CONS-AREA.
           03  WRK-CONS-SRYYM-200804   PIC X(06)   VALUE   "200804".
      *
      *****************************************************************
      *    ファイルレイアウト
      *****************************************************************
      *
      *    患者情報
       01  PTINF-REC.
           COPY    "CPPTINF.INC".
      *
      *　　保険者情報
       01  HKNJAINF-REC.
           COPY    "CPHKNJAINF.INC".
      *
      *    保険番号
       01  HKNNUM-REC.
           COPY    "CPHKNNUM.INC".
      *
      *    患者公費情報
       01  PTKOHINF-REC.
           COPY    "CPPTKOHINF.INC".
      *
      *    医療機関情報情報
           COPY    "CPSK1001.INC".
      *
      *    医療機関情報−所在地、連絡先
           COPY    "CPSK1002.INC".
      *
      *    医療機関編集情報
           COPY    "CPSK1900.INC".
      *
      *    医療機関名称編集情報
           COPY    "CPSK1901.INC".
      *
      *    医療機関住所編集情報
           COPY    "CPSK1902.INC".
      *
      *    医療機関住所編集情報（２）
           COPY    "CPSK1903.INC".
      *
      *    公費番号まとめ情報
           COPY    "CPSK2007.INC".
      *
      *    請求管理情報
           COPY    "CPRCF010.INC".
      *
      *    ジョブ管理マスタ
       01  JOBKANRI-REC.
           COPY    "CPJOBKANRI.INC".
      *
      *    プログラムオプション
       01  PRGOPTION-REC.
           COPY    "CPPRGOPTION.INC".
      *
      *    ファイルディレクトリ位置指定サブ
           COPY    "CPMKPASS.INC".
      *
      *    全角チェックパラメタ
           COPY    "CPORCSKANACHK.INC".
      *
      ******************************************************************
      *    サブプロ用　領域
      *****************************************************************
      *
      *    ジョブ管理ＤＢ制御サブ
           COPY    "CPORCSJOBKANRI.INC".
      *
      *    印刷ＤＢ更新サブ
           COPY    "CPORCSPRT.INC".
      *
      *    日付変換サブ
           COPY    "CPORCSDAY.INC".
           COPY    "CPORCSLNK.INC".
      *
      *    共通パラメタ
           COPY    "MCPAREA".
      *
           COPY    "MCPDATA.INC".
      *
      *    一時ファイル名編集
           COPY    "CPORCSGETTEMP.INC".
      *
      ****************************************************************
       LINKAGE                 SECTION.
       01  COMMAND-PARAM.
           02  FILLER      PIC X(256).
      ****************************************************************
       PROCEDURE           DIVISION
               USING
           COMMAND-PARAM.
      *****************************************************************
      *    主　　処理
      *****************************************************************
       000-PROC-SEC                SECTION.
      *
           PERFORM 100-INIT-SEC
      *
           PERFORM 200-MAIN-SEC
                   UNTIL       FLG-END     =   1
      *
           PERFORM 300-END-SEC
      *
           STOP    RUN
           .
      *****************************************************************
      *    初期　処理
      *****************************************************************
       100-INIT-SEC                SECTION.
      *
           INITIALIZE                  FLG-AREA
           INITIALIZE                  STS-AREA
           INITIALIZE                  WRK-AREA
           INITIALIZE                  CNT-AREA
           INITIALIZE                  IDX-AREA
           INITIALIZE                  SPA-AREA
           INITIALIZE                  RECEERR
      *
           PERFORM 100-DBOPEN-SEC
      *
      *
           UNSTRING   COMMAND-PARAM    DELIMITED  BY  ","
                                       INTO    LNK-PRTKANRI-RENNUM
                                               LNK-PRTKANRI-TBL-KEY
                                               LNK-PRTKANRI-TBL-GROUP
                                               LNK-PRTKANRI-SHORI-RENNUM
                                               LNK-PRTKANRI-SRYYM
                                               LNK-PRTKANRI-SKYYMD
                                               LNK-PRTKANRI-SHELLID
                                               LNK-PRTKANRI-PRIORITY
                                               LNK-PRTKANRI-TERMID
                                               LNK-PRTKANRI-OPID
                                               LNK-PRTKANRI-PRTNM
      *
                                               WRK-PARA-HOSPNUM
                                               WRK-PARA-JOBID
                                               WRK-PARA-SHELLID
                                               WRK-PARA-HKNJANUM
                                               WRK-PARA-SYOKBN
                                               WRK-PARA-TOKUBETU
                                               WRK-PARA-STSRYYM
                                               WRK-PARA-EDSRYYM
                                               RECEERR
           END-UNSTRING
           MOVE    WRK-PARA-HOSPNUM    TO  SPA-HOSPNUM
      *
      *    ステップ管理開始処理
           MOVE    "STS"           TO  SJOBKANRI-MODE
           INITIALIZE                  JOBKANRI-REC
           MOVE    SPA-HOSPNUM     TO  JOB-HOSPNUM
           MOVE    WRK-PARA-JOBID  TO  JOB-JOBID
           MOVE    WRK-PARA-SHELLID
                                   TO  JOB-SHELLID
           MOVE    "SOKATU4510"    TO  JOB-PGID
           MOVE   "診療報酬請求書" TO  JOB-SHELLMSG
           CALL    "ORCSJOB"       USING
                                   ORCSJOBKANRIAREA
                                   JOBKANRI-REC
                                   SPA-AREA
      *
      *    サブプログラムより一時ファイル名編集
           INITIALIZE                      SGETTEMP-AREA
           MOVE    "SKT4510"           TO  SKT4510TP-FILE-ID
           MOVE    LNK-PRTKANRI-TERMID TO  SKT4510TP-TERMID
           MOVE    WRK-PARA-HOSPNUM    TO  SKT4510TP-HOSPNUM
      *
           MOVE    "RECEUPD"           TO  RECEUPD-FILE-ID
           MOVE    LNK-PRTKANRI-TERMID TO  RECEUPD-TERMID
           MOVE    WRK-PARA-HOSPNUM    TO  RECEUPD-HOSPNUM
      *
           MOVE  SKT4510TP-BASENAME      TO  SGETTEMP-BASENAMES (1)
           MOVE  RECEUPD-BASENAME        TO  SGETTEMP-BASENAMES (2)
           MOVE  RECEERR                 TO  SGETTEMP-BASENAMES (3)
           CALL  "ORCSGETTEMP"           USING
                                         SGETTEMP-AREA
           MOVE  SGETTEMP-FULLNAMES (1)  TO  SKT4510TP-FULLNAME
           MOVE  SGETTEMP-FULLNAMES (2)  TO  RECEUPD-FULLNAME
           MOVE  SGETTEMP-FULLNAMES (3)  TO  RECEERR
      *
           MOVE    LNK-PRTKANRI-SRYYM  TO  WRK-SYMD (1:6)
           MOVE    "01"                TO  WRK-SYMD (7:2)
           PERFORM 31012-SEIWA-HEN-SEC
           MOVE    WRK-HENYMDG(1:16)   TO  WRK-SRYYMWH
      *
           INITIALIZE                      STS-AREA-DAY
           INITIALIZE                      LNK-DAY2-AREA
           MOVE    "21"                TO  LNK-DAY2-IRAI
           MOVE    LNK-PRTKANRI-SKYYMD     TO  LNK-DAY2-YMD
           CALL    "ORCSDAY"           USING   STS-AREA-DAY
                                               LNK-DAY2-AREA
           IF      STS-DAY-RC1         =   ZERO
                   MOVE    LNK-DAY2-EDTYMD3    TO  WRK-SEIYMDWH
           END-IF
      *
      *    医療機関ＩＤ編集
           INITIALIZE                  SYS-1001-REC
           MOVE    "1001"              TO  SYS-1001-KANRICD
           MOVE    "*"                 TO  SYS-1001-KBNCD
           IF  WRK-PARA-STSRYYM  =  "999999"
               MOVE    LNK-PRTKANRI-SRYYM  TO  SYS-1001-STYUKYMD(1:6)
                                               SYS-1001-EDYUKYMD(1:6)
           ELSE
               MOVE    WRK-PARA-EDSRYYM    TO  SYS-1001-STYUKYMD(1:6)
                                               SYS-1001-EDYUKYMD(1:6)
           END-IF
           MOVE    "01"                TO  SYS-1001-STYUKYMD(7:2)
                                           SYS-1001-EDYUKYMD(7:2)
           MOVE    SPA-HOSPNUM         TO  SYS-1001-HOSPNUM
           MOVE    SYS-1001-REC        TO  MCPDATA-REC
           PERFORM 910-SYSKANRI-INV-SEC
           IF      FLG-SYSKANRI        =   ZERO
               MOVE    MCPDATA-REC         TO  SYS-1001-REC
               IF      SYS-1001-HOSPNAME   =   LOW-VALUE
                   MOVE    SPACE               TO  SYS-1001-HOSPNAME
               END-IF
               IF      SYS-1001-KAISETUNAME    =   LOW-VALUE
                   MOVE    SPACE               TO  SYS-1001-KAISETUNAME
               END-IF
           ELSE
               MOVE    "医療機関情報が取得できませんでした"
                                           TO  WRK-RECEERR
               PERFORM 500-ERR-HENSYU-SEC
               MOVE    1                   TO  FLG-END
           END-IF
      *
      *    医療機関情報−所在地、連絡先
           INITIALIZE                  SYS-1002-REC
           MOVE    "1002"              TO  SYS-1002-KANRICD
           MOVE    "*"                 TO  SYS-1002-KBNCD
           IF  WRK-PARA-STSRYYM  =  "999999"
               MOVE    LNK-PRTKANRI-SRYYM  TO  SYS-1002-STYUKYMD(1:6)
                                               SYS-1002-EDYUKYMD(1:6)
           ELSE
               MOVE    WRK-PARA-EDSRYYM    TO  SYS-1002-STYUKYMD(1:6)
                                               SYS-1002-EDYUKYMD(1:6)
           END-IF
           MOVE    "01"                TO  SYS-1002-STYUKYMD(7:2)
                                           SYS-1002-EDYUKYMD(7:2)
           MOVE    SPA-HOSPNUM         TO  SYS-1002-HOSPNUM
           MOVE    SYS-1002-REC        TO  MCPDATA-REC
           PERFORM 910-SYSKANRI-INV-SEC
           IF      FLG-SYSKANRI        =   ZERO
               MOVE    MCPDATA-REC        TO  SYS-1002-REC
               IF      SYS-1002-ADRS       =   LOW-VALUE
                   MOVE    SPACE               TO  SYS-1002-ADRS
               END-IF
           ELSE
               MOVE    "医療機関情報（所在地）が取得できませんでした"
                                           TO  WRK-RECEERR
               PERFORM 500-ERR-HENSYU-SEC
               MOVE    1                   TO  FLG-END
           END-IF
      *
      *    公費番号まとめ情報
           INITIALIZE                      WRK-2007-AREA
      *
           INITIALIZE                      SYS-2007-REC
           MOVE    "2007"              TO  SYS-2007-KANRICD
           MOVE    LNK-PRTKANRI-SRYYM  TO  SYS-2007-STYUKYMD (1:6)
           MOVE    "01"                TO  SYS-2007-STYUKYMD (7:2)
           MOVE    SYS-2007-STYUKYMD   TO  SYS-2007-EDYUKYMD
           MOVE    WRK-PARA-HOSPNUM    TO  SYS-2007-HOSPNUM
           MOVE    SYS-2007-REC        TO  MCPDATA-REC
           MOVE    "tbl_syskanri"      TO  MCP-TABLE
           MOVE    "key2"              TO  MCP-PATHNAME
           MOVE    "DBSELECT"          TO  MCP-FUNC
           PERFORM 900-ORCDBMAIN-SEC
           IF      MCP-RC          =   ZERO
               MOVE    "tbl_syskanri"  TO  MCP-TABLE
               MOVE    "key2"          TO  MCP-PATHNAME
               PERFORM 900-SYSKANRI-READ-N-SEC
           ELSE
               MOVE    1               TO  FLG-SYSKANRI
           END-IF
      *
           MOVE    ZERO            TO  IDX1
           PERFORM         UNTIL   FLG-SYSKANRI   =   1
                           OR      IDX1           >=  100
               MOVE    MCPDATA-REC         TO  SYS-2007-REC
      *
               ADD     1                   TO  IDX1
               MOVE    SYS-2007-MEISYO(1) TO  WRK-2007-MEISYO(IDX1)
               MOVE    ZERO                TO  IDZ
               PERFORM VARYING IDX2 FROM    1   BY  1
                       UNTIL   IDX2 >       10
                       OR      SYS-2007-KOHNUM (IDX2)   =   SPACE
                   ADD     1               TO  IDZ
                   MOVE    SYS-2007-KOHNUM (IDX2)
                                           TO  WRK-2007-KOHNUM(IDX1 IDZ)
               END-PERFORM
               PERFORM VARYING IDX2 FROM    1   BY  1
                       UNTIL   IDX2 >       20
                       OR      SYS-2007-KOHNUM1(IDX2)   =   SPACE
                   ADD     1               TO  IDZ
                   MOVE    SYS-2007-KOHNUM1(IDX2)
                                           TO  WRK-2007-KOHNUM(IDX1 IDZ)
               END-PERFORM
      *
               MOVE    "tbl_syskanri"      TO  MCP-TABLE
               MOVE    "key2"              TO  MCP-PATHNAME
               PERFORM 900-SYSKANRI-READ-N-SEC
           END-PERFORM
      *
           MOVE    "tbl_syskanri"          TO  MCP-TABLE
           MOVE    "key2"                  TO  MCP-PATHNAME
           MOVE    "DBCLOSECURSOR"         TO  MCP-FUNC
           PERFORM 900-ORCDBMAIN-SEC
      *
           MOVE    IDX1                    TO  WRK-2007-MAX
      *
           MOVE    SYS-1001-HOSPNAME   TO  WRK-HOSPNAME
           MOVE    SYS-1001-KAISETUNAME
                                       TO  WRK-KAISETUNAME
           MOVE    SYS-1002-ADRS       TO  WRK-HOSPADRS
           MOVE    SYS-1002-TEL        TO  WRK-HOSPTEL
      *    医療機関編集情報
           PERFORM 900-1900-READ-SEC
      *
      *    プログラムオプション取込み
           PERFORM 100-OPTION-SELECT-SEC
      *
           OPEN    OUTPUT  UPD-FILE
      *
           EVALUATE    WRK-PARA-SYOKBN  ALSO  WRK-PARA-TOKUBETU
               WHEN   "2"               ALSO  "1"
                   MOVE    "key37"        TO  WRK-PATH
                   MOVE    "key65"        TO  WRK-PATH2
               WHEN   "1"               ALSO  "1"
                   MOVE    "key38"        TO  WRK-PATH
                   MOVE    "key66"        TO  WRK-PATH2
               WHEN   "2"               ALSO  "2"
                   MOVE    "key39"        TO  WRK-PATH
                   MOVE    "key65"        TO  WRK-PATH2
               WHEN   "1"               ALSO  "2"
                   MOVE    "key40"        TO  WRK-PATH
                   MOVE    "key66"        TO  WRK-PATH2
           END-EVALUATE
      *
           PERFORM 900-SEIKYU-SELECT-SEC
           .
       100-INIT-EXT.
           EXIT.
      *
      *****************************************************************
      *    プログラムオプション取込み処理
      *****************************************************************
       100-OPTION-SELECT-SEC             SECTION.
      *
           MOVE    "SOKATU4510"              TO  WRK-PRGID
      *
      *    プログラムオプション取得
      *    優先保険者番号
           MOVE    "YUSEN"                   TO  WRK-KBNCD
           PERFORM 1001-OPTION-SET-SEC
           MOVE    WRK-OPTION(1)(1:6)        TO  WRK-YUSEN
           DISPLAY "優先保険者番号="  WRK-YUSEN
      *
           .
       100-OPTION-SELECT-EXT.
           EXIT.
      *****************************************************************
      *    プログラムオプションパラメタセット処理
      *****************************************************************
       1001-OPTION-SET-SEC              SECTION.
      *
           INITIALIZE                            WRK-OPTION-TBL
                                                 FLG-OK
      *    プログラムオプション情報読込
           PERFORM  900-PRGOPTION-READ-SEC
      *
      *    オプション数の初期設定
           MOVE    1                         TO  IDX1
           IF  FLG-PRGOPTION  =  ZERO
             PERFORM  VARYING  IDX  FROM  1  BY  1
                      UNTIL   (IDX     >   44000 )
                         OR   (IDX1  >  100)
                         OR   (FLG-OK  NOT =   ZERO)
      *
      *        オプションの1桁目が空白であれば処理終了
               IF   PRGOPTION-OPTION(IDX:1)  =  SPACE
               AND  IDY                      =  ZERO
                   MOVE    1                 TO  FLG-OK
               END-IF
      *
               IF  FLG-OK  =  ZERO
      *          改行地点か
                 IF  PRGOPTION-OPTION(IDX:1)  =   X"0A"
                 OR  IDY  >=  100
                   ADD    1                  TO  IDX1
                   MOVE    ZERO              TO  IDY
                 ELSE
                   ADD    1                  TO  IDY
      *            PGオプション内容を１文字ずつセット
                   MOVE    PRGOPTION-OPTION(IDX:1)
                                         TO  WRK-OPTION(IDX1)(IDY:1)
                 END-IF
               END-IF
             END-PERFORM
           END-IF
      *
           .
       1001-OPTION-SET-EXT.
           EXIT.
      *
      *****************************************************************
      *    主　　処理
      *****************************************************************
       200-MAIN-SEC                SECTION.
      *
           DISPLAY  "200-MAIN-SEC START"
      *
           OPEN     OUTPUT  SKT4510TMP-FILE
           CLOSE            SKT4510TMP-FILE
           OPEN     I-O     SKT4510TMP-FILE
      *
           MOVE     ZERO    TO      FLG-END
           MOVE     ZERO    TO      FLG-UPD
      *
           PERFORM  2001-KOKUHO-SYUKEI-SEC
                    UNTIL   FLG-END   =   1
      *
           CLOSE   UPD-FILE
           CLOSE   SKT4510TMP-FILE
      *
           IF       FLG-UPD   =       1
                    PERFORM   2002-PRINT-SEC
           END-IF
      *
           .
       200-MAIN-EXT.
           EXIT.
      *
      *****************************************************************
      *    終了    処理
      *****************************************************************
       300-END-SEC                     SECTION.
      *
           IF      CNT-RECE10          =   ZERO
                   CONTINUE
           ELSE
                   IF      FLG-UPD     =   1
                           PERFORM     310-SEIKYU-UPD-SEC
                   END-IF
           END-IF
           MOVE  CNT-PAGE  TO  WRK-PARA-PAGE
      *
           DISPLAY "*** SOKATU4510 IN  "  CNT-RECE10
           DISPLAY "*** SOKATU4510 END ***"
      *
      *    ステップ終了処理
           MOVE    "STE"           TO  SJOBKANRI-MODE
           INITIALIZE                  JOBKANRI-REC
      *
           MOVE    SPA-HOSPNUM     TO  JOB-HOSPNUM
           MOVE    WRK-PARA-PAGE   TO  JOB-UPDCNT
      *
           MOVE    WRK-PARA-JOBID  TO  JOB-JOBID
           MOVE    WRK-PARA-SHELLID
                                   TO  JOB-SHELLID
           CALL    "ORCSJOB"       USING
                                   ORCSJOBKANRIAREA
                                   JOBKANRI-REC
                                   SPA-AREA

           PERFORM 900-DBDISCONNECT-SEC
           .
       300-END-EXT.
           EXIT.
      *
      *****************************************************************
      *    保険者番号別集計処理
      *****************************************************************
       2001-KOKUHO-SYUKEI-SEC              SECTION.
      *
      *    TMP-FILE キーセット
           INITIALIZE  SKT4510TMP-REC
      *
           MOVE    RECE10-HKNJANUM-S       TO  SKT4510TMP-HKNJANUM
      *
      *    在総診・在医総チェック
           IF      RECE10-ZAITAKU  NOT =   SPACE
               MOVE    "1"     TO  SKT4510TMP-ZAITAKU
           END-IF
      *
      *    出力順設定
           IF      WRK-YUSEN           =   SKT4510TMP-HKNJANUM
               MOVE    ZERO        TO  SKT4510TMP-SYUTURYOKU
           ELSE
               MOVE    1           TO  SKT4510TMP-SYUTURYOKU
           END-IF
      *    県内・県外判定
           IF  RECE10-PREFKBN  =  "1"
      *        県外公費検索
               MOVE   ZERO         TO  FLG-TAKOH
               PERFORM  VARYING  IDX1  FROM  1  BY  1
                        UNTIL    IDX1  >     4
                        OR       RECE10-KOHNUM(IDX1)  =  SPACE
                        OR       FLG-TAKOH            =  1
      *          原爆を除く全国公費及び熊本県水俣病のみ対象
                 IF  (RECE10-KOHNUM(IDX1)(1:1)  =  "0"  AND
                      RECE10-KOHNUM(IDX1)   NOT =  "019")
                 OR  RECE10-KOHNUM(IDX1)  =  "145"
                   MOVE  RECE10-KOHNUM(IDX1)   TO  WRK-PTKOHNUM
      *            患者公費情報読込
                   PERFORM  900-PTKOHINF-SET-SEC
                 END-IF
               END-PERFORM
      *
      *        県外の公費があったか判定
               IF  FLG-TAKOH  =  1
                   MOVE  2     TO  SKT4510TMP-SYUTURYOKU
               ELSE
                   MOVE  1     TO  SKT4510TMP-SYUTURYOKU
               END-IF
           ELSE
               MOVE    2       TO  SKT4510TMP-SYUTURYOKU
           END-IF
      *
      *    国保組合の法定外給付判定
           IF  RECE10-SRYYM  >=  WRK-CONS-SRYYM-200804
             IF      SKT4510TMP-HKNJANUM(3:1)    =   "3"
             AND     RECE10-RJNHKNNUM            =   SPACE
             AND     RECE10-AGE                  <   70
             AND     RECE10-FLG (1)          NOT =   1
             AND     RECE10-KYURATE          NOT =   7
                 MOVE    RECE10-KYURATE      TO  SKT4510TMP-KYURATE
             END-IF
           ELSE
             IF      SKT4510TMP-HKNJANUM(3:1)    =   "3"
             AND     RECE10-RJNHKNNUM            =   SPACE
             AND     RECE10-AGE                  <   70
             AND     RECE10-AGE                  >   2
             AND     RECE10-KYURATE          NOT =   7
                 MOVE    RECE10-KYURATE      TO  SKT4510TMP-KYURATE
             END-IF
           END-IF
      *
      *    TMPファイル読み込み
           PERFORM  800-SKTTMP-READ-SEC
      *
           PERFORM  210-HKN-SYUKEI-SEC
      *
      *    集計の対象にならなかったとき
           IF      FLG-UPD             =   1
               PERFORM 230-UPD-FILE-HENSYU-SEC
      *
               PERFORM 900-SEIKYU-READ-SEC
           ELSE
               MOVE    SPACE               TO  WRK-RECEERR
               STRING "集計対象外　患者番号＝"     DELIMITED  BY  SIZE
                       RECE10-PTNUM                DELIMITED  BY  SPACE
                       " 診療年月＝"               DELIMITED  BY  SIZE
                       RECE10-SRYYM                DELIMITED  BY  SIZE
                       " レセ種別＝"               DELIMITED  BY  SIZE
                       RECE10-RECESYUBETU          DELIMITED  BY  SIZE
                                           INTO    WRK-RECEERR
               END-STRING
               PERFORM 500-ERR-HENSYU-SEC
               MOVE    1                   TO  FLG-END
           END-IF
           .
       2001-KOKUHO-SYUKEI-EXT.
           EXIT.
      *
      *****************************************************************
      *    保険集計処理
      *****************************************************************
       210-HKN-SYUKEI-SEC          SECTION.
      *
      *    入院／外来
           IF      RECE10-NYUGAIKBN    =   "1"
               MOVE    1       TO   IDX2
           ELSE
               MOVE    2       TO   IDX2
           END-IF
      *
      *    平成２０年３月以前の老人・一部前期高齢者は公費再掲欄へ
           IF  (RECE10-SRYYM  <  WRK-CONS-SRYYM-200804)
           AND ((RECE10-AGE  >=  70  AND  RECE10-KYURATE  =  9)
           OR  (RECE10-HKNNUM  =  "067"  AND  RECE10-AGE  >=  70)
           OR  (RECE10-RJNHKNNUM  NOT =  SPACE))
               PERFORM 2203-IZEN-SYUKEI-SEC
           ELSE
      *
      *    老人かどうか
             IF      RECE10-RJNHKNNUM    NOT =   SPACE
                 IF      RECE10-KYURATE  =   9
                     MOVE    10      TO  IDX1
                 ELSE
                     MOVE    11      TO  IDX1
                 END-IF
                 MOVE    1   TO  SKT4510TMP-PAGE-FLG2
             ELSE
                 MOVE    1   TO  SKT4510TMP-PAGE-FLG1
      *        一般国保
                 IF      RECE10-HKNNUM   =   "060" OR "068"
                     IF      RECE10-AGE  >=  70
                       EVALUATE  RECE10-KYURATE
                         WHEN  9
                           MOVE    1         TO  IDX1
                         WHEN  8
                           IF  RECE10-RECESYUBETU(4:1)  =  "7" OR "8"
                               MOVE    1     TO  IDX1
                           ELSE
                               MOVE    2     TO  IDX1
                           END-IF
                         WHEN  7
                           MOVE    2         TO  IDX1
                       END-EVALUATE
                     ELSE
                       IF  (RECE10-SRYYM  >=  WRK-CONS-SRYYM-200804)
                       AND  (RECE10-FLG (1)   =  1)
                         MOVE        4       TO  IDX1
                       ELSE
                         IF      RECE10-AGE  >=  3
                             MOVE    3       TO  IDX1
                         ELSE
                             MOVE    4       TO  IDX1
                         END-IF
                       END-IF
                     END-IF
      *        退職国保
                 ELSE
                     IF      RECE10-AGE  >=  70
                         IF      RECE10-KYURATE  =   9
                             MOVE    6       TO  IDX1
                         ELSE
                             MOVE    7       TO  IDX1
                         END-IF
                     ELSE
                         IF      RECE10-HONKZKKBN    =   "1"
                            MOVE    5       TO  IDX1
                         ELSE
                           IF  (RECE10-SRYYM  >=  WRK-CONS-SRYYM-200804)
                           AND  (RECE10-FLG (1)   =  1)
                             MOVE        7       TO  IDX1
                           ELSE
                             IF      RECE10-AGE  <   3
                                 MOVE    7       TO  IDX1
                             ELSE
                                 MOVE    6       TO  IDX1
                             END-IF
                           END-IF
                         END-IF
                     END-IF
                 END-IF
             END-IF
      *
      *    集計
             PERFORM 2101-SYUKEI-SEC
      *
           END-IF
      *    公費併用
           IF      RECE10-KOHNUM (1)   NOT =   SPACE
               MOVE    1       TO  SKT4510TMP-PAGE-FLG2
               PERFORM 2202-KOH-SEC
           END-IF
      *
      *    ２７老人併用時の原爆公費分集計処理
           IF      RECE10-ETC-KOHNUM(1)    =   "019"
           OR      RECE10-ETC-KOHNUM(2)    =   "019"
           OR      RECE10-ETC-KOHNUM(3)    =   "019"
           OR      RECE10-ETC-KOHNUM(4)    =   "019"
               PERFORM 22021-GENBK-KOH-SEC
           END-IF
      *
           IF      FLG-SKT4510TMP  =  1
               WRITE   SKT4510TMP-REC
           ELSE
               REWRITE SKT4510TMP-REC
           END-IF
      *
           MOVE    1               TO  FLG-UPD
      *
           .
       210-HKN-SYUKEI-EXT.
           EXIT.
      *
      *****************************************************************
      *    医療保険集計処理
      *****************************************************************
       2101-SYUKEI-SEC         SECTION.
      *
      *    療養の給付
      *    件数
           ADD     1                   TO  SKT4510TMP-KENSU(IDX1 IDX2)
      *
      *    日数
           ADD     RECE10-JNISSU(1)    TO  SKT4510TMP-NISSU(IDX1 IDX2)
      *
      *    点数
           ADD     RECE10-TOTALTEN(1)  TO  SKT4510TMP-TENSU(IDX1 IDX2)
      *
      *    一部負担金
           ADD     RECE10-FTNMONEY(1)  TO  SKT4510TMP-ITBFTN(IDX1 IDX2)
      *
      *    食事療養
           IF      RECE10-SHOKUJINISSU(1)  >   ZERO
      *        件数
               ADD     1               TO  SKT4510TMP-SKENSU(IDX1)
           END-IF
      *    日数
           ADD     RECE10-SHOKUJINISSU(1)
                                       TO  SKT4510TMP-SNISSU(IDX1)
      *    金額
           ADD     RECE10-SHOKUJIRYOYOHI(1)
                                       TO  SKT4510TMP-SKINGK(IDX1)
      *    標準負担額
           ADD     RECE10-SHOKUJIFTN(1)
                                       TO  SKT4510TMP-SFTNGK(IDX1)
           .
       2101-SYUKEI-EXT.
           EXIT.
      *
      *****************************************************************
      *    編集処理
      *****************************************************************
       2202-KOH-SEC              SECTION.
      *
      *    負担割合設定
           EVALUATE    RECE10-KYURATE
               WHEN    7
                   MOVE    3       TO  WRK-FTNRATE
               WHEN    8
                   MOVE    2       TO  WRK-FTNRATE
               WHEN    9
                   MOVE    1       TO  WRK-FTNRATE
               WHEN    OTHER
                   MOVE    ZERO    TO  WRK-FTNRATE
           END-EVALUATE
      *
           PERFORM VARYING IDX3    FROM    1   BY  1
                   UNTIL   IDX3    >       4
                   OR      RECE10-KOHNUM(IDX3) =   SPACE
      *
      *       ０５１と０９１は同じ場所に集計する
               IF      RECE10-KOHNUM(IDX3) =   "091"
                   MOVE    "051"       TO  RECE10-KOHNUM(IDX3)
               END-IF
      *
      *        １８１、２８１は１つにまとめる
               IF      RECE10-KOHNUM(IDX3) =   "281" OR "381"
                                            OR "481" OR "581"
                                            OR "681"
      *
                   MOVE    "181"       TO  RECE10-KOHNUM(IDX3)
               END-IF
      *
      *        公費番号まとめ情報があるとき
               MOVE  SPACE     TO  WRK-KOHNAME
               IF  WRK-2007-MAX  >  ZERO
                 PERFORM VARYING IDZ1    FROM    1   BY  1
                         UNTIL   IDZ1    >       WRK-2007-MAX
                   PERFORM VARYING IDZ2  FROM    1   BY  1
                           UNTIL   IDZ2  >       20
                           OR WRK-2007-KOHNUM(IDZ1 IDZ2)  =  SPACE
                     IF  RECE10-KOHNUM(IDX3)
                                   =   WRK-2007-KOHNUM(IDZ1 IDZ2)
                       MOVE  WRK-2007-KOHNUM(IDZ1 1)
                                   TO  RECE10-KOHNUM (IDX3)
                       MOVE  WRK-2007-MEISYO (IDZ1)
                                   TO  WRK-KOHNAME
                       MOVE  20            TO  IDZ2
                       MOVE  WRK-2007-MAX  TO  IDZ1
                     END-IF
                   END-PERFORM
                 END-PERFORM
               END-IF
      *
      *      公費・負担割合判別
               PERFORM  VARYING  IDX4  FROM  1  BY  1
                        UNTIL    IDX4  >     20
                        OR       SKT4510TMP-KOHNUM (IDX4) = SPACE
                   IF      RECE10-KOHNUM(IDX3)
                                       =   SKT4510TMP-KOHNUM(IDX4)
                   AND     WRK-FTNRATE =   SKT4510TMP-KOH-FTNRATE(IDX4)
                       MOVE    IDX4    TO  CNT-KOH
                       MOVE    20      TO  IDX4
                   END-IF
               END-PERFORM
      *
      *
      *       該当公費がなかった場合
               IF      IDX4    <   20
                   MOVE    IDX4    TO  CNT-KOH
               END-IF
      *
      *        負担割合
               MOVE    WRK-FTNRATE TO  SKT4510TMP-KOH-FTNRATE(CNT-KOH)
      *
      *        公費番号
               MOVE    RECE10-KOHNUM(IDX3)
                               TO  SKT4510TMP-KOHNUM(CNT-KOH)
      *
      *        療養の給付：件数
               ADD     1       TO  SKT4510TMP-K-KENSU(CNT-KOH IDX2)
      *
      *        療養の給付:診療実日数
               ADD     RECE10-JNISSU(IDX3 + 1)
                               TO  SKT4510TMP-K-NISSU(CNT-KOH IDX2)
      *
      *        療養の給付：点数
               ADD     RECE10-TOTALTEN(IDX3 + 1)
                               TO  SKT4510TMP-K-TENSU(CNT-KOH IDX2)
      *
      *        療養の給付：一部負担金
      *        保険番号１９０は一部負担金を印字しない
               IF  RECE10-KOHNUM(IDX3)  NOT =  "190"
                   ADD     RECE10-FTNMONEY (IDX3 + 1)
                                 TO  SKT4510TMP-K-ITBFTN(CNT-KOH IDX2)
               END-IF
      *
      *        食事療養：件数
               IF      RECE10-SHOKUJINISSU (IDX3 + 1)  >   ZERO
                   ADD     1       TO  SKT4510TMP-K-SKENSU(CNT-KOH)
               END-IF
      *        食事療養：日数
               ADD     RECE10-SHOKUJINISSU(IDX3 + 1)
                                   TO  SKT4510TMP-K-SNISSU(CNT-KOH)
      *        食事療養：金額
               ADD     RECE10-SHOKUJIRYOYOHI (IDX3 + 1)
                                   TO  SKT4510TMP-K-SKINGK(CNT-KOH)
      *        食事療養：標準負担額
               ADD     RECE10-SHOKUJIFTN (IDX3 + 1)
                                   TO  SKT4510TMP-K-SFTNGK(CNT-KOH)
           END-PERFORM
      *
           .
       2202-KOH-EXT.
           EXIT.
      *
      *****************************************************************
      *    ２７老人併用時の原爆公費分集計処理
      *****************************************************************
       22021-GENBK-KOH-SEC              SECTION.
      *
      *    負担割合設定
           EVALUATE    RECE10-KYURATE
               WHEN    7
                   MOVE    3       TO  WRK-FTNRATE
               WHEN    8
                   MOVE    2       TO  WRK-FTNRATE
               WHEN    9
                   MOVE    1       TO  WRK-FTNRATE
               WHEN    OTHER
                   MOVE    ZERO    TO  WRK-FTNRATE
           END-EVALUATE
      *
      *
      *    公費・負担割合判別
           PERFORM VARYING IDX4    FROM    1   BY  1
                   UNTIL   IDX4    >       20
                   OR      SKT4510TMP-KOHNUM (IDX4) = SPACE
               IF      "019"       =   SKT4510TMP-KOHNUM(IDX4)
               AND     WRK-FTNRATE =   SKT4510TMP-KOH-FTNRATE(IDX4)
                   MOVE    IDX4    TO  CNT-KOH
                   MOVE    20      TO  IDX4
               END-IF
           END-PERFORM
      *
      *    該当公費がなかった場合
           IF      IDX4    <   20
               MOVE    IDX4    TO  CNT-KOH
           END-IF
      *
      *    負担割合
           MOVE    WRK-FTNRATE TO  SKT4510TMP-KOH-FTNRATE(CNT-KOH)
      *
      *    公費番号
           MOVE    "019"
                               TO  SKT4510TMP-KOHNUM(CNT-KOH)
      *
      *    療養の給付：件数
           ADD     1           TO  SKT4510TMP-K-KENSU(CNT-KOH IDX2)
      *
      *    療養の給付:診療実日数
           ADD     RECE10-JNISSU(1)
                               TO  SKT4510TMP-K-NISSU(CNT-KOH IDX2)
      *
      *    療養の給付：点数
           ADD     RECE10-TOTALTEN(1)
                               TO  SKT4510TMP-K-TENSU(CNT-KOH IDX2)
           IF      RECE10-SHOKUJINISSU (1)  >  ZERO
      *        食事療養：件数
               ADD  1          TO  SKT4510TMP-K-SKENSU(CNT-KOH)
           END-IF
      *    食事療養：日数
           ADD  RECE10-SHOKUJINISSU(1)
                               TO  SKT4510TMP-K-SNISSU(CNT-KOH)
      *    食事療養：金額
           ADD  RECE10-SHOKUJIRYOYOHI (1)
                               TO  SKT4510TMP-K-SKINGK(CNT-KOH)
      *    食事療養：標準負担額
           ADD  RECE10-SHOKUJIFTN (1)
                               TO  SKT4510TMP-K-SFTNGK(CNT-KOH)
      *
           .
       22021-GENBK-KOH-EXT.
           EXIT.
      *
      *****************************************************************
      *    平成２０年３月以前編集処理
      *****************************************************************
       2203-IZEN-SYUKEI-SEC              SECTION.
      *
      *    負担割合設定
           IF  RECE10-RJNHKNNUM  NOT =  SPACE
               IF  RECE10-KYURATE  =  7
                   MOVE  7         TO  WRK-FTNRATE
               ELSE
                   MOVE  9         TO  WRK-FTNRATE
               END-IF
               MOVE  "027"         TO  WRK-KOHNUM
           ELSE
               IF  RECE10-HKNNUM  =  "060"
                   MOVE  9         TO  WRK-FTNRATE
                   MOVE  "025"     TO  WRK-KOHNUM
               ELSE
                   IF  RECE10-KYURATE  =  7
                       MOVE  7     TO  WRK-FTNRATE
                   ELSE
                       MOVE  9     TO  WRK-FTNRATE
                   END-IF
                   MOVE  "067"     TO  WRK-KOHNUM
               END-IF
           END-IF
      *
      *    種別・負担割合判別
           PERFORM  VARYING  IDX4  FROM  1  BY  1
                    UNTIL    IDX4  >     20
                    OR       SKT4510TMP-KOHNUM (IDX4) = SPACE
               IF  SKT4510TMP-KOHNUM(IDX4)       =  WRK-KOHNUM
               AND SKT4510TMP-KOH-FTNRATE(IDX4)  =  WRK-FTNRATE
                   MOVE    IDX4    TO  CNT-KOH
                   MOVE    20      TO  IDX4
               END-IF
           END-PERFORM
      *
      *    該当がなかった場合
           IF      IDX4    <   20
               MOVE    IDX4    TO  CNT-KOH
           END-IF
      *
      *    負担割合
           MOVE    WRK-FTNRATE  TO  SKT4510TMP-KOH-FTNRATE(CNT-KOH)
      *
      *    公費番号
           MOVE    WRK-KOHNUM   TO  SKT4510TMP-KOHNUM(CNT-KOH)
      *
      *    療養の給付：件数
           ADD     1       TO  SKT4510TMP-K-KENSU(CNT-KOH IDX2)
      *
      *    療養の給付:診療実日数
           ADD     RECE10-JNISSU(1)
                               TO  SKT4510TMP-K-NISSU(CNT-KOH IDX2)
      *
      *    療養の給付：点数
           ADD     RECE10-TOTALTEN(1)
                               TO  SKT4510TMP-K-TENSU(CNT-KOH IDX2)
      *
      *    療養の給付：一部負担金
           ADD     RECE10-FTNMONEY (1)
                               TO  SKT4510TMP-K-ITBFTN(CNT-KOH IDX2)
      *
      *    食事療養：件数
           IF      RECE10-SHOKUJINISSU (1)  >   ZERO
               ADD     1       TO  SKT4510TMP-K-SKENSU(CNT-KOH)
           END-IF
      *    食事療養：日数
           ADD     RECE10-SHOKUJINISSU(1)
                               TO  SKT4510TMP-K-SNISSU(CNT-KOH)
      *    食事療養：金額
           ADD     RECE10-SHOKUJIRYOYOHI (1)
                               TO  SKT4510TMP-K-SKINGK(CNT-KOH)
      *    食事療養：標準負担額
           ADD     RECE10-SHOKUJIFTN (1)
                               TO  SKT4510TMP-K-SFTNGK(CNT-KOH)
      *
           .
       2203-IZEN-SYUKEI-EXT.
           EXIT.
      *
      *****************************************************************
006900*    請求ＤＢ更新用ファイル出力処理
      *****************************************************************
       230-UPD-FILE-HENSYU-SEC         SECTION.
      *
           MOVE    RECE10-REC          TO  RECE10X-REC
           WRITE   RECE10X-REC
           ADD     1                   TO  CNT-UPDFILE
           .
       230-UPD-FILE-HENSYU-EXT.
           EXIT.
057400*
      *****************************************************************
      *    帳票ファイル出力処理
      *****************************************************************
       2002-PRINT-SEC                SECTION.
      *
           OPEN    INPUT   SKT4510TMP-FILE
      *
           MOVE    ZERO    TO  FLG-PRINT
      *
      *    一時ファイル読込
           PERFORM  900-TMP-READ-SEC
      *
           PERFORM  UNTIL     FLG-PRINT   =  1
               MOVE  ZERO  TO  FLG-PASS
               MOVE  1     TO  WRK-CNT
      *
      *        帳票編集<見出し>処理
               PERFORM  310-HEAD-HEN-SEC
      *
      *        公費ソート処理
               PERFORM KOH-SORT-SEC
      *        帳票編集<明細>処理
               PERFORM  320-BODY-HEN-SEC
      *
      *        帳票印刷処理
               PERFORM  330-PRINT-OUT-SEC
      *
               PERFORM  UNTIL  SKT4510TMP-KOHNUM(WRK-CNT) = SPACE
                        OR     WRK-CNT  >  20
      *
      *          帳票編集<見出し>処理
                 PERFORM  311-HEAD-HEN-SEC
      *
      *          帳票編集<公費>処理
                 PERFORM  321-BODY-HEN-SEC
      *
      *          帳票印刷処理
                 PERFORM  331-PRINT-OUT-SEC
               END-PERFORM
      *        一時ファイル読込
               PERFORM  900-TMP-READ-SEC
           END-PERFORM
      *
           CLOSE    SKT4510TMP-FILE
      *
           .
       2002-PRINT-EXT.
           EXIT.
      *
      *****************************************************************
      *    帳票編集<ヘッダー部>処理
      *****************************************************************
       310-HEAD-HEN-SEC          SECTION.
      *
           ADD     1       TO  CNT-PAGE
      *
           INITIALIZE  SKT4510
      *
      *    NO
           MOVE    CNT-PAGE                TO  WRK-Z4
           MOVE    WRK-Z4                  TO  SKT4510-NO
      *
      *    診療年月
           MOVE    WRK-SRYYMWH             TO  SKT4510-SRYYM
      *
      *    請求年月日
           MOVE    WRK-SEIYMDWH            TO  SKT4510-SEIYMD
      *
      *    医療機関住所
           MOVE    WRK-HOSPADRS            TO  SKT4510-ADRS
           INSPECT SKT4510-ADRS            REPLACING ALL "  " BY "　"
      *
      *    医療機関名
           MOVE    WRK-HOSPNAME            TO  SKT4510-HOSPNAME
           INSPECT SKT4510-HOSPNAME        REPLACING ALL "  " BY "　"
      *
      *    開設者名
           MOVE    WRK-KAISETUNAME         TO  SKT4510-KAISETU
      *
      *    電話番号
           MOVE    WRK-HOSPTEL             TO  SKT4510-TEL
      *
      *    医療機関コード
           MOVE    SYS-1001-HOSPCD         TO  WRK-MAE-INPUT
           PERFORM 2009-HENKAN-SEC
           MOVE    WRK-OUT-INPUT           TO  SKT4510-HOSPCD
      *
      *    保険者番号
           MOVE    SKT4510TMP-HKNJANUM     TO  WRK-MAE-INPUT
           PERFORM 2009-HENKAN-SEC
           MOVE    WRK-OUT-INPUT           TO  SKT4510-HKNJANUM
      *
      *    保険者名
           MOVE    SKT4510TMP-HKNJANUM     TO  WRK-HKNJANUM
           PERFORM  920-HKNJAINF-SET-SEC
           IF      HKNJA-HKNJANAME     NOT =   SPACE
               IF      SKT4510TMP-HKNJANUM(3:1)    NOT =   "3"
      *
      *            県名編集
                   PERFORM 3101-KENNAME-HEN-SEC
                   STRING  WRK-KENNAME         DELIMITED BY SIZE
                           HKNJA-HKNJANAME     DELIMITED BY SPACE
                           "  殿"              DELIMITED BY SIZE
                           INTO                SKT4510-HKNJANAME
                   END-STRING
               ELSE
      *
      *          国保組合は県名と"市町村"を記載しない
                   STRING  HKNJA-HKNJANAME     DELIMITED BY SPACE
                           "  殿"              DELIMITED BY SIZE
                           INTO                SKT4510-HKNJANAME
                   END-STRING
               END-IF
           ELSE
               MOVE    "  殿"      TO  SKT4510-HKNJANAME(25:4)
           END-IF
      *
      *    在宅
           IF      SKT4510TMP-ZAITAKU  =   "1"
               MOVE    "在"        TO  SKT4510-ZAI1
               MOVE    "○"        TO  SKT4510-ZAI2
           END-IF
      *
      *    給付割合
           MOVE    SKT4510TMP-KYURATE      TO  WRK-Z2
           MOVE    WRK-Z2                  TO  SKT4510-KYURATE
      *
      *    県内／県外
           IF  SKT4510TMP-SYUTURYOKU  =  2
               MOVE  "（県外分）"  TO  SKT4510-PREFKBN
           ELSE
               MOVE  "（県内分）"  TO  SKT4510-PREFKBN
           END-IF
      *
      *    特別療養費
           IF  WRK-PARA-TOKUBETU  =  "2"
             MOVE  "特別療養費"            TO  SKT4510-TOKUBETU
           END-IF
      *
           .
       310-HEAD-HEN-EXT.
           EXIT.
      *
      *****************************************************************
      *    県名編集処理
      *****************************************************************
       3101-KENNAME-HEN-SEC                SECTION.
      *
           INITIALIZE  WRK-KENNAME
           EVALUATE    SKT4510TMP-HKNJANUM (1:2)
               WHEN    "01"    MOVE    "北海道"    TO  WRK-KENNAME
               WHEN    "02"    MOVE    "青森県"    TO  WRK-KENNAME
               WHEN    "03"    MOVE    "岩手県"    TO  WRK-KENNAME
               WHEN    "04"    MOVE    "宮城県"    TO  WRK-KENNAME
               WHEN    "05"    MOVE    "秋田県"    TO  WRK-KENNAME
               WHEN    "06"    MOVE    "山形県"    TO  WRK-KENNAME
               WHEN    "07"    MOVE    "福島県"    TO  WRK-KENNAME
               WHEN    "08"    MOVE    "茨城県"    TO  WRK-KENNAME
               WHEN    "09"    MOVE    "栃木県"    TO  WRK-KENNAME
               WHEN    "10"    MOVE    "群馬県"    TO  WRK-KENNAME
               WHEN    "11"    MOVE    "埼玉県"    TO  WRK-KENNAME
               WHEN    "12"    MOVE    "千葉県"    TO  WRK-KENNAME
               WHEN    "13"    MOVE    "東京都"    TO  WRK-KENNAME
               WHEN    "14"    MOVE    "神奈川県"  TO  WRK-KENNAME
               WHEN    "15"    MOVE    "新潟県"    TO  WRK-KENNAME
               WHEN    "16"    MOVE    "富山県"    TO  WRK-KENNAME
               WHEN    "17"    MOVE    "石川県"    TO  WRK-KENNAME
               WHEN    "18"    MOVE    "福井県"    TO  WRK-KENNAME
               WHEN    "19"    MOVE    "山梨県"    TO  WRK-KENNAME
               WHEN    "20"    MOVE    "長野県"    TO  WRK-KENNAME
               WHEN    "21"    MOVE    "岐阜県"    TO  WRK-KENNAME
               WHEN    "22"    MOVE    "静岡県"    TO  WRK-KENNAME
               WHEN    "23"    MOVE    "愛知県"    TO  WRK-KENNAME
               WHEN    "24"    MOVE    "三重県"    TO  WRK-KENNAME
               WHEN    "25"    MOVE    "滋賀県"    TO  WRK-KENNAME
               WHEN    "26"    MOVE    "京都府"    TO  WRK-KENNAME
               WHEN    "27"    MOVE    "大阪府"    TO  WRK-KENNAME
               WHEN    "28"    MOVE    "兵庫県"    TO  WRK-KENNAME
               WHEN    "29"    MOVE    "奈良県"    TO  WRK-KENNAME
               WHEN    "30"    MOVE    "和歌山県"  TO  WRK-KENNAME
               WHEN    "31"    MOVE    "鳥取県"    TO  WRK-KENNAME
               WHEN    "32"    MOVE    "島根県"    TO  WRK-KENNAME
               WHEN    "33"    MOVE    "岡山県"    TO  WRK-KENNAME
               WHEN    "34"    MOVE    "広島県"    TO  WRK-KENNAME
               WHEN    "35"    MOVE    "山口県"    TO  WRK-KENNAME
               WHEN    "36"    MOVE    "徳島県"    TO  WRK-KENNAME
               WHEN    "37"    MOVE    "香川県"    TO  WRK-KENNAME
               WHEN    "38"    MOVE    "愛媛県"    TO  WRK-KENNAME
               WHEN    "39"    MOVE    "高知県"    TO  WRK-KENNAME
               WHEN    "40"    MOVE    "福岡県"    TO  WRK-KENNAME
               WHEN    "41"    MOVE    "佐賀県"    TO  WRK-KENNAME
               WHEN    "42"    MOVE    "長崎県"    TO  WRK-KENNAME
               WHEN    "43"    MOVE    "熊本県"    TO  WRK-KENNAME
               WHEN    "44"    MOVE    "大分県"    TO  WRK-KENNAME
               WHEN    "45"    MOVE    "宮崎県"    TO  WRK-KENNAME
               WHEN    "46"    MOVE    "鹿児島県"  TO  WRK-KENNAME
               WHEN    "47"    MOVE    "沖縄県"    TO  WRK-KENNAME
           END-EVALUATE
      *
           .
       3101-KENNAME-HEN-EXT.
           EXIT.
      *
      *****************************************************************
      *    帳票編集<明細行>処理
      *****************************************************************
       320-BODY-HEN-SEC      SECTION.
      *
           PERFORM VARYING IDX1    FROM    1   BY  1
                   UNTIL   IDX1    >       7
               PERFORM VARYING IDX2    FROM    1   BY  1
                       UNTIL   IDX2    >       2
      *
      *            療養の給付：件数
                   MOVE    SKT4510TMP-KENSU(IDX1 IDX2) TO  WRK-Z5
                   MOVE    WRK-Z5      TO  SKT4510-KENSU(IDX1 IDX2)
      *
      *            療養の給付：日数
                   MOVE    SKT4510TMP-NISSU(IDX1 IDX2) TO  WRK-Z5
                   MOVE    WRK-Z5      TO  SKT4510-NISSU(IDX1 IDX2)
      *
      *            療養の給付：点数
                   MOVE    SKT4510TMP-TENSU(IDX1 IDX2) TO  WRK-Z9
                   MOVE    WRK-Z9      TO  SKT4510-TENSU(IDX1 IDX2)
      *
      *           療養の給付：一部負担金
                   MOVE    SKT4510TMP-ITBFTN(IDX1 IDX2)    TO  WRK-Z9
                   MOVE    WRK-Z9      TO  SKT4510-ITBFTN(IDX1 IDX2)
               END-PERFORM
      *
      *        食事療養：件数
               MOVE    SKT4510TMP-SKENSU(IDX1)     TO  WRK-Z5
               MOVE    WRK-Z5      TO  SKT4510-SKENSU(IDX1)
      *
      *        食事療養：日数
               MOVE    SKT4510TMP-SNISSU(IDX1)     TO  WRK-Z5
               MOVE    WRK-Z5      TO  SKT4510-SNISSU(IDX1)
      *
      *        食事療養：金額
               MOVE    SKT4510TMP-SKINGK(IDX1)     TO  WRK-Z9
               MOVE    WRK-Z9      TO  SKT4510-SKINGK(IDX1)
      *
      *        食事療養：標準負担額
               MOVE    SKT4510TMP-SFTNGK(IDX1)     TO  WRK-Z9
               MOVE    WRK-Z9      TO  SKT4510-SFTNGK(IDX1)
           END-PERFORM
      *
      *    公費再掲欄
           PERFORM  VARYING  IDX1  FROM  8  BY  1
                    UNTIL    IDX1  >     9
      *    法別番号
             IF  SKT4510TMP-KOHNUM (WRK-CNT)  =  "025" OR "067" OR "027"
               MOVE  SKT4510TMP-KOHNUM (WRK-CNT)(2:)
                                       TO  SKT4510-HBTNUM(WRK-CNT)
             ELSE
               MOVE    SKT4510TMP-KOHNUM (WRK-CNT) TO  WRK-KOHNUM
               PERFORM 950-HKNNUM-SET-SEC
               MOVE    HKN-HBTNUM      TO  SKT4510-HBTNUM(WRK-CNT)
             END-IF
      *    割合
             MOVE  SKT4510TMP-KOH-FTNRATE(WRK-CNT)  TO  WRK-Z1
             MOVE  WRK-Z1              TO  SKT4510-FTNRATE(WRK-CNT)
      *
             PERFORM VARYING IDX2    FROM    1   BY  1
                     UNTIL   IDX2    >       2
      *
      *        療養の給付：件数
               MOVE    SKT4510TMP-K-KENSU(WRK-CNT IDX2)  TO  WRK-Z5
               MOVE    WRK-Z5  TO  SKT4510-KENSU(IDX1 IDX2)
      *
      *        療養の給付：日数
               MOVE    SKT4510TMP-K-NISSU(WRK-CNT IDX2)  TO  WRK-Z5
               MOVE    WRK-Z5  TO  SKT4510-NISSU(IDX1 IDX2)
      *
      *        療養の給付：点数
               MOVE    SKT4510TMP-K-TENSU(WRK-CNT IDX2)  TO  WRK-Z9
               MOVE    WRK-Z9  TO  SKT4510-TENSU(IDX1 IDX2)
      *
      *        療養の給付：一部負担金
               MOVE    SKT4510TMP-K-ITBFTN(WRK-CNT IDX2) TO  WRK-Z9
               MOVE    WRK-Z9  TO  SKT4510-ITBFTN(IDX1 IDX2)
             END-PERFORM
      *
      *      食事療養：件数
             MOVE    SKT4510TMP-K-SKENSU(WRK-CNT)    TO  WRK-Z5
             MOVE    WRK-Z5  TO  SKT4510-SKENSU(IDX1)
      *
      *      食事療養：日数
             MOVE    SKT4510TMP-K-SNISSU(WRK-CNT)    TO  WRK-Z5
             MOVE    WRK-Z5  TO  SKT4510-SNISSU(IDX1)
      *
      *      食事療養：金額
             MOVE    SKT4510TMP-K-SKINGK(WRK-CNT)    TO  WRK-Z9
             MOVE    WRK-Z9  TO  SKT4510-SKINGK(IDX1)
      *
      *      食事療養：標準負担額
             MOVE    SKT4510TMP-K-SFTNGK(WRK-CNT)    TO  WRK-Z9
             MOVE    WRK-Z9  TO  SKT4510-SFTNGK(IDX1)
      *
             ADD  1  TO  WRK-CNT
           END-PERFORM
      *
           .
       320-BODY-HEN-EXT.
           EXIT.
      *
      *****************************************************************
      *    帳票印刷処理
      *****************************************************************
       330-PRINT-OUT-SEC                SECTION.
      *
      *
           INITIALIZE                  ORCSPRTAREA
           MOVE    "INS"               TO  SPRT-MODE
           MOVE    LNK-PRTKANRI-RENNUM TO  SPRT-RENNUM
           MOVE    LNK-PRTKANRI-TBL-KEY
                                       TO  SPRT-TBL-KEY
           MOVE    LNK-PRTKANRI-TBL-GROUP
                                       TO  SPRT-TBL-GROUP
           MOVE    LNK-PRTKANRI-SRYYM  TO  SPRT-SRYYM
           MOVE    LNK-PRTKANRI-SKYYMD TO  SPRT-SKYYMD
           MOVE    LNK-PRTKANRI-SHELLID
                                       TO  SPRT-SHELLID
           MOVE    LNK-PRTKANRI-SHORI-RENNUM
                                       TO  SPRT-SHORI-RENNUM
           MOVE    LNK-PRTKANRI-PRIORITY
                                       TO  SPRT-PRIORITY
           MOVE   "SKT4510.red"        TO  SPRT-PRTID
           EVALUATE  WRK-PARA-SYOKBN  ALSO  WRK-PARA-TOKUBETU
             WHEN  "1"  ALSO  "1"
               MOVE  "国保請求書"                TO  SPRT-TITLE
             WHEN  "2"  ALSO  "1"
               MOVE  "国保請求書［返戻分］"      TO  SPRT-TITLE
             WHEN  "1"  ALSO  "2"
               MOVE  "国保請求書［特別療養費］"  TO  SPRT-TITLE
             WHEN  "2"  ALSO  "2"
               MOVE  "国保請求書［返戻分］［特別療養費］" TO  SPRT-TITLE
           END-EVALUATE
           MOVE    SKT4510             TO  SPRT-PRTDATA
           MOVE    LNK-PRTKANRI-TERMID TO  SPRT-TERMID
           MOVE    LNK-PRTKANRI-OPID   TO  SPRT-OPID
           MOVE    LNK-PRTKANRI-PRTNM  TO  SPRT-PRTNM
           MOVE    "2"                 TO  SPRT-SITEKBN
           CALL    "ORCSPRT"           USING
                                       ORCSPRTAREA
                                       SPA-AREA
           IF      SPRT-RETURN         =   ZERO
               CONTINUE
           ELSE
               MOVE    "印刷ＤＢに更新できませんでした"
                                          TO  WRK-RECEERR
               PERFORM 500-ERR-HENSYU-SEC
           END-IF
           .
       330-PRINT-OUT-EXT.
           EXIT.
      *
      *****************************************************************
      *    帳票編集<ヘッダー部>処理（公費）
      *****************************************************************
       311-HEAD-HEN-SEC          SECTION.
      *
      *
           ADD     1       TO  CNT-PAGE
      *
           INITIALIZE                             SKT4511
      *
      *    NO
           MOVE    CNT-PAGE                TO  WRK-Z4
           MOVE    WRK-Z4                  TO  SKT4511-NO
      *
      *    診療年月
           MOVE    WRK-SRYYMWH             TO  SKT4511-SRYYM
      *
      *    請求年月日
           MOVE    WRK-SEIYMDWH            TO  SKT4511-SEIYMD
      *
      *    医療機関住所
           MOVE    WRK-HOSPADRS            TO  SKT4511-ADRS
           INSPECT SKT4511-ADRS            REPLACING ALL "  " BY "　"
      *
      *    医療機関名
           MOVE    WRK-HOSPNAME            TO  SKT4511-HOSPNAME
           INSPECT SKT4511-HOSPNAME        REPLACING ALL "  " BY "　"
      *
      *    開設者名
           MOVE    WRK-KAISETUNAME         TO  SKT4511-KAISETU
      *
      *    電話番号
           MOVE    WRK-HOSPTEL             TO  SKT4511-TEL
      *
      *    医療機関コード
           MOVE    SYS-1001-HOSPCD         TO  WRK-MAE-INPUT
           PERFORM 2009-HENKAN-SEC
           MOVE    WRK-OUT-INPUT           TO  SKT4511-HOSPCD
      *
      *    保険者番号
           MOVE    SKT4510TMP-HKNJANUM     TO  WRK-MAE-INPUT
           PERFORM 2009-HENKAN-SEC
           MOVE    WRK-OUT-INPUT           TO  SKT4511-HKNJANUM
      *
      *    保険者名
           MOVE    SKT4510TMP-HKNJANUM     TO  WRK-HKNJANUM
           PERFORM 920-HKNJAINF-SET-SEC
           IF      HKNJA-HKNJANAME     NOT =   SPACE
               IF      SKT4510TMP-HKNJANUM(3:1)    NOT =   "3"
      *
      *            県名編集
                   PERFORM 3101-KENNAME-HEN-SEC
                   STRING  WRK-KENNAME         DELIMITED BY SIZE
                           HKNJA-HKNJANAME     DELIMITED BY SPACE
                           "  殿"              DELIMITED BY SIZE
                           INTO                SKT4511-HKNJANAME
                   END-STRING
               ELSE
      *
      *          国保組合は県名と"市町村"を記載しない
                   STRING  HKNJA-HKNJANAME     DELIMITED BY SPACE
                           "  殿"              DELIMITED BY SIZE
                           INTO                SKT4511-HKNJANAME
                   END-STRING
               END-IF
           ELSE
               MOVE    "  殿"      TO  SKT4511-HKNJANAME(25:4)
           END-IF
      *
      *    在宅
           IF      SKT4510TMP-ZAITAKU  =   "1"
               MOVE    "在"        TO  SKT4511-ZAI1
               MOVE    "○"        TO  SKT4511-ZAI2
           END-IF
      *
      *    県内／県外
           IF  SKT4510TMP-SYUTURYOKU  =  2
               MOVE  "（県外分）"  TO  SKT4511-PREFKBN
           ELSE
               MOVE  "（県内分）"  TO  SKT4511-PREFKBN
           END-IF
      *
      *    特別療養費
           IF  WRK-PARA-TOKUBETU  =  "2"
             MOVE  "特別療養費"            TO  SKT4511-TOKUBETU
           END-IF
      *
           .
       311-HEAD-HEN-EXT.
           EXIT.
      *
      *****************************************************************
      *    帳票編集<明細行>処理（公費）
      *****************************************************************
       321-BODY-HEN-SEC      SECTION.
      *
      *    公費の印字
           PERFORM VARYING IDX3    FROM    1   BY  1
                   UNTIL   IDX3    >       7
                   OR      WRK-CNT >       20
                   OR      SKT4510TMP-KOHNUM(WRK-CNT)  =   SPACE
               PERFORM VARYING IDX2    FROM    1   BY  1
                       UNTIL   IDX2    >       2
      *
      *            療養の給付：件数
                   MOVE    SKT4510TMP-K-KENSU(WRK-CNT IDX2)  TO  WRK-Z5
                   MOVE    WRK-Z5  TO  SKT4511-K-KENSU(IDX3 IDX2)
      *
      *            療養の給付：日数
                   MOVE    SKT4510TMP-K-NISSU(WRK-CNT IDX2)  TO  WRK-Z5
                   MOVE    WRK-Z5  TO  SKT4511-K-NISSU(IDX3 IDX2)
      *
      *            療養の給付：点数
                   MOVE    SKT4510TMP-K-TENSU(WRK-CNT IDX2)  TO  WRK-Z9
                   MOVE    WRK-Z9  TO  SKT4511-K-TENSU(IDX3 IDX2)
      *
      *            療養の給付：一部負担金
                   MOVE    SKT4510TMP-K-ITBFTN(WRK-CNT IDX2) TO  WRK-Z9
                   MOVE    WRK-Z9  TO  SKT4511-K-ITBFTN(IDX3 IDX2)
               END-PERFORM
      *
      *        食事療養：件数
               MOVE    SKT4510TMP-K-SKENSU(WRK-CNT)    TO  WRK-Z5
               MOVE    WRK-Z5  TO  SKT4511-K-SKENSU(IDX3)
      *
      *        食事療養：日数
               MOVE    SKT4510TMP-K-SNISSU(WRK-CNT)    TO  WRK-Z5
               MOVE    WRK-Z5  TO  SKT4511-K-SNISSU(IDX3)
      *
      *        食事療養：金額
               MOVE    SKT4510TMP-K-SKINGK(WRK-CNT)    TO  WRK-Z9
               MOVE    WRK-Z9  TO  SKT4511-K-SKINGK(IDX3)
      *
      *        食事療養：標準負担額
               MOVE    SKT4510TMP-K-SFTNGK(WRK-CNT)    TO  WRK-Z9
               MOVE    WRK-Z9  TO  SKT4511-K-SFTNGK(IDX3)
      *
      *        法別番号
               IF      SKT4510TMP-KOHNUM (WRK-CNT) =   "019"
                   MOVE    "19"    TO  SKT4511-HBTNUM(IDX3)
               ELSE
                 IF  SKT4510TMP-KOHNUM (WRK-CNT)
                                   =  "025" OR "067" OR "027"
                   MOVE  SKT4510TMP-KOHNUM (WRK-CNT)(2:)
                                       TO  SKT4511-HBTNUM(IDX3)
                 ELSE
                   MOVE    SKT4510TMP-KOHNUM (WRK-CNT) TO  WRK-KOHNUM
                   PERFORM 950-HKNNUM-SET-SEC
                   MOVE    HKN-HBTNUM      TO  SKT4511-HBTNUM(IDX3)
                 END-IF
               END-IF
      *
      *        負担割合
               MOVE  SKT4510TMP-KOH-FTNRATE(WRK-CNT)
                                      TO  SKT4511-FTNRATE(IDX3)
      *
               ADD  1  TO  WRK-CNT
           END-PERFORM
      *
           .
       321-BODY-HEN-EXT.
           EXIT.
      *
      *****************************************************************
      *    帳票印刷処理（公費）
      *****************************************************************
       331-PRINT-OUT-SEC                SECTION.
      *
           INITIALIZE                  ORCSPRTAREA
           MOVE    "INS"               TO  SPRT-MODE
           MOVE    LNK-PRTKANRI-RENNUM TO  SPRT-RENNUM
           MOVE    LNK-PRTKANRI-TBL-KEY
                                       TO  SPRT-TBL-KEY
           MOVE    LNK-PRTKANRI-TBL-GROUP
                                       TO  SPRT-TBL-GROUP
           MOVE    LNK-PRTKANRI-SRYYM  TO  SPRT-SRYYM
           MOVE    LNK-PRTKANRI-SKYYMD TO  SPRT-SKYYMD
           MOVE    LNK-PRTKANRI-SHELLID
                                       TO  SPRT-SHELLID
           MOVE    LNK-PRTKANRI-SHORI-RENNUM
                                       TO  SPRT-SHORI-RENNUM
           MOVE    LNK-PRTKANRI-PRIORITY
                                       TO  SPRT-PRIORITY
           MOVE   "SKT4511.red"            TO  SPRT-PRTID
           EVALUATE  WRK-PARA-SYOKBN  ALSO  WRK-PARA-TOKUBETU
             WHEN  "1"  ALSO  "1"
               MOVE  "国保請求書"                TO  SPRT-TITLE
             WHEN  "2"  ALSO  "1"
               MOVE  "国保請求書［返戻分］"      TO  SPRT-TITLE
             WHEN  "1"  ALSO  "2"
               MOVE  "国保請求書［特別療養費］"  TO  SPRT-TITLE
             WHEN  "2"  ALSO  "2"
               MOVE  "国保請求書［返戻分］［特別療養費］" TO  SPRT-TITLE
           END-EVALUATE
           MOVE    SKT4511             TO  SPRT-PRTDATA
           MOVE    LNK-PRTKANRI-TERMID TO  SPRT-TERMID
           MOVE    LNK-PRTKANRI-OPID   TO  SPRT-OPID
           MOVE    LNK-PRTKANRI-PRTNM  TO  SPRT-PRTNM
           MOVE    "2"                 TO  SPRT-SITEKBN
           CALL    "ORCSPRT"           USING
                                       ORCSPRTAREA
                                       SPA-AREA
           IF      SPRT-RETURN         =   ZERO
               CONTINUE
           ELSE
               MOVE    "印刷ＤＢに更新できませんでした"
                                          TO  WRK-RECEERR
               PERFORM 500-ERR-HENSYU-SEC
           END-IF
           .
       331-PRINT-OUT-EXT.
           EXIT.
      *
      *****************************************************************
      *    公費ソート処理
      *****************************************************************
       KOH-SORT-SEC          SECTION.
      *
           INITIALIZE  SORT-AREA
      *    公費数を取得
           MOVE    ZERO    TO    IDX-MAX
                                 IDX2
           INITIALIZE  WRK-KOH-KEY
           PERFORM  VARYING  IDX1  FROM  1  BY  1
                    UNTIL    IDX1  >  20
             IF  SKT4510TMP-KOHNUM(IDX1)  =  SPACE
               MOVE  20    TO    IDX1
             ELSE
               IF    WRK-KOHNUM                =   SPACE   OR
                     SKT4510TMP-KOH-KEY(IDX1)  <   WRK-KOH-KEY
                 MOVE     IDX1                TO  IDX2
                 MOVE     SKT4510TMP-KOH-KEY(IDX1) TO  WRK-KOH-KEY
               END-IF
               ADD   1     TO    IDX-MAX
             END-IF
           END-PERFORM
           IF     IDX2     >     ZERO
             MOVE    SKT4510TMP-KOH-TBL(IDX2) TO  SORT-KOH-TBL(1)
           END-IF

           DISPLAY "一般ソート前１" SKT4510TMP-KOH-KEY(1)
           DISPLAY "一般ソート前２" SKT4510TMP-KOH-KEY(2)
           DISPLAY "一般ソート前３" SKT4510TMP-KOH-KEY(3)
      *
           IF      IDX-MAX    >     ZERO
             MOVE    2    TO    IDX1
             MOVE    1    TO    IDX2
             MOVE    1    TO    IDX3
             MOVE   HIGH-VALUE  TO  WRK-KOH-KEY
             PERFORM    UNTIL    IDX1  >    IDX-MAX
               PERFORM  VARYING  IDX2  FROM  1  BY  1
                        UNTIL    IDX2  >    IDX-MAX
                 IF  SKT4510TMP-KOH-KEY(IDX2)
                                       > SORT-KOHNUM-KEY(IDX1 - 1)
                 AND SKT4510TMP-KOH-KEY(IDX2)
                                       < WRK-KOH-KEY
                     MOVE  SKT4510TMP-KOH-KEY(IDX2)  TO WRK-KOH-KEY
                     MOVE  IDX2          TO    IDX3
                 END-IF
               END-PERFORM
               MOVE  SKT4510TMP-KOH-TBL(IDX3)  TO  SORT-KOH-TBL(IDX1)
               MOVE  HIGH-VALUE  TO  WRK-KOH-KEY
               ADD   1    TO    IDX1
             END-PERFORM
           END-IF
      *
           INITIALIZE             SKT4510TMP-KOH-AREA
           MOVE  SORT-AREA  TO    SKT4510TMP-KOH-AREA
      *
           DISPLAY "一般ソート後１" SKT4510TMP-KOH-KEY(1)
           DISPLAY "一般ソート後２" SKT4510TMP-KOH-KEY(2)
           DISPLAY "一般ソート後３" SKT4510TMP-KOH-KEY(3)
      *
      *
           .
       KOH-SORT-EXT.
           EXIT.
      *
      *****************************************************************
      *    エラー出力処理
      *****************************************************************
       500-ERR-HENSYU-SEC                SECTION.
      *
           OPEN    INPUT   RECEERR-FILE
           IF      STS-RECEERR         =   ZERO
               CLOSE   RECEERR-FILE
           ELSE
               OPEN    OUTPUT              RECEERR-FILE
      *
               MOVE    WRK-RECEERR         TO  RECEERR-REC
               WRITE   RECEERR-REC
               CLOSE   RECEERR-FILE
      *
      *        ジョブ管理開始処理
               MOVE    "JBE"           TO  SJOBKANRI-MODE
               INITIALIZE                  JOBKANRI-REC
               MOVE    SPA-HOSPNUM     TO  JOB-HOSPNUM
               MOVE    WRK-PARA-JOBID  TO  JOB-JOBID
               MOVE    WRK-PARA-SHELLID
                                       TO  JOB-SHELLID
               MOVE    WRK-RECEERR     TO  JOB-YOBI
               MOVE    "9999"          TO  JOB-ERRCD
               CALL    "ORCSJOB"       USING
                                       ORCSJOBKANRIAREA
                                       JOBKANRI-REC
                                       SPA-AREA
           END-IF
      *
           MOVE    1                   TO  FLG-END
      *
           .
       500-ERR-HENSYU-EXT.
           EXIT.
      *
      *****************************************************************
      *   請求管理ＤＢ更新処理
      *****************************************************************
       310-SEIKYU-UPD-SEC              SECTION.
      *
           OPEN    INPUT    UPD-FILE
      *
           PERFORM 900-UPD-FILE-READ-SEC
           PERFORM             UNTIL   FLG-UPDFILE    =   1
               MOVE    RECE10X-KEY         TO  RECE10-KEY
               MOVE    RECE10-REC          TO  MCPDATA-REC
               PERFORM 910-SEIKYU-INV-SEC
               IF      FLG-SEIKYU          =   ZERO
                   MOVE    "2"             TO  RECE10-SKYKBN
      *
                   MOVE    LNK-PRTKANRI-SRYYM
                                           TO  RECE10-SKYYM
                   MOVE    LNK-PRTKANRI-SKYYMD
                                           TO  RECE10-UPDYMD
      *
                   MOVE    RECE10-REC      TO  MCPDATA-REC
                   MOVE    "DBUPDATE"      TO  MCP-FUNC
                   MOVE    "tbl_seikyu"    TO  MCP-TABLE
                   MOVE    "key"           TO  MCP-PATHNAME
                   CALL    "ORCDBMAIN"     USING
                                           MCPAREA
                                           MCPDATA-REC
                                           SPA-AREA
                   IF      MCP-RC          =   ZERO
                       PERFORM 900-UPD-FILE-READ-SEC
                   ELSE
                       DISPLAY "UPD ERR KEY="  RECE10-KEY
                       MOVE  "*** SOKATU4510 SEIKYU UPD ERR ***"
                                           TO  WRK-RECEERR
                       MOVE    RECE10-KEY  TO  WRK-RECEERR(33:)
                       PERFORM 500-ERR-HENSYU-SEC
                       MOVE    1           TO  FLG-UPDFILE
                   END-IF
               ELSE
                       DISPLAY "INV ERR KEY="  RECE10-KEY
                   MOVE  "*** SOKATU4510 SEIKYU INVALID ***"
                                           TO  WRK-RECEERR
                   MOVE    RECE10-KEY      TO  WRK-RECEERR(33:)
                   PERFORM 500-ERR-HENSYU-SEC
                   MOVE    1               TO  FLG-UPDFILE
               END-IF
           END-PERFORM
      *
           CLOSE   UPD-FILE
           .
       310-SEIKYU-UPD-EXT.
           EXIT.
      *
      *****************************************************************
      *    西暦日本語変換処理
      *****************************************************************
       31012-SEIWA-HEN-SEC        SECTION.
      *
           INITIALIZE                      STS-AREA-DAY
           INITIALIZE                      LNK-DAY2-AREA
           MOVE    "21"                TO  LNK-DAY2-IRAI
           MOVE    WRK-SYMD            TO  LNK-DAY2-YMD
           CALL    "ORCSDAY"           USING   STS-AREA-DAY
                                               LNK-DAY2-AREA
           MOVE    LNK-DAY2-EDTYMD3    TO  WRK-HENYMDG
           INSPECT WRK-HENYMDG    REPLACING  ALL "  "  BY  "　"
           .
       31012-SEIWA-HEN-EXT.
           EXIT.
      *
      *****************************************************************
      *   保険者情報セット
      *****************************************************************
       920-HKNJAINF-SET-SEC  SECTION.
      *
           INITIALIZE  HKNJAINF-REC
           MOVE    SPA-HOSPNUM             TO  HKNJA-HOSPNUM
           MOVE    WRK-HKNJANUM            TO  HKNJA-HKNJANUM
           MOVE    HKNJAINF-REC            TO  MCPDATA-REC
           MOVE    "DBSELECT"              TO  MCP-FUNC
           MOVE    "tbl_hknjainf"       TO  MCP-TABLE
           MOVE    "key"                TO  MCP-PATHNAME
           CALL    "ORCDBMAIN"          USING
                                        MCPAREA
                                        MCPDATA-REC
                                        SPA-AREA
           IF  MCP-RC  =  ZERO
               MOVE    "tbl_hknjainf"     TO  MCP-TABLE
               MOVE    "key"              TO  MCP-PATHNAME
               PERFORM  925-HKNJAINF-READ-SEC
           ELSE
               DISPLAY  "HKNJAINF SELECT ERR"
           END-IF
      *
           INITIALIZE  MCPAREA
           MOVE    "DBCLOSECURSOR"         TO  MCP-FUNC
           MOVE    "tbl_hknjainf"       TO  MCP-TABLE
           MOVE    "key"                TO  MCP-PATHNAME
           CALL    "ORCDBMAIN"          USING
                                        MCPAREA
                                        MCPDATA-REC
                                        SPA-AREA
      *
           .
       920-HKNJAINF-SET-EXT.
           EXIT.
      *
      *****************************************************************
      *    保険者情報読込
      *****************************************************************
       925-HKNJAINF-READ-SEC  SECTION.
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
           CALL    "ORCDBMAIN"          USING
                                        MCPAREA
                                        MCPDATA-REC
                                        SPA-AREA
           IF      MCP-RC  =   ZERO
               MOVE    MCPDATA-REC TO  HKNJAINF-REC
               MOVE    ZERO        TO  FLG-HKNJAINF
           ELSE
               INITIALIZE  HKNJAINF-REC
               MOVE    1           TO  FLG-HKNJAINF
           END-IF
      *
           .
       925-HKNJAINF-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    保険番号情報セット
      *****************************************************************
       950-HKNNUM-SET-SEC  SECTION.
      *
           INITIALIZE  HKNNUM-REC
           MOVE    SPA-HOSPNUM          TO  HKN-HOSPNUM
           MOVE    WRK-KOHNUM           TO  HKN-HKNNUM
           MOVE    LNK-PRTKANRI-SRYYM(1:6)
                                        TO  HKN-TEKSTYMD(1:6)
           MOVE    "01"                 TO  HKN-TEKSTYMD(7:2)
           MOVE    HKN-TEKSTYMD         TO  HKN-TEKEDYMD
           MOVE    HKNNUM-REC           TO  MCPDATA-REC
           MOVE    "DBSELECT"           TO  MCP-FUNC
           MOVE    "tbl_hknnum"         TO  MCP-TABLE
           MOVE    "key5"               TO  MCP-PATHNAME
           CALL    "ORCDBMAIN"          USING
                                        MCPAREA
                                        MCPDATA-REC
                                        SPA-AREA
           IF  MCP-RC  =  ZERO
               MOVE    "tbl_hknnum"          TO  MCP-TABLE
               MOVE    "key5"                TO  MCP-PATHNAME
               PERFORM  955-HKNNUM-READ-SEC
           ELSE
               DISPLAY  "HKNNUM SELECT ERR"
           END-IF
      *
           INITIALIZE  MCPAREA
           MOVE    "DBCLOSECURSOR"             TO  MCP-FUNC
           MOVE    "tbl_hknnum"          TO  MCP-TABLE
           MOVE    "key5"                TO  MCP-PATHNAME
           CALL    "ORCDBMAIN"           USING
                                         MCPAREA
                                         MCPDATA-REC
                                         SPA-AREA
      *
           .
       950-HKNNUM-SET-EXT.
           EXIT.
      *
      *****************************************************************
      *    保険番号情報読込
      *****************************************************************
       955-HKNNUM-READ-SEC  SECTION.
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
           CALL    "ORCDBMAIN"           USING
                                         MCPAREA
                                         MCPDATA-REC
                                         SPA-AREA
           IF  MCP-RC  =  ZERO
             MOVE  MCPDATA-REC  TO  HKNNUM-REC
             MOVE  ZERO         TO  FLG-HKNNUM
      *
             IF    HKN-HKNNUM  =  "051"
               INITIALIZE                HKN-TANSEIDONAME
               MOVE    "特定疾患"   TO   HKN-TANSEIDONAME
             END-IF
      *
           ELSE
             INITIALIZE  HKNNUM-REC
             MOVE  1  TO  FLG-HKNNUM
           END-IF
      *
           .
       955-HKNNUM-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    請求管理読込
      *****************************************************************
       900-SEIKYU-SELECT-SEC             SECTION.
      *
      *    請求管理ＤＢ検索
           INITIALIZE                  RECE10-REC
      *
           MOVE   SPA-HOSPNUM           TO  RECE10-HOSPNUM
           MOVE   LNK-PRTKANRI-SRYYM    TO    RECE10-SKYYM.
      *
           IF    WRK-PARA-HKNJANUM  NOT = ZERO
               MOVE    WRK-PARA-HKNJANUM  TO  RECE10-XXSTHKNJANUM
                                              RECE10-XXEDHKNJANUM
           ELSE
               MOVE    SPACE            TO  RECE10-XXSTHKNJANUM
               MOVE    "99999999"       TO  RECE10-XXEDHKNJANUM
           END-IF
           IF  WRK-PARA-TOKUBETU  =  "2"
             MOVE  000000               TO  RECE10-SRYYM
             MOVE  999999               TO  RECE10-XXSRYYM
           END-IF
      *
           MOVE    RECE10-REC          TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "tbl_seikyu"        TO  MCP-TABLE
           MOVE    WRK-PATH            TO  MCP-PATHNAME
           CALL    "ORCDBMAIN"             USING
                                           MCPAREA
                                           MCPDATA-REC
                                           SPA-AREA
           IF      MCP-RC              =   ZERO
               PERFORM 900-SEIKYU-READ-SEC
           ELSE
               MOVE    1                   TO  FLG-END
           END-IF
           .
       900-SEIKYU-SELECT-EXT.
           EXIT.
      *
      *****************************************************************
      *    請求マスター読込
      *****************************************************************
       900-SEIKYU-READ-SEC           SECTION.
      *
           MOVE    "tbl_seikyu"            TO  MCP-TABLE
           MOVE    WRK-PATH                TO  MCP-PATHNAME
           MOVE    "DBFETCH"               TO  MCP-FUNC
           CALL    "ORCDBMAIN"             USING
                                           MCPAREA
                                           MCPDATA-REC
                                           SPA-AREA
           IF      MCP-RC              =   ZERO
               MOVE    ZERO            TO  FLG-END
               MOVE    MCPDATA-REC     TO  RECE10-REC
      *
               IF      WRK-PARA-STSRYYM    =   "999999"
                   CONTINUE
               ELSE
                   IF  ( RECE10-SRYYM      <   WRK-PARA-STSRYYM )
                   OR  ( RECE10-SRYYM      >   WRK-PARA-EDSRYYM )
                       GO  TO  900-SEIKYU-READ-SEC
                   END-IF
               END-IF
      *
      *        返戻分でオンライン請求は読み飛ばす
               IF      (WRK-PARA-SYOKBN         =  "2")
               AND     (RECE10-HENREI-SIJI-FLG  =  1)
                   GO  TO  900-SEIKYU-READ-SEC
               END-IF
      *
      *        テスト患者のときは読み飛ばす
               MOVE    SPACE               TO  PTINF-REC
               INITIALIZE                      PTINF-REC
               MOVE    SPA-HOSPNUM         TO  PTINF-HOSPNUM
               MOVE    RECE10-PTID         TO  PTINF-PTID
               MOVE    PTINF-REC           TO  MCPDATA-REC
               PERFORM 800-PTINF-READ-SEC
               IF      FLG-PTINF           =   ZERO
               AND     PTINF-TSTPTNUMKBN   =   "1"
                   GO  TO  900-SEIKYU-READ-SEC
               END-IF
      *
               ADD     1                   TO  CNT-RECE10
           ELSE
               MOVE    1               TO  FLG-END
           END-IF
      *
           .
       900-SEIKYU-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    公費の負担者番号取得
      *****************************************************************
       900-PTKOHINF-SET-SEC  SECTION.
      *
           INITIALIZE  PTKOHINF-REC
           MOVE  SPA-HOSPNUM               TO  PTKOH-HOSPNUM
           MOVE  RECE10-PTID               TO  PTKOH-PTID
           MOVE  WRK-PTKOHNUM              TO  PTKOH-KOHNUM
      *
           MOVE  PTKOHINF-REC              TO  MCPDATA-REC
           MOVE  "DBSELECT"                TO  MCP-FUNC
           MOVE  "tbl_ptkohinf"            TO  MCP-TABLE
           MOVE  "key4"                    TO  MCP-PATHNAME
           CALL  "ORCDBMAIN"               USING
                                           MCPAREA
                                           MCPDATA-REC
                                           SPA-AREA
           IF  MCP-RC  =  ZERO
             MOVE    "tbl_ptkohinf"        TO  MCP-TABLE
             MOVE    "key4"                TO  MCP-PATHNAME
             PERFORM  900-PTKOHINF-READ-SEC
             PERFORM  UNTIL  FLG-PTKOHINF    =  1
                 IF  (PTKOH-TEKSTYMD(1:6)   <=  RECE10-SRYYM
                 AND  PTKOH-TEKEDYMD(1:6)   >=  RECE10-SRYYM)
                 AND  PTKOH-SAKJOKBN    NOT  =  "1"
      *              公費負担者番号で判定
                     IF  PTKOH-FTNJANUM (3:2)  NOT =  "45"
      *                他県公費
                       MOVE  1        TO  FLG-TAKOH
                     END-IF
                     MOVE    1        TO  FLG-PTKOHINF
                 ELSE
                     PERFORM  900-PTKOHINF-READ-SEC
                 END-IF
             END-PERFORM
           END-IF
      *
           INITIALIZE  MCPAREA
           MOVE  "DBCLOSECURSOR"           TO  MCP-FUNC
           MOVE  "tbl_ptkohinf"            TO  MCP-TABLE
           MOVE  "key4"                    TO  MCP-PATHNAME
           CALL  "ORCDBMAIN"               USING
                                           MCPAREA
                                           MCPDATA-REC
                                           SPA-AREA
      *
           .
       900-PTKOHINF-SET-EXT.
           EXIT.
      *
      *****************************************************************
      *    公費の負担者番号取得
      *****************************************************************
       900-PTKOHINF-READ-SEC  SECTION.
      *
           MOVE  "DBFETCH"  TO  MCP-FUNC
           CALL  "ORCDBMAIN"      USING
                                  MCPAREA
                                  MCPDATA-REC
                                  SPA-AREA
           IF  MCP-RC  =  ZERO
             MOVE  MCPDATA-REC  TO  PTKOHINF-REC
             MOVE  ZERO         TO  FLG-PTKOHINF
           ELSE
             INITIALIZE  PTKOHINF-REC
             MOVE  1  TO  FLG-PTKOHINF
           END-IF
      *
           .
       900-PTKOHINF-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    プログラムオプション情報読込
      *****************************************************************
       900-PRGOPTION-READ-SEC  SECTION.
      *
           INITIALIZE                      PRGOPTION-REC
           MOVE    SPA-HOSPNUM         TO  PRGOPTION-HOSPNUM
           MOVE    WRK-PRGID           TO  PRGOPTION-PRGID
           MOVE    WRK-KBNCD           TO  PRGOPTION-KBNCD
           MOVE    PRGOPTION-REC       TO  MCPDATA-REC
           MOVE    "DBSELECT"          TO  MCP-FUNC
           MOVE    "tbl_prgoption"     TO  MCP-TABLE
           MOVE    "key"               TO  MCP-PATHNAME
           CALL    "ORCDBMAIN"          USING
                                        MCPAREA
                                        MCPDATA-REC
                                        SPA-AREA
           IF      MCP-RC              =   ZERO
               MOVE    "DBFETCH"           TO  MCP-FUNC
               MOVE    "tbl_prgoption"     TO  MCP-TABLE
               MOVE    "key"               TO  MCP-PATHNAME
               CALL    "ORCDBMAIN"          USING
                                            MCPAREA
                                            MCPDATA-REC
                                            SPA-AREA
               IF      MCP-RC              =   ZERO
                   MOVE    MCPDATA-REC         TO  PRGOPTION-REC
                   MOVE    ZERO                TO  FLG-PRGOPTION
               ELSE
                   INITIALIZE                      PRGOPTION-REC
                   MOVE    1                   TO  FLG-PRGOPTION
               END-IF
           ELSE
               MOVE    1                   TO  FLG-PRGOPTION
           END-IF
           MOVE    "DBCLOSECURSOR"            TO  MCP-FUNC
           MOVE    "tbl_prgoption"     TO  MCP-TABLE
           MOVE    "key"               TO  MCP-PATHNAME
           CALL    "ORCDBMAIN"          USING
                                        MCPAREA
                                        MCPDATA-REC
                                        SPA-AREA
      *
           .
       900-PRGOPTION-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    患者マスタ読込
      *****************************************************************
       800-PTINF-READ-SEC         SECTION.
      *
           MOVE    "DBSELECT"                 TO  MCP-FUNC
           MOVE    "tbl_ptinf"         TO  MCP-TABLE
           MOVE    "key"               TO  MCP-PATHNAME
           CALL    "ORCDBMAIN"         USING
                                       MCPAREA
                                       MCPDATA-REC
                                       SPA-AREA
           IF      MCP-RC              =   ZERO
               MOVE    "DBFETCH"                  TO  MCP-FUNC
               MOVE    "tbl_ptinf"     TO  MCP-TABLE
               MOVE    "key"           TO  MCP-PATHNAME
               CALL    "ORCDBMAIN"     USING
                                       MCPAREA
                                       MCPDATA-REC
                                       SPA-AREA
               IF      MCP-RC              =   ZERO
                   MOVE    ZERO                TO  FLG-PTINF
                   MOVE    MCPDATA-REC         TO  PTINF-REC
               ELSE
                   MOVE    1                   TO  FLG-PTINF
                   INITIALIZE                  PTINF-REC
               END-IF
           ELSE
               MOVE    1                   TO  FLG-PTINF
               INITIALIZE                  PTINF-REC
           END-IF
      *
           MOVE    "DBCLOSECURSOR"            TO  MCP-FUNC
           MOVE    "tbl_ptinf"         TO  MCP-TABLE
           MOVE    "key"               TO  MCP-PATHNAME
           CALL    "ORCDBMAIN"         USING
                                       MCPAREA
                                       MCPDATA-REC
                                       SPA-AREA
      *
           .
       800-PTINF-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    集計ファイル読込
      *****************************************************************
       800-SKTTMP-READ-SEC             SECTION.
      *
           READ    SKT4510TMP-FILE
               INVALID    KEY
                   MOVE    1           TO  FLG-SKT4510TMP
               NOT INVALID  KEY
                   MOVE    ZERO        TO  FLG-SKT4510TMP
           END-READ
           .
       800-SKTTMP-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    一時ファイル読込
      *****************************************************************
       900-TMP-READ-SEC                SECTION.
      *
           READ  SKT4510TMP-FILE   NEXT
               AT  END
                   MOVE  1  TO  FLG-PRINT
           END-READ
           .
       900-TMP-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    請求管理ＤＢ更新用ファイル読込
      *****************************************************************
       900-UPD-FILE-READ-SEC             SECTION.
      *
           READ    UPD-FILE
               AT  END
                   MOVE    1           TO  FLG-UPDFILE
               NOT AT  END
                   MOVE    ZERO        TO  FLG-UPDFILE
           END-READ
           .
       900-UPD-FILE-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    システム管理マスタ読込
      *****************************************************************
       910-SYSKANRI-INV-SEC         SECTION.
      *
           MOVE    "DBSELECT"                   TO  MCP-FUNC
           MOVE    "tbl_syskanri"           TO  MCP-TABLE
           MOVE    "key10"                  TO  MCP-PATHNAME
           CALL    "ORCDBMAIN"              USING
                                            MCPAREA
                                            MCPDATA-REC
                                            SPA-AREA
           IF      MCP-RC              =   ZERO
               MOVE    "DBFETCH"                    TO  MCP-FUNC
               MOVE    "tbl_syskanri"       TO  MCP-TABLE
               MOVE    "key10"              TO  MCP-PATHNAME
               CALL    "ORCDBMAIN"          USING
                                            MCPAREA
                                            MCPDATA-REC
                                            SPA-AREA
               IF      MCP-RC              =   ZERO
                   MOVE    ZERO                TO  FLG-SYSKANRI
               ELSE
                   MOVE    1                   TO  FLG-SYSKANRI
               END-IF
           ELSE
               MOVE    1                   TO  FLG-SYSKANRI
           END-IF
      *
           MOVE    "DBCLOSECURSOR"              TO  MCP-FUNC
           MOVE    "tbl_syskanri"       TO  MCP-TABLE
           MOVE    "key10"              TO  MCP-PATHNAME
           CALL    "ORCDBMAIN"          USING
                                        MCPAREA
                                        MCPDATA-REC
                                        SPA-AREA
      *
           .
       910-SYSKANRI-INV-EXT.
           EXIT.
      *
      *****************************************************************
      *    請求管理マスタ読込
      *****************************************************************
       910-SEIKYU-INV-SEC         SECTION.
      *
           MOVE    "DBSELECT"                   TO  MCP-FUNC
           MOVE    "tbl_seikyu"         TO  MCP-TABLE
           MOVE    "key"                TO  MCP-PATHNAME
           CALL    "ORCDBMAIN"          USING
                                        MCPAREA
                                        MCPDATA-REC
                                        SPA-AREA
           IF      MCP-RC              =   ZERO
               MOVE    "DBFETCH"                TO  MCP-FUNC
               MOVE    "tbl_seikyu"         TO  MCP-TABLE
               MOVE    "key"                TO  MCP-PATHNAME
               CALL    "ORCDBMAIN"          USING
                                            MCPAREA
                                            MCPDATA-REC
                                            SPA-AREA
               IF      MCP-RC              =   ZERO
                   MOVE    ZERO                TO  FLG-SEIKYU
                   MOVE    MCPDATA-REC         TO  RECE10-REC
               ELSE
                   MOVE    1                   TO  FLG-SEIKYU
               END-IF
           ELSE
               MOVE    1                   TO  FLG-SEIKYU
           END-IF
      *
           MOVE    "DBCLOSECURSOR"                    TO  MCP-FUNC
           MOVE    "tbl_seikyu"         TO  MCP-TABLE
           MOVE    "key"                TO  MCP-PATHNAME
           CALL    "ORCDBMAIN"          USING
                                        MCPAREA
                                        MCPDATA-REC
                                        SPA-AREA
      *
           .
       910-SEIKYU-INV-EXT.
           EXIT.
      *
      *****************************************************************
      *    全角変換処理
      *****************************************************************
       2009-HENKAN-SEC                 SECTION.
      *
           INITIALIZE                  WRK-OUT-INPUT
      *
           INITIALIZE                  ORCSKANACHKAREA
           MOVE    "2"                 TO  KANACHK-SYORI
           MOVE    WRK-MAE-INPUT       TO  KANACHK-MAE-INPUT
           CALL    "ORCSKANACHK"       USING
                                       ORCSKANACHKAREA
           IF      KANACHK-RC          =   ZERO
               MOVE    KANACHK-OUT-INPUT
                                           TO  WRK-OUT-INPUT
           ELSE
               MOVE    WRK-MAE-INPUT       TO  WRK-OUT-INPUT
           END-IF
      *
           .
       2009-HENKAN-EXT.
           EXIT.
      *
      *****************************************************************
      *    テーブルアクセス処理
      *****************************************************************
       900-ORCDBMAIN-SEC               SECTION.
      *
           CALL    "ORCDBMAIN"         USING   MCPAREA
                                               MCPDATA-REC
                                               SPA-AREA
           .
      *
       900-ORCDBMAIN-EXT.
           EXIT.
      *
      *****************************************************************
      *    システム管理マスタ読込（ＮＥＸＴ）
      *****************************************************************
       900-SYSKANRI-READ-N-SEC         SECTION.
      *
           PERFORM 900-DBFETCH-SEC
           IF      MCP-RC              =   ZERO
               MOVE    ZERO                TO  FLG-SYSKANRI
           ELSE
               MOVE    1                   TO  FLG-SYSKANRI
           END-IF
           .
       900-SYSKANRI-READ-N-EXT.
           EXIT.
      *
      *****************************************************************
      *    ＤＢＦＥＴＣＨ処理
      *****************************************************************
       900-DBFETCH-SEC                SECTION.
      *
           MOVE    "DBFETCH"           TO  MCP-FUNC
           PERFORM 900-ORCDBMAIN-SEC
      *
           .
       900-DBFETCH-EXT.
           EXIT.
      *
      *****************************************************************
      *    医療機関編集情報読み込み
      *****************************************************************
       900-1900-READ-SEC           SECTION.
      *
      *    医療機関編集情報読み込み
           MOVE    SPACE                   TO  SYS-1900-REC
           INITIALIZE                          SYS-1900-REC
           MOVE    "1900"                  TO  SYS-1900-KANRICD
           MOVE    "*"                     TO  SYS-1900-KBNCD
           MOVE    SPA-HOSPNUM             TO  SYS-1900-HOSPNUM
           IF  WRK-PARA-STSRYYM  =  "999999"
               MOVE    LNK-PRTKANRI-SRYYM  TO  SYS-1900-STYUKYMD(1:6)
                                               SYS-1900-EDYUKYMD(1:6)
           ELSE
               MOVE    WRK-PARA-EDSRYYM    TO  SYS-1900-STYUKYMD(1:6)
                                               SYS-1900-EDYUKYMD(1:6)
           END-IF
           MOVE    "01"                    TO  SYS-1900-STYUKYMD(7:2)
                                               SYS-1900-EDYUKYMD(7:2)
           MOVE    SYS-1900-REC            TO  MCPDATA-REC
           PERFORM 910-SYSKANRI-INV-SEC
           IF      FLG-SYSKANRI        =   ZERO
               MOVE    MCPDATA-REC         TO  SYS-1900-REC
               IF      SYS-1900-PRTKBN(8) NOT =   SPACE
      *            医療機関名称編集情報
                   PERFORM 900-1901-READ-SEC
      *            医療機関住所編集情報
                   PERFORM 900-1902-READ-SEC
      *            医療機関住所編集情報（２）
                   PERFORM 900-1903-READ-SEC
               END-IF
           ELSE
               INITIALIZE                  SYS-1900-REC
           END-IF
           .
      *
       900-1900-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    医療機関名称編集情報読み込み
      *****************************************************************
       900-1901-READ-SEC           SECTION.
      *
      *    医療機関名称編集情報読み込み
           MOVE    SPACE               TO  SYS-1901-REC
           INITIALIZE                      SYS-1901-REC
           MOVE    "1901"              TO  SYS-1901-KANRICD
           MOVE    SYS-1900-PRTKBN(8)  TO  SYS-1901-KBNCD
           MOVE    SPA-HOSPNUM         TO  SYS-1901-HOSPNUM
           IF  WRK-PARA-STSRYYM  =  "999999"
               MOVE    LNK-PRTKANRI-SRYYM  TO  SYS-1901-STYUKYMD(1:6)
                                               SYS-1901-EDYUKYMD(1:6)
           ELSE
               MOVE    WRK-PARA-EDSRYYM    TO  SYS-1901-STYUKYMD(1:6)
                                               SYS-1901-EDYUKYMD(1:6)
           END-IF
           MOVE    "01"                TO  SYS-1901-STYUKYMD(7:2)
                                           SYS-1901-EDYUKYMD(7:2)
           MOVE    SYS-1901-REC        TO  MCPDATA-REC
           PERFORM 910-SYSKANRI-INV-SEC
           IF      FLG-SYSKANRI        =   ZERO
               MOVE    MCPDATA-REC         TO  SYS-1901-REC
               MOVE    SYS-1901-HOSPNAME1  TO  WRK-HOSPNAME1
               MOVE    SYS-1901-HOSPNAME2  TO  WRK-HOSPNAME2
           ELSE
               INITIALIZE                  SYS-1901-REC
           END-IF
           .
      *
       900-1901-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    医療機関住所編集情報読み込み
      *****************************************************************
       900-1902-READ-SEC           SECTION.
      *
      *    医療機関住所編集情報読み込み
           MOVE    SPACE               TO  SYS-1902-REC
           INITIALIZE                      SYS-1902-REC
           MOVE    "1902"              TO  SYS-1902-KANRICD
           MOVE    SYS-1900-PRTKBN(8)  TO  SYS-1902-KBNCD
           MOVE    SPA-HOSPNUM         TO  SYS-1902-HOSPNUM
           IF  WRK-PARA-STSRYYM  =  "999999"
               MOVE    LNK-PRTKANRI-SRYYM  TO  SYS-1902-STYUKYMD(1:6)
                                               SYS-1902-EDYUKYMD(1:6)
           ELSE
               MOVE    WRK-PARA-EDSRYYM    TO  SYS-1902-STYUKYMD(1:6)
                                               SYS-1902-EDYUKYMD(1:6)
           END-IF
           MOVE    "01"                TO  SYS-1902-STYUKYMD(7:2)
                                           SYS-1902-EDYUKYMD(7:2)
           MOVE    SYS-1902-REC        TO  MCPDATA-REC
           PERFORM 910-SYSKANRI-INV-SEC
           IF      FLG-SYSKANRI        =   ZERO
               MOVE    MCPDATA-REC     TO  SYS-1902-REC
               MOVE    SYS-1902-ADRS1  TO  WRK-HOSPADRS1
               MOVE    SYS-1902-ADRS2  TO  WRK-HOSPADRS2
               MOVE    SYS-1902-TEL    TO  WRK-HOSPTEL
           ELSE
               INITIALIZE                  SYS-1902-REC
           END-IF
           .
      *
       900-1902-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    医療機関住所編集情報読み込み
      *****************************************************************
       900-1903-READ-SEC           SECTION.
      *
           MOVE    SPACE               TO  SYS-1903-REC
           INITIALIZE                      SYS-1903-REC
           MOVE    SPA-HOSPNUM         TO  SYS-1903-HOSPNUM
           MOVE    "1903"              TO  SYS-1903-KANRICD
           MOVE    SYS-1900-PRTKBN(8)  TO  SYS-1903-KBNCD
           IF  WRK-PARA-STSRYYM  =  "999999"
               MOVE    LNK-PRTKANRI-SRYYM  TO  SYS-1903-STYUKYMD(1:6)
           ELSE
               MOVE    WRK-PARA-EDSRYYM    TO  SYS-1903-STYUKYMD(1:6)
           END-IF
           MOVE    "01"                TO  SYS-1903-STYUKYMD(7:2)
           MOVE    SYS-1903-STYUKYMD   TO  SYS-1903-EDYUKYMD
           MOVE    SYS-1903-REC        TO  MCPDATA-REC
           PERFORM 910-SYSKANRI-INV-SEC
           IF      FLG-SYSKANRI        =   ZERO
               MOVE    MCPDATA-REC     TO  SYS-1903-REC
               MOVE    SYS-1903-KAISETUNAME
                                       TO  WRK-KAISETUNAME
           ELSE
               INITIALIZE                  SYS-1903-REC
           END-IF
           .
      *
       900-1903-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    ＤＢ　オープン処理
      *****************************************************************
       100-DBOPEN-SEC                SECTION.
      *
           MOVE    LOW-VALUE           TO  MCP-TABLE
                                           MCP-PATHNAME
           MOVE    "DBOPEN"            TO  MCP-FUNC.
           CALL    "ORCDBMAIN"         USING
                                       MCPAREA
                                       MCPDATA-REC
                                       SPA-AREA
      *
           MOVE    LOW-VALUE           TO  MCP-TABLE
                                           MCP-PATHNAME
           MOVE    "DBSTART"           TO  MCP-FUNC.
           CALL    "ORCDBMAIN"         USING
                                       MCPAREA
                                       MCPDATA-REC
                                       SPA-AREA
           .
       100-DBOPEN-EXT.
           EXIT.
      *
      *****************************************************************
      *    ＤＢ　クローズ処理
      *****************************************************************
       900-DBDISCONNECT-SEC            SECTION.
      *
           MOVE    LOW-VALUE           TO  MCP-TABLE
                                           MCP-PATHNAME
           MOVE    "DBCOMMIT"          TO  MCP-FUNC.
           CALL    "ORCDBMAIN"         USING
                                       MCPAREA
                                       MCPDATA-REC
                                       SPA-AREA
      *
           MOVE    LOW-VALUE           TO  MCP-TABLE
                                           MCP-PATHNAME
           MOVE    "DBDISCONNECT"      TO  MCP-FUNC.
           CALL    "ORCDBMAIN"         USING
                                       MCPAREA
                                       MCPDATA-REC
                                       SPA-AREA
           .
       900-DBDISCONNECT-EXT.
           EXIT.
