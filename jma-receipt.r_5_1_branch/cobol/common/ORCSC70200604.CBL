      *******************************************************************
      * Project code name "ORCA"
      * 日医標準レセプトソフト（JMA standard receipt software）
      * Copyright(C) 2002 JMA (Japan Medical Association)
      *
      * This program is part of "JMA standard receipt software".
      *
      *     This program is distributed in the hope that it will be useful
      * for further advancement in medical care, according to JMA Open
      * Source License, but WITHOUT ANY WARRANTY.
      *     Everyone is granted permission to use, copy, modify and
      * redistribute this program, but only under the conditions described
      * in the JMA Open Source License. You should have received a copy of
      * this license along with this program. If not, stop using this
      * program and contact JMA, 2-28-16 Honkomagome, Bunkyo-ku, Tokyo,
      * 113-8621, Japan.
      *******************************************************************
       IDENTIFICATION              DIVISION.
       PROGRAM-ID.                 ORCSC70200604.
      *****************************************************************
      *  システム名        : ＯＲＣＡ
      *  サブシステム名    : 診療行為入力
      *  コンポーネント名  : 診療行為チェック、算定（画像診断）
      *                      レセ電対応（Ｈ１７年？月から）
      *  管理者            : 
      *  作成日付    作業者        記述
      *  00/12/01    MCC−多々納   新規作成
      *****************************************************************
      *  プログラム修正履歴
      * Maj/Min/Rev  修正者       日付      内容
      * 01.00.01     MCC-多々納   01/05/23  フィルム料、薬剤料計算修正
      * 01.00.02     MCC-多々納   02/03/18  施設不適合追加
      * 01.00.03     MCC-多々納   02/04/25  撮影料に回数入力追加
      * 01.00.04     MCC-多々納   02/04/**  労災用追加
      * 01.00.05     NACL-多々納  02/09/27  診断自動発生追加
      * 01.00.06     NACL-多々納  02/10/29  入院対応
      * 01.00.07     NACL−多々納 02/11/22  点滴注射同時重複エラー追加
      * 01.00.08     NACL−多々納 02/12/24  フィルム料を手技料へ加算
      * 01.00.09     NACL-多々納  03/01/16  撮影量回数訂正時修正
      * 01.00.11     NACL-多々納  03/02/18  フィルム料を手技料２へ
      * 01.00.12     NACL-多々納  03/02/26  点数ゼロのきざみ値計算の修正
      * 01.00.13     NACL-多々納  03/04/25  単純撮影に間接撮影などを追加
      * 01.00.14     NACL-多々納  03/07/30  同時併施点数計算修正他
      * 01.00.15     NACL-多々納  03/09/26  ＣＴ、ＭＲＩ同時入力追加
      * 01.00.16     NACL-多々納  03/12/03  同時併施の単独入力警告へ
      * 01.00.17     NACL-多々納  04/12/17  ＣＴ、ＭＲＩの造影剤判定修正
      * 01.00.18     NACL-多々納  05/01/27  酸素追加
      * 02.00.01     NACL-多々納  05/04/27  レセ電対応
      * 02.00.02     NACL-多々納  05/12/14  MONFUNC 対応
      * 02.00.03     NACL-多々納  06/03/16  Ｈ１８年４月改正追加
      * 03.05.00     NACL-多々納  07/04/XX  グループ診療対応
      *****************************************************************
      *
      *
       ENVIRONMENT                 DIVISION.
       CONFIGURATION               SECTION.
       INPUT-OUTPUT                SECTION.
       FILE-CONTROL.
      *
      *
       DATA                        DIVISION.
      *FILE                        SECTION.
      *
       WORKING-STORAGE             SECTION.
      *
      *    フラグ領域
       01  FLG-AREA.
           03  FLG-END             PIC 9(01).
           03  FLG-SANTEI          PIC 9(01).
           03  FLG-TENSU           PIC 9(01).
           03  FLG-SYSKANRI        PIC 9(01).
      *
           03  FLG-ERR             PIC 9(01).
           03  FLG-ARI             PIC 9(01).
           03  FLG-ADD             PIC 9(01).
           03  FLG-ARIOK           PIC 9(01).
           03  FLG-MADOKU          PIC 9(01).
           03  FLG-SEIBETU         PIC 9(01).
           03  FLG-BUIKBN          PIC 9(01).
           03  FLG-JIKASAN         PIC 9(01).
      *    乳幼児加算有り
           03  FLG-NYUYOJI         PIC 9(01).
      *    造影剤加算有り
           03  FLG-ZOEIZAI         PIC 9(01).
      *    自動追加種別
           03  FLG-SATU             PIC 9(01).
      *    ＣＴ有り
           03  FLG-XSEN             PIC 9(01).
           03  FLG-KAKUI            PIC 9(01).
           03  FLG-CTMRI            PIC 9(01).
      *
           03  FLG-SINDAN           PIC 9(01).
      *
           03  FLG-CHK              PIC 9(01).
      *
           03  FLG-SYORI            PIC 9(01).
      *    施設基準不適合
           03  FLG-KIJUNN          PIC 9(01).
      *    遠隔画像診断加算
           03  FLG-ENKALL          PIC 9(01).
           03  FLG-ENK1            PIC 9(01).
           03  FLG-ENK2            PIC 9(01).
           03  FLG-ENK3            PIC 9(01).
      *    点滴のチェック
           03  FLG-TENTEKI         PIC 9(01).
      *    診断料点数逓減
           03  FLG-SATU-GEN        PIC 9(01).
      *
           03  FLG-NENREI          PIC 9(01).
           03  FLG-SYUGI           PIC 9(01).
      *
      *    添字領域
       01  IDX-AREA.
           03  IDX                 PIC 9(04).
           03  IDY                 PIC 9(04).
           03  IDX1                PIC 9(04).
           03  IDX2                PIC 9(04).
           03  IDXS                PIC 9(04).
           03  IDXS1               PIC 9(04).
           03  IDX-Z               PIC 9(04).
           03  IDX-Z2              PIC 9(04).
           03  IDX-T               PIC 9(04).
           03  IDX-S               PIC 9(04).
           03  IDX-SAT             PIC 9(04).
           03  IDX-SAT2            PIC 9(04).
           03  IDX-SAT3            PIC 9(04).
           03  IDX-SYU             PIC 9(04).
           03  IDX-SA              PIC 9(04).
           03  IDX-KAS             PIC 9(04).
           03  IDX-SIN             PIC 9(04).
      *
           03  IDX-GEN             PIC 9(04).
           03  IDX-GEN2            PIC 9(04).
           03  IDX-NYU             PIC 9(04).
           03  IDX-GAI             PIC 9(04).
      *
           03  IDX-9               PIC 9(04).
           03  IDX-Y               PIC 9(04).
           03  IDX-Y2              PIC 9(02).
           03  IDX-CT              PIC 9(04).
           03  IDX-BUI             PIC 9(04).
      *    ＭＡＸ行
           03  IDX-MAX             PIC 9(04).
      *    Ｘ線の開始行
           03  IDX-HOZ             PIC 9(04).
           03  IDX-HOZ2            PIC 9(04).
      *    行削除
           03  IDX-D1              PIC 9(04).
           03  IDX-D2              PIC 9(04).
      *
           03  IDX-K               PIC 9(04).
      *    同時併施
           03  IDX-S1              PIC 9(04).
           03  IDX-DOJI1           PIC 9(04).
           03  IDX-DOJI2           PIC 9(04).
      *
           03  IDX-XSEN             PIC 9(04).
           03  IDX-KAKUI            PIC 9(04).
           03  IDX-CTMRI            PIC 9(04).
      *
           03  IDX-STR              PIC 9(04).
           03  HZN-IDX              PIC 9(04).
      *
      *    一時領域
       01  WRK-AREA.
           03  WRK-MOJI            PIC X(01). 
           03  WRK-SRYCD           PIC X(09). 
           03  WRK-SRYCD-2         PIC X(09). 
           03  WRK-HEN-SRYCD       PIC X(09). 
           03  WRK-KEIMAI          PIC 9(08).
           03  WRK-TENSU           PIC 9(08)V99.
           03  WRK-TENSU1          PIC 9(08).
           03  WRK-IDX             PIC 9(04).
           03  WRK-TENSUKEI        PIC 9(08).
      *
           03  WRK-SRYCD1          PIC X(09). 
           03  WRK-SRYCD2          PIC X(09). 
      *
           03  WRK-TENSU-KEISAN.
               05  WRK-SUM-TENSUKEI    PIC 9(08)V99.
               05  WRK-SUM-TENGAKEI    PIC 9(08)V99.
      *        撮影料の点数計
               05  WRK-TENSUKEI1       PIC S9(08)V999.
      *        診断料の点数計
               05  WRK-TENSUKEI2       PIC S9(08)V999.
      *        その他の点数計
               05  WRK-TENSUKEI3       PIC S9(08)V999.
      *
           03  WRK-SYOKEIS         PIC 9(08)V99.
           03  WRK-SYOKEIS2        PIC 9(08)V9999.
           03  WRK-SYOKEIS3        PIC 9(08)V9999.
           03  WRK-SYOKEI          PIC 9(08).
      *    点数計算用
           03  WRK-KISOTEN-9           PIC 9(07)V9(03).
           03  WRK-KISOTEN             PIC 9(07).
           03  WRK-KISOTEN-G           PIC 9(07).
      *    逓減点数
           03  WRK-GEN-TEN             PIC 9(07)V9(03).
           03  WRK-GENTEN              PIC 9(07)V9(03).
      *    撮影料回数
           03  WRK-CNT-SATUEI          PIC 9(03).
      *
      *    乳幼児加算用ワーク
           03  WRK-NYUYOJI-AREA.
               05  WRK-NYU-BUI     PIC X(01).
               05  WRK-NYU-CD      PIC X(01).
               05  WRK-NYU-FIR     PIC X(01).
               05  WRK-NYU-IDX     PIC 9(04).
               05  WRK-NYU-TEN     PIC 9(08).
      *
               05  WRK-FIRTEN      PIC 9(08).
      *
           03  WRK-TENSUKEIS       PIC 9(08)V999.
           03  WRK-TENSUKEI-9      PIC S9(08).
           03  WRK-TENSUKEI-9G     PIC S9(08).
           03  WRK-TENSUKEI-G      PIC S9(08).
           03  WRK-TENSUKEI-K      PIC S9(08).
      *
           03  WRK-FIRTEN-KEI      PIC 9(08).
      *
           03  WRK-TENSU-X.
               05  WRK-TENSU-Z     PIC ZZZZZZZZ.
           03  WRK-KAISU-X.
               05  WRK-KAISU-Z     PIC ZZZZ.
      *
      *   きざみ値行為再計算用
           03  WRK-SATU-TEN        PIC 9(08)V9(03).
           03  WRK-KEI-TEN         PIC 9(08)V9(03).
           03  WRK-KEI-TEN1        PIC 9(08)V9(03).
           03  WRK-KEI-TEN2        PIC 9(08).
           03  WRK-SURYO           PIC 9(05)V9(03).
           03  WRK-SURYO-R         REDEFINES   WRK-SURYO.
               05  WRK-SURYO-S1    PIC 9(05).
               05  WRK-SURYO-S2    PIC 9(03).
           03  WRK-KZMTEN          PIC 9(08)V9(03).
      *    乳幼児加算割合
           03  WRK-SATU-KASAN      PIC 9(03)V9(03).
      *    撮影料の数量（診断料へ）
           03  WRK-SATUEI-SURYO    PIC 9(05)V9(03).
      *
      *    減コード
           03 WRK-GEN-SRYCD        PIC X(09).
      *
      *    Ｘ線診断料
           03  WRK-XSEN-SRYCD-I     PIC X(09).
           03  WRK-XSEN-SRYCD-R     PIC X(09).
      *
      *    ＣＴ，ＭＲＩ回数
           03  WRK-CTMRI-CNT-G.
               05  WRK-CTMRI-CNT-TBL            OCCURS  3.
                   07  WRK-CTMRI-CNT   PIC 9(04).
                   07  WRK-CTMRI-DCNT  PIC 9(04).
           03  WRK-CTMRI-KAI       PIC 9(03).
      *
      *    警告メッセージ
           03  WRK-KIDCD           PIC X(04).
      *
           03  WRK-ERRCD           PIC X(04).
      *
           03  WRK-SRYKA           PIC X(02).
           03  WRK-HKNCOMBI        PIC X(04).
      *!
      *    酸素計算用
           03  WRK-SANSO               PIC 9(08).
           03  WRK-SANSOKIN            PIC 9(08)V99.
           03  WRK-SANKEI              PIC 9(08).
           03  WRK-SANTEN              PIC 9(08).
           03  WRK-SANSOKIN-SUM        PIC 9(08)V99.
           03  IDX-SANSO               PIC 9(04).
      *
      *    入院日
       01  WRK-NYUINDAY-AREA.
           03  WRK-XSEN-DAY            PIC 9(02).
           03  WRK-CTMRI-DAY           PIC 9(02).
           03  WRK-KAKUI-DAY           PIC 9(02).
           03  WRK-NYU-DAY             PIC 9(02).
           03  IDN1                    PIC 9(02).
           03  IDN2                    PIC 9(02).
           03  FLG-XSEN-ARI            PIC 9(01).
           03  FLG-CTMRI-ARI           PIC 9(01).
           03  FLG-KAKUI-ARI           PIC 9(01).
      *
      *    保存用
       01  HOZ-SCR-REC.
           COPY    "CPK02SPASCR.INC"  REPLACING
                                       //SPA-// BY //HOZ-//.
      *
      *時間外コメント（日時分）
       01  CONS-JIKAN                  PIC X(09)   VALUE   "840000114".
      *
      *   Ｘ線同時併施（診断・撮影）
       01  TBL-DOUJI-HEISI-AREA.
      *    特殊撮影
           03  FILLER              PIC X(09)  VALUE   "170005530".
           03  FILLER              PIC X(09)  VALUE   "170005630".
      *    動態撮影
           03  FILLER              PIC X(09)  VALUE   "170005730".
           03  FILLER              PIC X(09)  VALUE   "170005830".
      *    重複撮影
           03  FILLER              PIC X(09)  VALUE   "170006130".
           03  FILLER              PIC X(09)  VALUE   "170006230".
      *    立体撮影
           03  FILLER              PIC X(09)  VALUE   "170006330".
           03  FILLER              PIC X(09)  VALUE   "170006430".
      *    パントモグラフィー
           03  FILLER              PIC X(09)  VALUE   "170006530".
           03  FILLER              PIC X(09)  VALUE   "170006630".
      *    パノララマＸ線
           03  FILLER              PIC X(09)  VALUE   "170006730".
           03  FILLER              PIC X(09)  VALUE   "170006830".
      *    断層撮影
           03  FILLER              PIC X(09)  VALUE   "170006930".
           03  FILLER              PIC X(09)  VALUE   "170007030".
      *    同時多層撮影
           03  FILLER              PIC X(09)  VALUE   "170007130".
           03  FILLER              PIC X(09)  VALUE   "170007200".
      *    回転横断撮影
           03  FILLER              PIC X(09)  VALUE   "170007330".
           03  FILLER              PIC X(09)  VALUE   "170007430".
      *    スポット撮影
           03  FILLER              PIC X(09)  VALUE   "170007530".
           03  FILLER              PIC X(09)  VALUE   "170007630".
      *    狙撃撮影
           03  FILLER              PIC X(09)  VALUE   "170007730".
           03  FILLER              PIC X(09)  VALUE   "170007830".
      *    曲面断層撮影
           03  FILLER              PIC X(09)  VALUE   "170007930".
           03  FILLER              PIC X(09)  VALUE   "170008030".
      *    児頭骨盤不均衡撮影
           03  FILLER              PIC X(09)  VALUE   "170008430".
           03  FILLER              PIC X(09)  VALUE   "170008530".
      *    特殊撮影（撮影・診断）新生児
           03  FILLER              PIC X(09)  VALUE   "170019130".
           03  FILLER              PIC X(09)  VALUE   "170019330".
      *    特殊撮影（撮影・診断）乳幼児
           03  FILLER              PIC X(09)  VALUE   "170019230".
           03  FILLER              PIC X(09)  VALUE   "170019430".
      *****単純撮影・特殊撮影
      *    03  FILLER              PIC X(09)  VALUE   "170001910".
      *****03  FILLER              PIC X(09)  VALUE   "170002010".
      *
       01  TBL-DOUJI-HEISI-R       REDEFINES  TBL-DOUJI-HEISI-AREA.
           03  TBL-DOUJI-OCC       OCCURS     15  INDEXED  TBL-IDX.
               05  TBL-DOUJI-1     PIC X(09).
               05  TBL-DOUJI-2     PIC X(09).
      *
      *    単純撮影
       01  TBL-XSEN-AREA.
      *    単純撮影
           03  FILLER              PIC X(09)  VALUE   "170001910".
      *    （イ）
           03  FILLER              PIC X(11)  VALUE   "I 170000410".
      *    （ロ）
           03  FILLER              PIC X(11)  VALUE   "R 170000510".
      *
      *    単純撮影（手前）
           03  FILLER              PIC X(09)  VALUE   "170021750".
      *    （イ）
           03  FILLER              PIC X(11)  VALUE   "I 170021550".
      *    （ロ）
           03  FILLER              PIC X(11)  VALUE   "R          ".
      *
      *    単純撮影（短期）
           03  FILLER              PIC X(09)  VALUE   "170023950".
      *    （イ）
           03  FILLER              PIC X(11)  VALUE   "I 170023750".
      *    （ロ）
           03  FILLER              PIC X(11)  VALUE   "R 170024250".
      *
      *    単純間接
           03  FILLER              PIC X(09)  VALUE   "170002410".
      *    （イ）
           03  FILLER              PIC X(11)  VALUE   "I 170000910".
      *    （ロ）
           03  FILLER              PIC X(11)  VALUE   "R 170001010".
      *
      *    単純間接（手前）
           03  FILLER              PIC X(09)  VALUE   "170021850".
      *    （イ）
           03  FILLER              PIC X(11)  VALUE   "I 170021650".
      *    （ロ）
           03  FILLER              PIC X(11)  VALUE   "R          ".
      *
      *    単純間接（短期）
           03  FILLER              PIC X(09)  VALUE   "170024050".
      *    （イ）
           03  FILLER              PIC X(11)  VALUE   "I 170023850".
      *    （ロ）
           03  FILLER              PIC X(11)  VALUE   "R 170024350".
      *
      *    乳房撮影
           03  FILLER              PIC X(09)  VALUE   "170027010".
      *    （イ）
           03  FILLER              PIC X(11)  VALUE   "I 170026910".
      *    （ロ）
           03  FILLER              PIC X(11)  VALUE   "R 170026910".
      *
      *
       01  TBL-XSEN-AREA-R         REDEFINES  TBL-XSEN-AREA.
           03  TBL-XSEN-OCC        OCCURS   7   INDEXED  TBL-XIDX.
               05  TBL-XSEN-SRYCD      PIC X(09).
               05  TBL-XSEN-KBN1       PIC X(02).
               05  TBL-XSEN-SRYCD-I    PIC X(09).
               05  TBL-XSEN-KBN2       PIC X(02).
               05  TBL-XSEN-SRYCD-R    PIC X(09).
      *
      *    年齢別コード（診断・撮影）
       01  TBL-DOUJI-NEBREI-AREA.
      *    特殊撮影
           03  FILLER              PIC X(09)  VALUE   "170005530".
           03  FILLER              PIC X(09)  VALUE   "170019130".
           03  FILLER              PIC X(09)  VALUE   "170019230".
      *    特殊撮影（同時併施）
           03  FILLER              PIC X(09)  VALUE   "170005630".
           03  FILLER              PIC X(09)  VALUE   "170019330".
           03  FILLER              PIC X(09)  VALUE   "170019430".
      *    Ｉ．Ｉ間接撮影
           03  FILLER              PIC X(09)  VALUE   "170011430".
           03  FILLER              PIC X(09)  VALUE   "170019730".
           03  FILLER              PIC X(09)  VALUE   "170019830".
      *
       01  TBL-DOUJI-NEBREI-AREA-R   REDEFINES  TBL-DOUJI-NEBREI-AREA.
           03  TBL-DOUJI-NEBREI-OCC    OCCURS   3   INDEXED  TBL-NIDX.
      *        一般
               05  TBL-NEN-SRYCD0      PIC X(09).
      *        新生児
               05  TBL-NEN-SRYCD1      PIC X(09).
      *        乳幼児
               05  TBL-NEN-SRYCD2      PIC X(09).
      *
      *    酸素コードテーブル
           COPY     "CMSANSOTBL.INC".
      *
      *****************************************************************
      *    ファイルレイアウト
      *****************************************************************
      *
      *    システム管理マスタ（施設基準）
           COPY    "CPSK1006.INC".
      *
      *    点数マスタ−
      *01  TENSU-REC.
           COPY    "CPTENSU.INC".
      *
      *    算定履歴
       01  SANTEI-REC.
           COPY    "CPSANTEI.INC".
      *
      *****************************************************************
      *    サブプロ用　領域
      *****************************************************************
      *
      *    診療行為算定編集パラメタ
           COPY    "CPORCSC90TEN.INC".
      *
      *    診療行為算定歴チェックパラメタ
           COPY    "CPORCSC91.INC".
      *
      *    診療行為点数編集・基本チェック
           COPY    "CPORCSC92.INC".
      *
      *01  MCPDATA-REC                 PIC X(5000).
           COPY    "MCPDATA.INC".
      **** COPY    "CPORCMCP.INC".
      *
      *    診療行為用共通パラメタ
           COPY    "K01COMMON-SPA".
      *
      *****************************************************************
      *    連絡　領域
      *****************************************************************
        LINKAGE                     SECTION.
      *
           COPY    "MCPAREA".
      *
      *    診療行為算定用共通パラメタ
           COPY    "CPORCSC00.INC".
      *
       01  WRK-SCR-REC.
           COPY    "CPK02SPASCR.INC"  REPLACING
                                       //SPA-// BY //WRK-//.
      *
      *    スパ領域
           COPY    "COMMON-SPA".
      *
       PROCEDURE                   DIVISION    USING
           MCPAREA
           ORCSC00AREA
           WRK-SCR-REC
           SPA-AREA
           .
      *
      *****************************************************************
      *    主　　処理
      *****************************************************************
       000-PROC-SEC                SECTION.
      *
           INITIALIZE                  FLG-AREA
           INITIALIZE                  IDX-AREA
           INITIALIZE                  WRK-AREA
      *
           EVALUATE    LNK-ORCSC-KBN
      *        剤別　チェック、編集、自動算定
               WHEN    "1"
                   PERFORM 100-SEL-SYORI-SEC
      *        全体のチェック
               WHEN    "2"
                   PERFORM 200-ALL-SYORI-SEC
      *        自動算定
               WHEN    "3"
                   PERFORM 300-AUTO-SYORI-SEC
               WHEN    OTHER
                   MOVE    "1001"          TO  SPA-ERRCD
           END-EVALUATE
      *
           .
           EXIT    PROGRAM
           .
      *****************************************************************
      *    剤別のチェック、編集、自動算定処理
      *****************************************************************
       100-SEL-SYORI-SEC                SECTION.
      *
      *    チェック、自動算定
           PERFORM 1001-NAI-CHK-SEC
           IF     (WRK-GMN-INPUTCD(IDX-MAX)(1:1)
                                       =   "."   )  OR
                  (SPA-ERRCD       NOT =   SPACE )
               GO      TO      100-SEL-SYORI-EXT
           END-IF
      *
      *    点数計算
           PERFORM 1002-TENSU-KEI-SEC
      *
           .
       100-SEL-SYORI-EXT.
           EXIT.
      *
      *****************************************************************
      *    明細別内容チェック、自動算定編集処理
      *****************************************************************
       1001-NAI-CHK-SEC                  SECTION.
      *
           MOVE    ZERO                TO  IDX-MAX
           MOVE    ZERO                TO  IDX-BUI
           MOVE    ZERO                TO  IDX-S
           MOVE    ZERO                TO  IDX-SAT
           MOVE    ZERO                TO  IDX-GEN
           MOVE    ZERO                TO  IDX-GEN2
           MOVE    ZERO                TO  IDX-NYU
           MOVE    1                   TO  IDX-HOZ
      *
           MOVE    ZERO                TO  FLG-END
           MOVE    ZERO                TO  FLG-SATU
           MOVE    ZERO                TO  FLG-BUIKBN
           MOVE    ZERO                TO  FLG-ZOEIZAI
           MOVE    ZERO                TO  FLG-SINDAN
           MOVE    ZERO                TO  FLG-KIJUNN
           MOVE    ZERO                TO  WRK-KEIMAI
           INITIALIZE                  WRK-NYUYOJI-AREA
           INITIALIZE                  WRK-AREA
      *
           PERFORM VARYING     IDX-Z   FROM    1   BY  1
                   UNTIL      (IDX-Z       >   SPA-MAX-LINE      )   OR
                              (WRK-GMN-INPUTCD (IDX-Z) =   SPACE )  OR
                              (WRK-ERRCD       NOT =   SPACE )
      *
               ADD     1                   TO  IDX-MAX
      *
               IF      WRK-GMN-INPUTCD(IDX-Z)(1:1)     =   "." OR "S"
                                                       OR  "*"
                   MOVE    WRK-GMN-INPUTCD(IDX-Z)(1:1) TO  WRK-MOJI
               ELSE
                   MOVE    WRK-COM-INPUTCD(IDX-Z)(1:1) TO  WRK-MOJI
                   IF      WRK-COM-INPUTCD(IDX-Z)(1:3) =   "059"
                       MOVE    "7"                     TO  WRK-MOJI
                   END-IF
               END-IF
      *
               EVALUATE    WRK-MOJI
                   WHEN    "8"
                   WHEN    "."
                   WHEN    "S"
                   WHEN    "*"
                       CONTINUE
      *
                   WHEN    OTHER
      *                編集チェックへ
                       PERFORM 10011-CHK-SEC
               END-EVALUATE
           END-PERFORM
           IF      IDX-SAT             >   ZERO
      *        自動加算を追加する
               PERFORM 1002-JIDO-KASAN-SEC
           END-IF
      *
      *    酸素の補正率を自動発生する
           IF     (WRK-ERRCD           =   SPACE)   AND
                  (IDX-SANSO           >   ZERO )
               PERFORM 10011-SANSO-ADD-SEC
           END-IF
      *
           IF      WRK-ERRCD           =   SPACE
               MOVE    WRK-KIDCD           TO  WRK-ERRCD
           END-IF
      *
           MOVE    WRK-ERRCD          TO  SPA-ERRCD
      *
           .
       1001-NAI-CHK-EXT.
           EXIT.
      *
      *****************************************************************
      *    明細別内容チェック、自動算定編集処理
      *****************************************************************
       10011-CHK-SEC                  SECTION.
      *
      *    各チェック処理
           EVALUATE    WRK-MOJI
               WHEN    "0"
      *            部位コードチェック
                   IF      WRK-COM-INPUTCD(IDX-Z)(1:3)   =   "002"
                       IF      FLG-BUIKBN      NOT =   ZERO
                           MOVE    "0011"          TO  WRK-ERRCD
                           MOVE    "1"             TO  WRK-COM-CUR
                                                            (IDX-Z)
                       ELSE
                           EVALUATE    WRK-COM-BUIKBN (IDX-Z)
      *                        頭部
                               WHEN    "1"
      *                        躯幹
                               WHEN    "2"
                                   MOVE    2       TO  FLG-BUIKBN
      *                        胸部・腹部
                               WHEN    "5"
                               WHEN    "6"
                                   MOVE    2       TO  FLG-BUIKBN
                                   MOVE    "1"     TO  WRK-NYU-BUI
      *                        脊髄
                               WHEN    "7"
                                   MOVE    2       TO  FLG-BUIKBN
      *                        消化器
                               WHEN    "8"
                                   MOVE    3       TO  FLG-BUIKBN
      *                        その他
                               WHEN    OTHER
                                   MOVE    1       TO  FLG-BUIKBN
                           END-EVALUATE
                       END-IF
                       MOVE    IDX-Z           TO  IDX-BUI
                   END-IF
      *
               WHEN    "1"
      *            撮影料
                   IF      WRK-COM-DATAKBN (IDX-Z) =   "1"
                       PERFORM 10010-SYUGICHK-SEC
                   END-IF
      *            間接撮影による逓減有り（基準不適合逓減区分以外
                   IF     (WRK-COM-TENSIKIBETU (IDX-Z) =   6 )  AND
                          (WRK-COM-KIJUNTEIGENKBN(IDX-Z)
                                                       =   ZERO ) AND
                          (WRK-COM-AUTOKBN     (IDX-Z) =   SPACE )
                       MOVE    IDX-Z               TO  IDX-GEN
                   END-IF
      *            手技料 加算チェック
                   IF      WRK-COM-DATAKBN (IDX-Z) =   "2"
                       PERFORM 10011-KASAN-CHK-SEC
                   END-IF
      *            単純撮影判定（乳幼児加算）の有無
                   IF      WRK-ERRCD           =   SPACE
                       PERFORM 1008-XSEN-NYUYOUJI-CHK-SEC
                   END-IF
      *            乳幼児加算（点数)
                   IF     (WRK-COM-DATAKBN (IDX-Z) =   "1" )
                     AND  (WRK-ERRCD           =   SPACE   )
                       MOVE    IDX-Z               TO  IDX-SYU
                       PERFORM 1007-NYUYOJI-SET-SEC
                   END-IF
      *            施設基準不適合チェック
                   IF     (WRK-COM-KIJUNTEIGENKBN(IDX-Z)
                                           NOT =   ZERO  )
                     AND  (WRK-ERRCD           =   SPACE )
                       PERFORM 100111-KIJUNN-CHK-SEC
                   END-IF
      *        薬剤
               WHEN    "6"
      *            造影剤
                   IF      WRK-COM-ZOEIZAIKBN(IDX-Z)   =   1
      *                内服薬剤は対象外とする
                       IF      WRK-COM-YKZKBN (IDX-Z)  =   "1"
                           CONTINUE
                       ELSE
                           MOVE   1                TO  FLG-ZOEIZAI
                       END-IF
                   END-IF
      *        器材
               WHEN    "7"
      *            フィルム判定
                   IF      WRK-COM-DATAKBN (IDX-Z)     =   "3"
      *                特定器材年齢加算区分の判定
                       IF      WRK-COM-TOKUKIZAIAGEKSNKBN (IDX-Z)
                                                       =   1
                           MOVE    "1"                 TO  WRK-NYU-FIR
                       END-IF
      *                Ｘ線の数量変更の為の枚数集計
                       IF      WRK-COM-BUNGSU (IDX-Z)  =  ZERO
                           MOVE    1               TO  WRK-COM-BUNGSU
                                                               (IDX-Z)
                       END-IF
                       COMPUTE WRK-KEIMAI      =   WRK-KEIMAI
                                               +  (WRK-COM-SURYO(IDX-Z)
                                               *   WRK-COM-BUNGSU
                                                               (IDX-Z))
                   END-IF
      *            酸素区分判定
                   IF      WRK-COM-SANSOKBN (IDX-Z)    =   1
      *                酸素の時、酸素補正率を自動加算
                       MOVE    WRK-COM-INPUTCD (IDX-Z) TO  WRK-SRYCD
                       PERFORM 100115-SANSO-CHK-SEC
                       IF      FLG-SANSO-OK        =   1
                           MOVE    IDX-Z       TO  IDX-SANSO
                       ELSE
                           IF      IDX-SANSO        =   ZERO
                               MOVE    "0038"       TO  WRK-ERRCD
                               MOVE    "1"          TO  WRK-COM-CUR
                                                              (IDX-Z)
                           END-IF
                       END-IF
                   END-IF
           END-EVALUATE
      *
           .
       10011-CHK-EXT.
           EXIT.
      *****************************************************************
      *    手技料判定処理
      *****************************************************************
       10010-SYUGICHK-SEC           SECTION.
      *
           EVALUATE    WRK-COM-TNSSRYSYUKBN(IDX-Z)
               WHEN    "721"
               WHEN    "720"
      *            Ｘ線撮影料
      *             部位区分があること
                   IF      WRK-COM-TNSSRYSYUKBN(IDX-Z)   =   "721"
                     IF      FLG-BUIKBN      =   ZERO
                       MOVE    "0036"      TO  WRK-ERRCD
                       MOVE    "1"         TO  WRK-COM-CUR (IDX-Z)
                     END-IF
                   ELSE
                       MOVE    1           TO  FLG-BUIKBN
                   END-IF
      *
                   IF      FLG-SINDAN          =   1
                       PERFORM 1002-XSEN-END-SEC
                   ELSE
                       IF      FLG-SINDAN      =   ZERO
      *                    同時併施のチェック等
                           PERFORM 10013-DOUJI-CHK-SEC
                       ELSE
      *                    他の撮影
                           MOVE    "0071"          TO  WRK-ERRCD
                           MOVE    "1"             TO  WRK-COM-CUR
                                                                (IDX-Z)
                       END-IF
                   END-IF
                   MOVE    1               TO  FLG-SINDAN
                   MOVE    IDX-Z           TO  IDX-SAT
                   ADD     1               TO  WRK-CNT-SATUEI
               WHEN    "722"
      *            核医学診断料
                   IF      FLG-SINDAN      NOT =   ZERO
                       MOVE    "1"             TO  WRK-COM-CUR (IDX-Z)
                       MOVE    "0071"          TO  WRK-ERRCD
                   ELSE
                       MOVE    2               TO  FLG-SINDAN
                       MOVE    IDX-Z           TO  IDX-SAT
                   END-IF
               WHEN    "723"
      *            ＣＴ撮影
                   IF      FLG-SINDAN      NOT =   ZERO
                       MOVE    "1"             TO  WRK-COM-CUR(IDX-Z)
                       MOVE    "0071"          TO  WRK-ERRCD
                   ELSE
                       MOVE    3               TO  FLG-SINDAN
                       MOVE    IDX-Z           TO  IDX-SAT
                       MOVE    1               TO  FLG-CTMRI
                   END-IF
                WHEN    "724"
      *            ＭＲＩ撮影
                   IF      FLG-SINDAN      NOT =   ZERO
                       MOVE    "1"             TO  WRK-COM-CUR(IDX-Z)
                       MOVE    "0071"          TO  WRK-ERRCD
                   ELSE
                       MOVE    3               TO  FLG-SINDAN
                       MOVE    IDX-Z           TO  IDX-SAT
                       MOVE    2               TO  FLG-CTMRI
                   END-IF
               WHEN    "711"
               WHEN    "710"
      *            同時併施のチェック等
                   PERFORM 10015-SIDAN-DOUJI-CHK-SEC
               WHEN    "712"
               WHEN    "713"
               WHEN    "714"
      *            Ｘ線診断料
                   CONTINUE
               WHEN    "700"
      *            その他（どの診断料にも付随する）
                   IF      FLG-SINDAN          =   4
                       CONTINUE
                   ELSE
                       IF      FLG-SINDAN  NOT =   ZERO
                           MOVE    "0071"      TO  WRK-ERRCD
                           MOVE    "1"         TO  WRK-COM-CUR (IDX-Z)
                       END-IF
                   END-IF
      *            透視診断はＸ線とする
                   IF     WRK-COM-INPUTCD (IDX-Z)
                                           =   "170000310"
                       CONTINUE
                   ELSE
                       MOVE    4               TO  FLG-SINDAN
                       MOVE    IDX-Z           TO  IDX-SAT
                  END-IF
           END-EVALUATE
      *
           .
       10010-SYUGICHK-EXT.
           EXIT.
      *****************************************************************
      *    酸素判定処理
      *****************************************************************
       100115-SANSO-CHK-SEC           SECTION.
      *
      *
           SET     TBL-SANSO           TO  1
           SEARCH      TBL-SANSO-OCC   VARYING   TBL-SANSO
                   AT  END
                       MOVE    ZERO            TO  FLG-SANSO-OK
                   WHEN    WRK-SRYCD       =   TBL-SANSO-SRYCD
                                                           (TBL-SANSO)
                       MOVE    1               TO  FLG-SANSO-OK
           END-SEARCH 
      *
           .
       100115-SANSO-CHK-EXT.
           EXIT.
      *****************************************************************
      *    酸素補正率自動加算処理
      *****************************************************************
       10011-SANSO-ADD-SEC                  SECTION.
      *
           MOVE    "770020070"         TO  WRK-SRYCD
           MOVE    ZERO                TO  FLG-ARI
           PERFORM VARYING     IDX1    FROM    1   BY  1
                   UNTIL      (IDX1    >   SPA-MAX-LINE  )  OR
                              (WRK-COM-INPUTCD(IDX1)   =   SPACE AND
                               WRK-GMN-INPUTCD(IDX1)   =   SPACE )
               IF     (WRK-COM-INPUTCD  (IDX1)(1:1)    =   "7" ) AND
                      (WRK-COM-SANSOKBN (IDX1) =   1           )
                   IF       WRK-COM-INPUTCD (IDX1) =   WRK-SRYCD OR
                                                       "770030070"
                       CONTINUE
                   ELSE
                       IF      IDX-SANSO       NOT =   IDX1
                           MOVE    IDX1                TO  IDX-SANSO
                       END-IF
                   END-IF
               END-IF
               IF      WRK-COM-INPUTCD (IDX1)  =   WRK-SRYCD
                   MOVE    IDX1                TO  IDX-T
                   MOVE    1                   TO  FLG-ARI
               END-IF
           END-PERFORM
           IF      FLG-ARI             =   ZERO
               MOVE    IDX-SANSO           TO  IDX-S
           END-IF
      *    酸素補正率自動加算
           MOVE    ZERO                TO  FLG-NENREI
           PERFORM 1002-JIDOUSET-SEC
      *
           .
       10011-SANSO-ADD-EXT.
           EXIT.
      *!
      *
      *****************************************************************
      *    施設基準不適合チェック加算処理
      *****************************************************************
       100111-KIJUNN-CHK-SEC           SECTION.
      *
      *    手技の時、適合チェック
           IF      WRK-COM-KIJUNTEIGENKBN (IDX-Z)  =   2  OR  3
               PERFORM 1001111-SSTKIJUN-CHK-SEC
      *        施設基準適応
               IF      FLG-ERR             =   ZERO
                   MOVE    1               TO  FLG-KIJUNN
                   GO      TO      100111-KIJUNN-CHK-EXT
               END-IF
      *        施設基準不適応
               IF     (WRK-COM-KIJUNTEIGENKBN (IDX-Z)  =   2 )  OR
                     ((WRK-COM-KIJUNTEIGENKBN (IDX-Z)  =   3   ) AND
                      (LNK-ORCSC-NENREI-YY             =   ZERO))
                   MOVE    2                   TO  FLG-KIJUNN
      *            施設基準不適合（画像診断）
                   MOVE    "170025170"         TO  WRK-SRYCD
                   IF      WRK-COM-INPUTCD (IDX-Z + 1) =   WRK-SRYCD
                       COMPUTE IDX-GEN2            =   IDX-Z  +  1
                   ELSE
                       MOVE    IDX-Z               TO  IDX-S
                       MOVE    ZERO                TO  FLG-ARI
                       PERFORM 1002-JIDOUSET-SEC
                       MOVE    IDX-S               TO  IDX-GEN2
                  END-IF
              END-IF
           END-IF
      *
      *    加算の時、対象の手技が有ること
           IF      WRK-COM-KIJUNTEIGENKBN (IDX-Z)  =   1
               IF      FLG-KIJUNN          =   ZERO
                   MOVE    "0073"          TO  WRK-ERRCD
                   MOVE    "1"             TO  WRK-COM-CUR (IDX-Z)
               END-IF
           END-IF
           .
       100111-KIJUNN-CHK-EXT.
           EXIT.
      *
      *****************************************************************
      *    施設基準不適応チェック処理
      *****************************************************************
       1001111-SSTKIJUN-CHK-SEC        SECTION.
      *
           MOVE    WRK-COM-KIJUNTEIGENCD (IDX-Z)
                                       TO  IDY
      *
           MOVE    ZERO                TO  FLG-ERR
      *    施設基準情報検索
           MOVE    SPACE               TO  SYS-1006-REC
           MOVE    "1006"              TO  SYS-1006-KANRICD
           IF      IDY                 <=  500
               MOVE    "1"                 TO  SYS-1006-KBNCD
           ELSE
               MOVE    "2"                 TO  SYS-1006-KBNCD
           END-IF
           MOVE    SPA-SRYYMD          TO  SYS-1006-STYUKYMD
           MOVE    SPA-SRYYMD          TO  SYS-1006-EDYUKYMD
      *    システム管理検索
           MOVE    SPA-HOSPNUM         TO  SYS-1006-HOSPNUM
           MOVE    SYS-1006-REC        TO  MCPDATA-REC
           MOVE    "tbl_syskanri"      TO  MCP-TABLE
           MOVE    "key10"             TO  MCP-PATHNAME
           PERFORM 910-DBSELECT-SEC
           IF      MCP-RC              =   ZERO
               MOVE    "tbl_syskanri"      TO  MCP-TABLE
               MOVE    "key10"             TO  MCP-PATHNAME
               PERFORM 900-KANRIMST-READ-SEC
           ELSE
               MOVE    1               TO  FLG-SYSKANRI
           END-IF
           MOVE    "tbl_syskanri"      TO  MCP-TABLE
           MOVE    "key10"             TO  MCP-PATHNAME
           PERFORM 990-DBCLOSE-SEC
           IF      FLG-SYSKANRI        =   ZERO
               MOVE    MCPDATA-REC         TO  SYS-1006-REC
           ELSE
               INITIALIZE                  SYS-1006-REC
           END-IF
      *
      *    基準不適合逓減対象施設基準
           MOVE    WRK-COM-KIJUNTEIGENCD (IDX-Z)
                                       TO  IDY
           IF      IDY                 <=  500
               IF      SYS-1006-SSTKIJUN (IDY) =   1
                   CONTINUE
               ELSE
                   MOVE    1               TO  FLG-ERR
               END-IF
           ELSE
               IF      SYS-1006-SSTKIJUN (IDY - 500) =   1
                   CONTINUE
               ELSE
                   MOVE    1               TO  FLG-ERR
               END-IF
           END-IF
      *
           .
       1001111-SSTKIJUN-CHK-EXT.
           EXIT.
      *
      *****************************************************************
      *    自動加算追加処理
      *****************************************************************
       1002-JIDO-KASAN-SEC        SECTION.
      *
      *    Ｘ線の時、エラーなら診断料の自動発生をおこなう
           IF     (FLG-SINDAN          =   1     )
      *??? AND    (WRK-ERRCD       NOT =   SPACE )
               PERFORM 10021-XSEN-SINDAN-SEC
           END-IF
      *
           MOVE    SPACE               TO  WRK-HEN-SRYCD
           IF      FLG-SINDAN          =   3
      *        ＣＴ．ＭＲＩの算定用コード編集
               MOVE    IDX-SAT             TO  IDX-Z
               PERFORM 10023-CTMRIHEN-SEC
           END-IF
           IF     (FLG-SINDAN          =   3     )  AND
                  (LNK-ORCSC-PG        =   "K02" )  AND
                  (LNK-ORCSC-SYORIFLG  =   ZERO  )  AND
                  (WRK-HEN-SRYCD   NOT =   SPACE )
      *        ＣＴ．ＭＲＩの手技料変更チェック
               PERFORM 10012-SYUGI-CHK-SEC
           END-IF
      *
           .
       1002-JIDO-KASAN-EXT.
           EXIT.
      *
      *****************************************************************
      *    ＣＴ．ＭＲＩの算定用コード変換処理
      *****************************************************************
       10023-CTMRIHEN-SEC                  SECTION.
      *
      *
           MOVE    SPACE               TO  WRK-HEN-SRYCD
      *    包括逓減区分判定（Ｋ０３でも同様の判定をしている）
           EVALUATE    WRK-COM-HOUKATUTEIGENKBN (IDX-Z)
      *        頭部
               WHEN    "201"
                   MOVE    "099700101"         TO  WRK-HEN-SRYCD
      *        躯幹
               WHEN    "202"
                   MOVE    "099700102"         TO  WRK-HEN-SRYCD
      *        四肢
               WHEN    "203"
                   MOVE    "099700103"         TO  WRK-HEN-SRYCD
           END-EVALUATE
           IF      WRK-HEN-SRYCD       =   SPACE
               MOVE    SPACE           TO  WRK-COM-CHK-SRYCD (IDX-Z)
           ELSE
               MOVE    WRK-HEN-SRYCD   TO  WRK-COM-CHK-SRYCD (IDX-Z)
           END-IF
      *    ＣＴ・ＭＲＩ診断料
           PERFORM 100231-CTMRI-SINDAN-SEC
      *
           .
       10023-CTMRIHEN-EXT.
           EXIT.
      *****************************************************************
      *    ＣＴ・ＭＲＩ診断料作成処理
      *****************************************************************
       100231-CTMRI-SINDAN-SEC           SECTION.
      *
      *    造影剤使用加算料
           IF      FLG-CTMRI           =   1
      *            ＣＴ加算
               MOVE    "170012070"         TO  WRK-SRYCD
               MOVE    "170020470"         TO  WRK-SRYCD-2
           ELSE
      *        ＭＩＲ加算
               MOVE    "170020470"         TO  WRK-SRYCD
               MOVE    "170012070"         TO  WRK-SRYCD-2
           END-IF
           MOVE    IDX-SAT             TO  IDX-S
           MOVE    ZERO                TO  FLG-SATU
           MOVE    ZERO                TO  FLG-NENREI
           PERFORM 1003-JIDOU-CHK-SEC
           IF      FLG-ZOEIZAI         =   1
      *        造影剤使用加算料あり
               IF      FLG-ARI             =   ZERO
                   PERFORM 1002-JIDOUSET-SEC
               END-IF
           ELSE
               IF      FLG-ARI             =   1
                   IF      WRK-COM-AUTOKBN (IDX-T) =   "2"
                       PERFORM 10023-JIDOU-DEL-SEC
                   END-IF
               END-IF
           END-IF
      *
           .
       100231-CTMRI-SINDAN-EXT.
           EXIT.
      *****************************************************************
      *    ＣＴ．ＭＲＩの手技料変更チェック処理
      *****************************************************************
       10012-SYUGI-CHK-SEC                  SECTION.
      *
      *    労災の時、変更なし
           IF      SPA-HKNKBN          =   1
      *****  AND  (SPA-RSI-HKNKBN  NOT =   "5" )
             AND  (SPA-RSI-JUNKYO  NOT =   "1" )
               GO      TO      10012-SYUGI-CHK-EXT
           END-IF
      *
      *    包括まとめ入力は対象外
           IF      SPA-HKNCOMBINUM     =   "9999"
             OR   (WRK-COM-HKNCOMBI (IDX-Z) =   "9999")
               GO      TO      10012-SYUGI-CHK-EXT
           END-IF
      *
           MOVE    IDX-SAT             TO  IDX-Z
      *
      *    算定履歴チェック
           INITIALIZE                      ORCSC91AREA
      *****MOVE    "3"                 TO  LNK-SC91-KBN
           MOVE    "4"                 TO  LNK-SC91-KBN
           MOVE    LNK-ORCSC-SYORIFLG  TO  LNK-SC91-SYORIFLG
           MOVE    WRK-HEN-SRYCD       TO  LNK-SC91-SRYCD
           MOVE    IDX-Z               TO  LNK-SC91-GMN-IDX
           MOVE    LNK-ORCSC-KAISU     TO  LNK-SC91-KAISU
           MOVE    LNK-ORCSC-TEISEI-AREA   TO  LNK-SC91-TEISEI-AREA
           CALL    "ORCSC91"           USING
                                       MCPAREA
                                       ORCSC91AREA
                                       WRK-SCR-REC
                                       SPA-AREA
                                       TNS-TENSU-REC
           IF      LNK-SC91-ERRCD      =   "0001"
               MOVE    LNK-SC91-ERRCD      TO  WRK-ERRCD
               MOVE    "1"                 TO  WRK-COM-CUR (IDX-Z)
               GO      TO      10012-SYUGI-CHK-EXT
           END-IF
      *
      *    当月算定なし
           IF      LNK-SC91-CNT        =   ZERO
               CONTINUE
           ELSE
      *        包括逓減区分判定
               EVALUATE    WRK-COM-HOUKATUTEIGENKBN (IDX-Z)
      *            頭部
                   WHEN    "201"
                       IF      WRK-COM-INPUTCD (IDX-Z) =   "170022290"
                                                       OR  "170701470"
                           CONTINUE
                       ELSE
      *                    （一般）
                           MOVE    "170022290"     TO  WRK-COM-INPUTCD
                                                               (IDX-Z)
                       END-IF
      *            躯幹
                   WHEN    "202"
                       IF      WRK-COM-INPUTCD (IDX-Z) =   "170022390" 
                                                       OR  "170701570"
                           CONTINUE
                       ELSE
      *                    （一般）
                           MOVE    "170022390"     TO  WRK-COM-INPUTCD
                                                                (IDX-Z)
                       END-IF
      *            四肢
                   WHEN    "203"
                       IF      WRK-COM-INPUTCD (IDX-Z) =   "170022490" 
                                                       OR  "170701670"
                           CONTINUE
                       ELSE
      *                    （一般）
                           MOVE    "170022490"     TO  WRK-COM-INPUTCD
                                                               (IDX-Z)
                       END-IF
               END-EVALUATE
      *        診療コード変更
               MOVE    1                   TO  FLG-ARI
               MOVE    IDX-Z               TO  IDX-T
               MOVE    WRK-COM-INPUTCD(IDX-Z)
                                           TO  WRK-SRYCD
               PERFORM 1002-JIDOUSET-SEC
               MOVE    SPACE               TO  WRK-COM-AUTOKBN (IDX-Z)
               IF      FLG-ADD             =   1
      *            変更の診療コードをセット
                   MOVE    WRK-HEN-SRYCD   TO  WRK-COM-CHK-SRYCD(IDX-S)
               END-IF
           END-IF
           .
       10012-SYUGI-CHK-EXT.
           EXIT.
      *
      *****************************************************************
      *    加算料チェック処理
      *****************************************************************
       10011-KASAN-CHK-SEC                  SECTION.
      *
      *    加算データの時、前に手技料があること
           MOVE    ZERO                TO  FLG-ARI
           PERFORM VARYING     IDX1    FROM    1   BY  1
                   UNTIL       IDX1    >=  IDX-Z
               IF     (WRK-COM-INPUTCD(IDX1)(1:1)  =   "1" )  AND
                      (WRK-COM-DATAKBN(IDX1)   NOT =   "2" )
                   IF      WRK-COM-CHUKSNCD (IDX1)     =
                                               WRK-COM-CHUKSNCD(IDX-Z)
                       MOVE    1               TO  FLG-ARIOK
                   END-IF
                   MOVE    1               TO  FLG-ARI
                   MOVE    IDX-Z           TO  IDX1
               END-IF
           END-PERFORM
      *
      *    画像診断の加算のみ
           IF     (WRK-COM-SRYSYUKBN(IDX-Z)    =   "704" )  AND
                  (WRK-COM-SRYKBN (IDX-Z)      =   "70"  )
               MOVE    1                   TO  FLG-ARI
           END-IF
      *
           IF      FLG-ARI             =   ZERO
               MOVE    "0013"          TO  WRK-ERRCD
               MOVE    "1"             TO  WRK-COM-CUR (IDX-Z)
           END-IF
      *
           .
       10011-KASAN-CHK-EXT.
           EXIT.
      *****************************************************************
      *    単純撮影判定（乳幼児加算）の有無チェック処理
      *****************************************************************
       1008-XSEN-NYUYOUJI-CHK-SEC           SECTION.
      *
      *    Ｘ線フィルムサブトラクションは対象
           IF      WRK-COM-INPUTCD (IDX-Z) =   "170011650"
               MOVE    "1"             TO  WRK-NYU-CD
           END-IF
      *    単純撮影であること
           SET     TBL-XIDX            TO  1
           SEARCH      TBL-XSEN-OCC    VARYING   TBL-XIDX
                   AT  END
                       GO      TO      1008-XSEN-NYUYOUJI-CHK-EXT
                   WHEN    WRK-COM-INPUTCD (IDX-Z) =   TBL-XSEN-SRYCD
                                                           (TBL-XIDX)
                       MOVE    "1"             TO  WRK-NYU-CD
           END-SEARCH
      *
           .
       1008-XSEN-NYUYOUJI-CHK-EXT.
           EXIT.
      *****************************************************************
      *    同時併施のチェック処理
      *****************************************************************
       10013-DOUJI-CHK-SEC           SECTION.
      *    同時併施のチェック
           SET     TBL-IDX             TO  1
           SEARCH      TBL-DOUJI-OCC   VARYING   TBL-IDX
                   AT  END
                       MOVE    SPACE           TO  WRK-SRYCD
                   WHEN    WRK-COM-INPUTCD (IDX-Z) =   TBL-DOUJI-2
                                                           (TBL-IDX)
                       MOVE    TBL-DOUJI-1 (TBL-IDX)
                                                TO  WRK-SRYCD
           END-SEARCH
      *
      *    特殊撮影以外エラー
           IF     (WRK-COM-INPUTCD (IDX-Z) =   "170002010" )  OR
                  (WRK-SRYCD               =   SPACE )
               CONTINUE
           ELSE
      ******   MOVE    "0070"          TO  WRK-ERRCD
      *****    MOVE    "1"             TO  WRK-COM-CUR (IDX-Z)
      *        警告へ変更する
               IF     (WRK-COM-KZMFLG (IDX-Z)  =   SPACE ) AND
                      (LNK-ORCSC-KEI-IDX       =   ZERO  )
                   MOVE    IDX-Z               TO  LNK-ORCSC-KEI-IDX
                   MOVE    "1"                 TO  WRK-COM-KZMFLG(IDX-Z)
                   MOVE    "1"                 TO  WRK-COM-CUR (IDX-Z)
                   MOVE    "K070"              TO  WRK-ERRCD
                   GO      TO      10013-DOUJI-CHK-EXT
               END-IF
           END-IF
      *
      *    年齢別にある場合は、年齢毎のコードへ
           IF      LNK-ORCSC-NENREI-YY <=  3
               MOVE    SPACE               TO  WRK-SRYCD
               PERFORM 10023-DOUJI-NENREI-SEC
               IF      WRK-SRYCD       NOT =   SPACE
      *            診療コード変更
                   MOVE    1                   TO  FLG-ARI
                   MOVE    IDX-Z               TO  IDX-T
                   PERFORM 1002-JIDOUSET-SEC
                   MOVE    SPACE               TO  WRK-COM-AUTOKBN
                                                       (IDX-S)
               END-IF
           END-IF
           .
      *
       10013-DOUJI-CHK-EXT.
           EXIT.
      *****************************************************************
      *    診断料同時併施のチェック処理
      *****************************************************************
       10015-SIDAN-DOUJI-CHK-SEC           SECTION.
      *
      *    撮影料があること
           IF      FLG-SINDAN          =   1
               CONTINUE
           ELSE
               MOVE    "1"             TO  WRK-COM-CUR(IDX-Z)
               MOVE    "0079"          TO  WRK-ERRCD
               GO      TO      10015-SIDAN-DOUJI-CHK-EXT
           END-IF
      *
           EVALUATE    WRK-COM-INPUTCD (IDX-Z)
               WHEN    "170000730"
      *            特殊撮影の写真診断（同時）
                   IF     (WRK-CNT-SATUEI      >   1   )  AND
                          (WRK-COM-INPUTCD(IDX-SAT)
                                               =   "170002010")
                       CONTINUE
                   ELSE
      ***********      MOVE    "0071"          TO  WRK-ERRCD
      ***********      MOVE    "1"             TO  WRK-COM-CUR (IDX-Z)
      *                警告へ変更する
                       IF     (WRK-COM-KZMFLG (IDX-Z)  =   SPACE ) AND
                              (LNK-ORCSC-KEI-IDX       =   ZERO  )
                           MOVE    IDX-Z           TO  LNK-ORCSC-KEI-IDX
                           MOVE    "1"             TO  WRK-COM-KZMFLG
                                                             (IDX-Z)
                           MOVE    "1"             TO  WRK-COM-CUR
                                                             (IDX-Z)
                           MOVE    "K070"          TO  WRK-ERRCD
                       END-IF
                   END-IF
               WHEN    "170022930"
      *            造影剤使用写真診断（同時）
                   IF     (WRK-CNT-SATUEI      >   1   )  AND
                          (WRK-COM-INPUTCD(IDX-SAT)
                                               =   "170002110")
                       CONTINUE
                   ELSE
      ***************  MOVE    "0071"          TO  WRK-ERRCD
      **************   MOVE    "1"             TO  WRK-COM-CUR (IDX-Z)
      *                警告へ変更する
                       IF     (WRK-COM-KZMFLG (IDX-Z)  =   SPACE ) AND
                              (LNK-ORCSC-KEI-IDX       =   ZERO  )
                           MOVE    IDX-Z           TO  LNK-ORCSC-KEI-IDX
                           MOVE    "1"             TO  WRK-COM-KZMFLG
                                                             (IDX-Z)
                           MOVE    "1"             TO  WRK-COM-CUR
                                                             (IDX-Z)
                           MOVE    "K070"          TO  WRK-ERRCD
                       END-IF
                   END-IF
           END-EVALUATE
      *
      *    同時併施の診断料で撮影が１回の場合警告
           IF     (WRK-CNT-SATUEI      =   1   )  AND
                  (WRK-COM-TNSSRYSYUKBN(IDX-Z) =   "710")
               IF     (WRK-COM-KZMFLG (IDX-Z)  =   SPACE ) AND
                      (LNK-ORCSC-KEI-IDX       =   ZERO  )
                   MOVE    IDX-Z           TO  LNK-ORCSC-KEI-IDX
                   MOVE    "1"             TO  WRK-COM-KZMFLG (IDX-Z)
                   MOVE    "1"             TO  WRK-COM-CUR   (IDX-Z)
                   MOVE    "K070"          TO  WRK-ERRCD
               END-IF
           END-IF
           .
      *
       10015-SIDAN-DOUJI-CHK-EXT.
           EXIT.
      *
      *****************************************************************
      *    Ｘ線撮影料終了処理
      *****************************************************************
       1002-XSEN-END-SEC           SECTION.
      *
      *    同時併施のチェック
           SET     TBL-IDX             TO  1
           SEARCH      TBL-DOUJI-OCC   VARYING   TBL-IDX
                   AT   END
                       MOVE    SPACE           TO  WRK-SRYCD
                   WHEN    WRK-COM-INPUTCD (IDX-Z) =   TBL-DOUJI-1
                                                           (TBL-IDX)
                       MOVE    TBL-DOUJI-2 (TBL-IDX)
                                                TO  WRK-SRYCD
           END-SEARCH
      *    年齢別にある場合は、年齢毎のコードへ
           IF      LNK-ORCSC-NENREI-YY <=  3
               PERFORM 10023-DOUJI-NENREI-SEC
           END-IF
      *
      *    単純撮影料の２回目を特殊撮影料へ変換
           IF      WRK-SRYCD       NOT =   SPACE
      *        診療コード変更
               MOVE    1                   TO  FLG-ARI
               MOVE    IDX-Z               TO  IDX-T
               PERFORM 1002-JIDOUSET-SEC
           END-IF
      *    撮影の切り替えフラグ
           MOVE    "K"                 TO  WRK-COM-AUTOKBN (IDX-Z)
           MOVE    IDX-Z               TO  IDX-HOZ2
      *
      *    診断料加算追加
           PERFORM 10021-XSEN-SINDAN-SEC
      *
      *    現在のチェック位置に戻す
           PERFORM VARYING     IDX     FROM    IDX-HOZ2  BY  1
                   UNTIL       IDX     >   SPA-MAX-LINE
               IF      WRK-COM-AUTOKBN (IDX)   =   "K"
                   MOVE    IDX             TO  IDX-Z
                   MOVE    SPA-MAX-LINE    TO  IDX
               END-IF
           END-PERFORM
      *
           MOVE    ZERO                TO  IDX-SAT
           MOVE    IDX-Z               TO  IDX-HOZ
           .
      *
       1002-XSEN-END-EXT.
           EXIT.
      *
      *****************************************************************
      *    Ｘ線撮影料終了処理
      *****************************************************************
       10021-XSEN-SINDAN-SEC           SECTION.
      *    フィルム枚数の設定
      *    きざみ値判定
           IF     (IDX-SAT             >   ZERO   )  AND
                  (WRK-COM-KZMCOMPSIKIBETU (IDX-SAT) NOT =   ZERO)
               PERFORM 100211-KZMSURYO-SET-SEC
           END-IF
      *    乳幼児加算あり
           IF      IDX-NYU             >   ZERO
               MOVE    IDX-NYU         TO  IDX-SAT2
           ELSE
               MOVE    IDX-SAT         TO  IDX-SAT2
           END-IF
      *
           MOVE    IDX-SAT2            TO  IDX-S
           PERFORM 100012-SINDAN-SET-SEC
      *    フィルム乳幼児加算の算定有無
           IF     (WRK-NYU-BUI         =   "1" )  AND
                  (WRK-NYU-CD          =   "1" )  AND
                  (WRK-NYU-FIR         =   "1" )  AND
                  (LNK-ORCSC-NENREI-YY <   6   )
               PERFORM 100211-FIRTEN-SET-SEC
           END-IF
           MOVE    ZERO                TO  IDX-NYU
           MOVE    ZERO                TO  IDX-GEN
      *    フィルム乳幼児加算領域クリア
           INITIALIZE                  WRK-NYUYOJI-AREA
           MOVE    ZERO                TO  WRK-KEIMAI
           MOVE    IDX-Z               TO  IDX-HOZ
      *
           .
       10021-XSEN-SINDAN-EXT.
           EXIT.
      *
      *****************************************************************
      *    診断料自動加算作成処理
      *****************************************************************
       100211-KZMSURYO-SET-SEC           SECTION.
      *
      *    数量入力のない時,数量を枚数とする
           IF     (WRK-COM-SURFLG (IDX-SAT)   =   SPACE )
               IF      WRK-KEIMAI          >   WRK-COM-KZMJGN(IDX-SAT)
                   MOVE    WRK-COM-KZMJGN (IDX-SAT)
                                               TO  WRK-COM-SURYO
                                                           (IDX-SAT)
               ELSE
                   IF      WRK-KEIMAI          =   ZERO
                       MOVE    1               TO  WRK-COM-SURYO
                                                           (IDX-SAT)
                   ELSE
                       MOVE    WRK-KEIMAI      TO  WRK-COM-SURYO
                                                           (IDX-SAT)
                   END-IF
               END-IF
           END-IF
      *
           .
       100211-KZMSURYO-SET-EXT.
           EXIT.
      *
      *****************************************************************
      *    診断料自動加算作成処理
      *****************************************************************
       100012-SINDAN-SET-SEC           SECTION.
      *
           MOVE    SPACE               TO  WRK-SRYCD
      *
      *    診断・撮影同時のチェック
           SET     TBL-IDX             TO  1
           SEARCH      TBL-DOUJI-OCC   VARYING   TBL-IDX
                   AT   END
                       MOVE    SPACE           TO  WRK-SRYCD
                   WHEN    WRK-COM-INPUTCD (IDX-SAT) =   TBL-DOUJI-1
                                                           (TBL-IDX)
                       MOVE    TBL-DOUJI-2 (TBL-IDX)
                                                TO  WRK-SRYCD
                   WHEN    WRK-COM-INPUTCD (IDX-SAT) =   TBL-DOUJI-2
                                                           (TBL-IDX)
                       MOVE    TBL-DOUJI-1 (TBL-IDX)
                                                TO  WRK-SRYCD
           END-SEARCH
           IF     (WRK-SRYCD           =   SPACE      )  OR
                  (WRK-SRYCD           =   "170002010"  OR 
                                           "170001910" )
               MOVE    ZERO                TO  FLG-SYORI
           ELSE
               MOVE    SPACE               TO  WRK-SRYCD
               MOVE    1                   TO  FLG-SYORI
               GO      TO      100012-SINDAN-SET-EXT
           END-IF
      *
      *    単純撮影の判定
           SET     TBL-XIDX            TO  1
           SEARCH      TBL-XSEN-OCC    VARYING   TBL-XIDX
                   AT  END
                       MOVE    SPACE           TO  WRK-XSEN-SRYCD-I
                       MOVE    SPACE           TO  WRK-XSEN-SRYCD-R
                   WHEN    WRK-COM-INPUTCD (IDX-SAT)
                                           =   TBL-XSEN-SRYCD
                                                           (TBL-XIDX)
                       MOVE    TBL-XSEN-SRYCD-I (TBL-XIDX)
                                                   TO  WRK-XSEN-SRYCD-I
                       MOVE    TBL-XSEN-SRYCD-R (TBL-XIDX)
                                                   TO  WRK-XSEN-SRYCD-R
           END-SEARCH
      *
      *    同時併施の診断料の判定
           MOVE    ZERO                TO  IDX-DOJI1
           MOVE    ZERO                TO  IDX-DOJI2
           PERFORM VARYING     IDX-S1  FROM    IDX-SAT   BY  1
                   UNTIL      (IDX-S1      >   SPA-MAX-LINE )  OR
                              (IDX-DOJI1   >   ZERO )    OR
                              (IDX-DOJI2   >   ZERO )    OR
                              (WRK-COM-INPUTCD (IDX-S1)  =   SPACE AND
                               WRK-GMN-INPUTCD (IDX-S1)  =   SPACE )
      *        同時併施の診断料判定
               IF      WRK-COM-TNSSRYSYUKBN(IDX-S1)    =   "710"
                   MOVE    IDX-S1              TO  IDX-DOJI1
               END-IF
               IF      WRK-COM-TNSSRYSYUKBN(IDX-S1)    =   "711"
                   MOVE    IDX-S1              TO  IDX-DOJI2
               END-IF
      *
               IF     (IDX-S1          NOT =   IDX-SAT)  AND
                      (WRK-COM-TNSSRYSYUKBN (IDX-S1)    =   "721"
                                                        OR  "720")
                   MOVE   SPA-MAX-LINE         TO  IDX-S1
               END-IF
           END-PERFORM
      *
           EVALUATE    WRK-COM-INPUTCD (IDX-SAT)
               WHEN    "170002010"
      *            特殊撮影の写真診断
                   IF      WRK-CNT-SATUEI      =   1
                       MOVE    "170000610"         TO  WRK-SRYCD
                       MOVE    "170000730"         TO  WRK-SRYCD-2
                   ELSE
                       MOVE    "170000730"         TO  WRK-SRYCD
                       MOVE    "170000610"         TO  WRK-SRYCD-2
                   END-IF
      ************ MOVE    SPACE               TO  WRK-SRYCD-2
                   IF     (IDX-DOJI1           >   ZERO )  AND
                          (WRK-COM-INPUTCD (IDX-DOJI1)
                                               =   "170000730")
      *                同時併施のコードあり
                       MOVE    SPACE               TO  WRK-SRYCD
                   END-IF
               WHEN    "170002110"
      *            造影剤使用の診断料
                   IF      WRK-CNT-SATUEI      =   1
                       MOVE    "170000810"         TO  WRK-SRYCD
                       MOVE    "170022930"         TO  WRK-SRYCD-2
                   ELSE
                       MOVE    "170022930"         TO  WRK-SRYCD
                       MOVE    "170000810"         TO  WRK-SRYCD-2
                   END-IF
      **********   MOVE    SPACE               TO  WRK-SRYCD-2
                   IF     (IDX-DOJI1           >   ZERO )  AND
                          (WRK-COM-INPUTCD (IDX-DOJI1)
                                               =   "170022930")
      *                同時併施のコードあり
                       MOVE    SPACE               TO  WRK-SRYCD
                   END-IF
               WHEN    "170001910"
      *            単純撮影の診断料
                   EVALUATE    FLG-BUIKBN
                       WHEN    1
                       WHEN    3
      *                    その他（ロ）
                           MOVE    WRK-XSEN-SRYCD-R    TO  WRK-SRYCD
                           MOVE    WRK-XSEN-SRYCD-I    TO  WRK-SRYCD-2
      *                    同時併施の場合
                           IF      WRK-CNT-SATUEI      >   1
                               MOVE    "170022830"     TO  WRK-SRYCD
                               MOVE    "170022730"     TO  WRK-SRYCD-2
                           END-IF
                           IF     (IDX-DOJI1           >   ZERO )  AND
                                  (WRK-COM-INPUTCD (IDX-DOJI1)
                                                       =   "170022830")
      *                        同時併施のコードあり
                               MOVE    SPACE               TO  WRK-SRYCD
                           END-IF
                       WHEN    2
      *                    頭・胸・腹・脊髄（イ）
                           MOVE    WRK-XSEN-SRYCD-I    TO  WRK-SRYCD
                           MOVE    WRK-XSEN-SRYCD-R    TO  WRK-SRYCD-2
      *                    同時併施の場合
                           IF      WRK-CNT-SATUEI      >   1
                               MOVE    "170022730"     TO  WRK-SRYCD
                               MOVE    "170022830"     TO  WRK-SRYCD-2
                           END-IF
                           IF     (IDX-DOJI1           >   ZERO )  AND
                                  (WRK-COM-INPUTCD (IDX-DOJI1)
                                                       =   "170022730")
      *                        同時併施のコードあり
                               MOVE    SPACE               TO  WRK-SRYCD
                           END-IF
                   END-EVALUATE
               WHEN    OTHER
      *            その他の単純撮影の診断料
                   EVALUATE    FLG-BUIKBN
                       WHEN    1
                       WHEN    3
      *                    その他（ロ）
                           MOVE    WRK-XSEN-SRYCD-R    TO  WRK-SRYCD
                           MOVE    WRK-XSEN-SRYCD-I    TO  WRK-SRYCD-2
                       WHEN    2
      *                    頭・胸・腹・脊髄（イ）
                           MOVE    WRK-XSEN-SRYCD-I    TO  WRK-SRYCD
                           MOVE    WRK-XSEN-SRYCD-R    TO  WRK-SRYCD-2
                   END-EVALUATE
           END-EVALUATE
           IF      FLG-SYORI           =   1
               MOVE    SPACE               TO  WRK-SRYCD
           END-IF
      *    診断料自動追加
           MOVE    IDX-SAT             TO  IDX-SIN
           IF      WRK-SRYCD       NOT =   SPACE
               MOVE    1                   TO  FLG-SATU
               MOVE    IDX-SAT2            TO  IDX-S
               MOVE    ZERO                TO  FLG-NENREI
               PERFORM 1003-JIDOU-CHK-SEC
               PERFORM 1002-JIDOUSET-SEC
               IF      FLG-ADD             =   1
                   MOVE    IDX-S               TO  IDX-SIN
               END-IF
      *
      ******   １回目で間接撮影コードの入力がある時、診断料の後にも追加
      *        IF      (FLG-ADD            =   1    )  AND
      *                (IDX-GEN            >   ZERO )  AND
      *                (WRK-CNT-SATUEI     =   1    )
      *            MOVE    ZERO            TO  FLG-ARI
      **           MOVE    WRK-COM-INPUTCD (IDX-GEN)
      *            MOVE    WRK-GEN-SRYCD   TO  WRK-SRYCD
      *            PERFORM 1002-JIDOUSET-SEC
      ******   END-IF
           END-IF
      *
      *    消化管で造影剤撮影の時、透視診断料の追加（撮影料の上に追加）
           IF     (FLG-BUIKBN          =   3  )   AND
                  (WRK-COM-INPUTCD (IDX-SAT)   =   "170002110" )
               MOVE    "170000310"         TO  WRK-SRYCD
               COMPUTE IDX-S               =   IDX-SAT    -    1
               MOVE    ZERO                TO  FLG-SATU
               MOVE    SPACE               TO  WRK-SRYCD-2
               MOVE    ZERO                TO   FLG-NENREI
               PERFORM 1003-JIDOU-CHK-SEC
               PERFORM 1002-JIDOUSET-SEC
               IF      FLG-ADD             =   1
                   IF      WRK-COM-INPUTCD (IDX-SAT + 1)
                                               =   "170002110"
                       COMPUTE IDX-SAT         =   IDX-SAT    +   1
                   END-IF
               END-IF
           END-IF
      *
           .
       100012-SINDAN-SET-EXT.
           EXIT.
      *
      *****************************************************************
      *    加算料追加処理
      *****************************************************************
       1002-JIDOUSET-SEC              SECTION.
      *
           MOVE    ZERO                TO  FLG-ADD
      *
           IF      FLG-ARI             =   ZERO
               ADD     1               TO  IDX-S
           ELSE
               MOVE    IDX-T           TO  IDX-S
           END-IF
      *
      *    点数マスタ編集・基本チェック
           MOVE    WRK-SCR-REC         TO  HOZ-SCR-REC
           INITIALIZE                  ORCSC92AREA
           IF      FLG-ARI             =   ZERO
               MOVE    "1"                 TO  LNK-SC92-KBN
           ELSE
               MOVE    SPACE               TO  WRK-COM-CHKFLG (IDX-S)
               MOVE    SPACE               TO  WRK-COM-GMNFLG (IDX-S)
           END-IF
           IF      FLG-NENREI          =   1
               MOVE    "1"                 TO  LNK-SC92-CHUKSN
           END-IF
           MOVE    LNK-ORCSC-SYORIFLG  TO  LNK-SC92-SYORIFLG
           MOVE    IDX-S               TO  LNK-SC92-GMN-IDX
           MOVE    LNK-ORCSC-PG        TO  LNK-SC92-PG
           MOVE    WRK-SRYCD           TO  LNK-SC92-SRYCD
           MOVE    LNK-ORCSC-NENREI    TO  LNK-SC92-NENREI
           MOVE    LNK-ORCSC-ROUJIN    TO  LNK-SC92-ROUJIN
           MOVE    LNK-ORCSC-LSTSRYKA  TO  LNK-SC92-LSTSRYKA
           MOVE    LNK-ORCSC-LSTSRYKAMEI   TO  LNK-SC92-LSTSRYKAMEI
           MOVE    LNK-ORCSC-TEISEI-AREA   TO  LNK-SC92-TEISEI-AREA
           CALL    "ORCSC92"           USING
                                       MCPAREA
                                       ORCSC92AREA
                                       WRK-SCR-REC
                                       SPA-AREA
      *
      *    エラーの時、追加なし
           IF     (SPA-ERRCD           NOT =   SPACE )  OR
                  (LNK-SC92-ERRAREA    NOT =   SPACE )
               MOVE    SPACE               TO  SPA-ERRCD
               MOVE    HOZ-SCR-REC         TO  WRK-SCR-REC
               IF      FLG-ARI             =   ZERO
                   COMPUTE IDX-S           =   IDX-S
                                           -   1
               END-IF
               GO      TO      1002-JIDOUSET-EXT
           END-IF
      *
           IF      IDX-S               >   1
               MOVE    WRK-COM-SRYKBN (IDX-S - 1)
                                           TO  WRK-COM-SRYKBN(IDX-S)
               MOVE    SPACE               TO  WRK-GMN-SRYKBN(IDX-S)
               MOVE    WRK-COM-SRYSYUKBN (IDX-S - 1)
                                           TO  WRK-COM-SRYSYUKBN(IDX-S)
           END-IF
      *
      *    自動追加分（入力時に追加）
           MOVE    "2"                 TO  WRK-COM-AUTOKBN (IDX-S)
      *
      *****ADD     1                   TO  WRK-GMN-MAX
      *
           MOVE    1                   TO  FLG-ADD
           .
       1002-JIDOUSET-EXT.
           EXIT.
      *
      *****************************************************************
      *    自動加算　存在チェック加処理
      *****************************************************************
       1003-JIDOU-CHK-SEC        SECTION.
      *
      *    存在チェック
           MOVE    ZERO                TO  FLG-ARI
           MOVE    ZERO                TO  IDX-T
           IF      FLG-NENREI          =   1
               MOVE    IDX-SYU             TO  IDXS1
           ELSE
               MOVE    1                   TO  IDXS1
      *        診断料の時、撮影料以下
               IF      FLG-SATU            =   1
                   MOVE    IDX-SAT             TO  IDXS1
               END-IF
           END-IF
           PERFORM VARYING     IDX1    FROM    IDXS1  BY  1
                   UNTIL      (IDX1        >   SPA-MAX-LINE     ) OR
                              (FLG-ARI                 =   1    ) OR
                              (WRK-GMN-INPUTCD (IDX1)  =   SPACE ) OR
                              (WRK-GMN-INPUTCD(IDX1)(1:1)  =   "*")
              IF      (WRK-COM-INPUTCD (IDX1) NOT =   SPACE )  AND
                     ((WRK-COM-INPUTCD (IDX1) =   WRK-SRYCD ) OR
                      (WRK-COM-INPUTCD (IDX1) =   WRK-SRYCD-2))
                    MOVE    IDX1           TO  IDX-T
                    MOVE    1              TO  FLG-ARI
               END-IF
      *!!      IF     (WRK-SRYCD               =    "170000510"   OR
      *                                             "170000410" )  AND
      *               (WRK-COM-INPUTCD (IDX1)  =    "170000510"   OR
      *                                             "170000410" )
      *             MOVE    IDX1           TO  IDX-T
      *!            MOVE    1              TO  FLG-ARI
      *!!      END-IF
           END-PERFORM
           .
       1003-JIDOU-CHK-EXT.
           EXIT.
      *
      *****************************************************************
      *    年齢加算追加処理（共通）
      *****************************************************************
       1007-NYUYOJI-SET-SEC            SECTION.
      *
           PERFORM VARYING     IDX2    FROM    1   BY  1
                   UNTIL      (IDX2        >   4       )   OR
                              (WRK-COM-AGEKSNSRYCD(IDX-SYU IDX2)
                                          =   SPACE  OR   ZERO )
               MOVE    WRK-COM-AGEKSNSRYCD (IDX-SYU IDX2)
                                               TO  WRK-SRYCD
               MOVE    IDX-SYU            TO   IDX-T
               MOVE    IDX-SYU            TO   IDX-S
               MOVE    SPACE              TO   WRK-SRYCD-2
               MOVE    1                  TO   FLG-NENREI
               MOVE    3                   TO  FLG-SATU
               PERFORM 1003-JIDOU-CHK-SEC
      *        加算追加
               PERFORM 1002-JIDOUSET-SEC
               IF      FLG-ADD             =   1
                   MOVE    2                   TO  WRK-COM-SANFLG 
                                                           (IDX-S)
      *            撮影料の乳幼児加算の時、位置保存
                   IF      IDX-SYU             =   IDX-SAT
                       MOVE    IDX-S           TO  IDX-NYU
                   END-IF
                   MOVE    4                   TO  IDX2
               END-IF
           END-PERFORM
      *
           .
       1007-NYUYOJI-SET-EXT.
           EXIT.
      *****************************************************************
      *    フィルム乳幼児加算処理
      *****************************************************************
       100211-FIRTEN-SET-SEC             SECTION.
      *
      *    乳幼児加算あり
           MOVE    "799990070"         TO  WRK-SRYCD
           MOVE    ZERO                TO  IDX-S
      *
      *    自動加算　存在チェック＆挿入位置セット
           MOVE    ZERO                TO  FLG-ARI
           MOVE    ZERO                TO  WRK-NYU-IDX
           PERFORM VARYING     IDX1    FROM    IDX-HOZ  BY  1
                   UNTIL      (IDX1        >   SPA-MAX-LINE     )   OR
                              (WRK-GMN-INPUTCD (IDX1)  =   SPACE )
                          OR  (WRK-COM-AUTOKBN (IDX1)  =   "K"   )
      *            フィルム判定
               IF    ((WRK-COM-INPUTCD (IDX1)(1:1) =   "7" ) OR
                      (WRK-COM-INPUTCD (IDX1)(1:3) =   "059"))  AND
                      (WRK-COM-DATAKBN (IDX1)      =   "3" )   AND
                      (WRK-COM-INPUTCD (IDX1)  NOT =   WRK-SRYCD )
      *            特定器材年齢加算区分の判定
                   IF      WRK-COM-TOKUKIZAIAGEKSNKBN (IDX1)
                                               =   1
                       IF      WRK-COM-INPUTCD (IDX1 + 1)
                                               =   WRK-SRYCD
                           CONTINUE
                       ELSE
                           MOVE    IDX1                TO  IDX-S
                           MOVE    1                   TO  FLG-NENREI
                           PERFORM 1002-JIDOUSET-SEC
                           IF      FLG-ADD             =   1
                               MOVE    IDX-S           TO  WRK-NYU-IDX
                               ADD     1               TO  IDX-MAX
                               ADD     1               TO  IDX1
                           END-IF
                       END-IF
                   END-IF
               END-IF
           END-PERFORM
      *
      ***  IF      WRK-NYU-IDX         >   ZERO
      *        MOVE    WRK-NYU-IDX         TO  IDX-S
      *    END-IF
      *    MOVE    1                   TO  FLG-NENREI
      **** PERFORM 1002-JIDOUSET-SEC
      *
           .
       100211-FIRTEN-SET-EXT.
           EXIT.
      *
      *****************************************************************
      *    年齢別コード変換処理
      *****************************************************************
       10023-DOUJI-NENREI-SEC           SECTION.
      *
      *    同時併施のチェック
           SET     TBL-NIDX            TO  1
           SEARCH      TBL-DOUJI-NEBREI-OCC    VARYING   TBL-NIDX
                   AT   END
                       MOVE    SPACE            TO  WRK-SRYCD1
                       MOVE    SPACE            TO  WRK-SRYCD2
                   WHEN    WRK-COM-INPUTCD (IDX-Z) =   TBL-NEN-SRYCD0
                                                           (TBL-NIDX)
                       MOVE    TBL-NEN-SRYCD1 (TBL-NIDX)
                                                TO  WRK-SRYCD1
                       MOVE    TBL-NEN-SRYCD2 (TBL-NIDX)
                                                TO  WRK-SRYCD2
           END-SEARCH
           IF      WRK-SRYCD1          =   SPACE
               GO      TO      10023-DOUJI-NENREI-EXT
           END-IF
      *
      *    新生児判定
           IF     (LNK-ORCSC-NENREI-YY     =   ZERO )  AND
                  (LNK-ORCSC-NENREI-MM     =   ZERO )  AND
                  (LNK-ORCSC-NENREI-DD     <   28   )
               MOVE    WRK-SRYCD1          TO  WRK-SRYCD
           ELSE
      *        乳幼児（３歳未満）
               IF      LNK-ORCSC-NENREI-YY     <   3
                   MOVE    WRK-SRYCD2          TO  WRK-SRYCD
               END-IF
           END-IF
           .
      *
       10023-DOUJI-NENREI-EXT.
           EXIT.
      *
      *****************************************************************
      *    点数計算編集処理
      *****************************************************************
       1002-TENSU-KEI-SEC                  SECTION.
      *
           INITIALIZE                  IDX-AREA
           INITIALIZE                  WRK-AREA
      *    撮影料の再計算
           MOVE    ZERO                TO  WRK-TENSUKEI
           MOVE    ZERO                TO  WRK-KISOTEN-G
      *    枚数計算
           MOVE    ZERO                TO  WRK-KEIMAI
      *
           INITIALIZE                  ORCSC90AREA
           MOVE    ZERO                TO  IDX-MAX
           MOVE    1                   TO  IDX-STR
           PERFORM VARYING     IDX-Z   FROM    1   BY  1
                   UNTIL      (IDX-Z       >   SPA-MAX-LINE  )  OR
                              (WRK-GMN-INPUTCD(IDX-Z)  =   SPACE )
      *
               ADD     1                   TO  IDX-MAX
      *
               MOVE    SPACE               TO  WRK-GMN-SURYO (IDX-Z)
               MOVE    SPACE               TO  WRK-GMN-TENSU (IDX-Z)
               MOVE    SPACE               TO  WRK-GMN-KAISU (IDX-Z)
               MOVE    ZERO                TO  WRK-GMN-KEI   (IDX-Z)
      *
      *        手技毎
               INITIALIZE                   WRK-COM-PLUS-AREA(IDX-Z)
      *
               IF      WRK-GMN-INPUTCD(IDX-Z)(1:1)     =   "."
                                                       OR "*"
                   MOVE    WRK-GMN-INPUTCD(IDX-Z)(1:1) TO  WRK-MOJI
               ELSE
                   MOVE    WRK-COM-INPUTCD(IDX-Z)(1:1) TO  WRK-MOJI
                   IF      WRK-COM-INPUTCD(IDX-Z)(1:3) =   "059"
                       MOVE    "7"                     TO  WRK-MOJI
                   END-IF
               END-IF
      *
               EVALUATE    WRK-MOJI
                   WHEN    "0"
                   WHEN    "8"
                   WHEN    "."
                   WHEN    "*"
                       CONTINUE
      *
                   WHEN    OTHER
      *                編集チェックへ
                       PERFORM 10021-KEISAN-SEC
               END-EVALUATE
           END-PERFORM
      *
      *    最終行
      *    割合計算加算
           IF     (IDX-SYU             =   ZERO ) AND
                  (FLG-SYUGI           =   1    )
               MOVE    1                   TO  IDX-SYU
           END-IF
           IF      IDX-SYU             >   ZERO
               COMPUTE IDX-Z               =   IDX-MAX   +  1
               PERFORM 10021-TENSUKEI-SEC
           END-IF
      *    合計点数計算・編集
           MOVE    "2"                 TO  LNK-SC90-KBN
           MOVE    IDX-SYU             TO  LNK-SC90-IDX
           MOVE    IDX-STR             TO  LNK-SC90-IDX-STR
           MOVE    IDX-MAX             TO  LNK-SC90-IDX-END
           CALL    "ORCSC90TEN"        USING
                                       ORCSC90AREA
                                       WRK-SCR-REC
      *
      *    計
           COMPUTE WRK-COM-KEI (IDX-MAX)   =   WRK-COM-TENSU  (IDX-MAX)
                                           *   LNK-ORCSC-KAISU
      *
           MOVE    WRK-COM-KEI  (IDX-MAX)  TO  LNK-ORCSC-ZAIKEI
           MOVE    WRK-COM-TENSU(IDX-MAX)  TO  LNK-ORCSC-ZAITEN
      *
      *    AUTKBNクリア
           PERFORM VARYING     IDX-Z   FROM    1   BY  1
                   UNTIL       IDX-Z       >   IDX-MAX
               IF      WRK-COM-AUTOKBN(IDX-Z)  =   "K"
                   MOVE    SPACE           TO  WRK-COM-AUTOKBN(IDX-Z)
               END-IF
           END-PERFORM
      *
           .
       1002-TENSU-KEI-EXT.
           EXIT.
      *
      *****************************************************************
      *    点数計算処理
      *****************************************************************
       10021-KEISAN-SEC                  SECTION.
      *
      *    点数記載判定
           IF      (WRK-MOJI                   =   "1"  )  AND
                   (WRK-COM-DATAKBN (IDX-Z)    =   "1"  )
               IF      FLG-SINDAN          =   1
      *        Ｘ線撮影料
                   IF      WRK-COM-AUTOKBN(IDX-Z)  =   "K"
                       IF      IDX-SYU             >   ZERO
      *                    手技料の計算
                           PERFORM 10021-TENSUKEI-SEC
      *                    点数計算
                           PERFORM 1007-TENSU-SUM-SEC
                       END-IF
                       MOVE    IDX-Z               TO  IDX-SYU
                   END-IF
               ELSE
                   IF      IDX-SYU             >   ZERO
      *                手技料の計算
                       PERFORM 10021-TENSUKEI-SEC
      *                点数計算
                       PERFORM 1007-TENSU-SUM-SEC
                   END-IF
                   MOVE    IDX-Z               TO  IDX-SYU
               END-IF
           END-IF
      *
           EVALUATE    WRK-MOJI
               WHEN    "1"
      *            手技料集計
      *            ％加算・減算は集計しない
                   IF      WRK-COM-TENSIKIBETU (IDX-Z)
                                               =   5
                                               OR  6
                       CONTINUE
                   ELSE
                       PERFORM 100212-SYUGI-KEI-SEC
                   END-IF
           END-EVALUATE
      *    回数
           MOVE    LNK-ORCSC-KAISU     TO  WRK-COM-KAISU (IDX-Z)
      *
           .
       10021-KEISAN-EXT.
           EXIT.
      *
      *****************************************************************
      *    手技料計算処理
      *****************************************************************
       100212-SYUGI-KEI-SEC            SECTION.
      *
           MOVE    1                   TO  FLG-SYUGI
           EVALUATE    FLG-SINDAN
               WHEN    1
      *            Ｘ線撮影料
                   PERFORM 1002121-XSEN-KEI-SEC
               WHEN    OTHER
      *            その他
      *            通則加算対象外区分判定
                   IF      WRK-COM-TUSOKUGAIKBN (IDX-Z)  =   "1"
                       ADD     WRK-COM-KISOTEN (IDX-Z)
                                                   TO  WRK-SUM-TENGAKEI
                   ELSE
                       ADD     WRK-COM-KISOTEN(IDX-Z)
                                                   TO  WRK-SUM-TENSUKEI
                   END-IF
           END-EVALUATE
      *
           .
       100212-SYUGI-KEI-EXT.
           EXIT.
      *****************************************************************
      *    Ｘ線撮影料計算処理
      *****************************************************************
       1002121-XSEN-KEI-SEC             SECTION.
      *
           EVALUATE    WRK-COM-TNSSRYSYUKBN(IDX-Z)(1:2)
               WHEN    "72"
      *            撮影料編集
                   MOVE    ZERO                    TO  FLG-SATU
      *            撮影料を逓減、乳幼児加算後にきざみ値計算をする
                   PERFORM 1000131-SATUEI-KEI-SEC
                   ADD     WRK-COM-KISOTEN (IDX-Z) TO  WRK-TENSUKEI1
                   ADD     1                   TO  WRK-CNT-SATUEI
                   MOVE    IDX-Z               TO  IDX-SYU
               WHEN    "71"
      *            診断料
                   MOVE    1                      TO  FLG-SATU
                   PERFORM 1000133-SINDAN-KEISAN-SEC
                   ADD     WRK-COM-KISOTEN (IDX-Z) TO  WRK-TENSUKEI2
      *        その他
                WHEN    OTHER
                   ADD     WRK-COM-KISOTEN(IDX-Z)
                                                   TO  WRK-TENSUKEI3
                   IF    WRK-COM-DATAKBN (IDX-Z)   =   "1"
                       MOVE    IDX-Z               TO  IDX-SYU
                   END-IF
           END-EVALUATE
      *
           .
       1002121-XSEN-KEI-EXT.
           EXIT.
      *
      *****************************************************************
      *     撮影料編集処理
      *****************************************************************
       1000131-SATUEI-KEI-SEC          SECTION.
      *
           MOVE    WRK-COM-TEN (IDX-Z) TO  WRK-SATU-TEN
           MOVE    ZERO                TO  WRK-SATUEI-SURYO
      *
      *    間接撮影による逓減計算
           PERFORM VARYING     IDX-Z2  FROM    IDX-STR BY  1
                   UNTIL      (IDX-Z2      >   SPA-MAX-LINE  )  OR
                              (WRK-GMN-INPUTCD(IDX-Z2)  =   SPACE )
               IF      (IDX-Z          NOT =   IDX-Z2   )  AND
                       (WRK-COM-AUTOKBN(IDX-Z2)  =   "K"   )
                   MOVE    SPA-MAX-LINE          TO  IDX-Z2
               ELSE
                   IF     (WRK-COM-TENSIKIBETU (IDX-Z2) =   6 )  AND
                          (WRK-COM-KIJUNTEIGENKBN(IDX-Z2)
                                                       =   ZERO ) AND
                          (WRK-COM-AUTOKBN    (IDX-Z2) =   SPACE )
      *                間接撮影による逓減計算（診断料）
                       COMPUTE WRK-GENTEN      =   WRK-SATU-TEN
                                           *  (WRK-COM-TEN  (IDX-Z2)
                                           /   100        )
                       COMPUTE WRK-SATU-TEN    =   WRK-SATU-TEN
                                               -   WRK-GENTEN
                       MOVE    1               TO  WRK-COM-SANFLG
                                                          (IDX-Z2)
                       MOVE    SPA-MAX-LINE    TO  IDX-Z2
                   END-IF
               END-IF
           END-PERFORM
      *
           MOVE    ZERO                TO  WRK-SATU-KASAN
      *    乳幼児加算（％加算）
      **** PERFORM VARYING     IDX-Z2  FROM    1    BY  1
           PERFORM VARYING     IDX-Z2  FROM    IDX-STR  BY  1
                   UNTIL      (IDX-Z2      >   SPA-MAX-LINE  )  OR
                              (WRK-GMN-INPUTCD(IDX-Z2)  =   SPACE ) OR
                              (WRK-COM-AUTOKBN(IDX-Z2)  =   "K"   )
              IF     (WRK-COM-SANFLG(IDX-Z2)  =   2 OR 5 )  AND
                     (WRK-COM-TENSIKIBETU(IDX-Z2)  =   5  )
                   MOVE    ZERO                TO  WRK-TENSUKEIS
                   COMPUTE WRK-TENSUKEIS       =   WRK-SATU-TEN
                                               *   WRK-COM-TEN (IDX-Z2)
                                               /   100
                   MOVE    5               TO  WRK-COM-SANFLG (IDX-Z2)
                   COMPUTE WRK-SATU-TEN        =   WRK-SATU-TEN
                                               +   WRK-TENSUKEIS
      *            加算の割合を編集する
                   COMPUTE WRK-SATU-KASAN      =   WRK-COM-TEN (IDX-Z2)
                                               /   100
      *
                   MOVE    SPA-MAX-LINE        TO  IDX-Z2
               END-IF
           END-PERFORM
      *
      *    きざみ値判定
           IF     (WRK-COM-INPUTCD (IDX-Z)(1:1)    =   "1" ) AND
                  (WRK-COM-DATAKBN (IDX-Z)         =   "1" ) AND
                  (WRK-COM-KZMCOMPSIKIBETU (IDX-Z) NOT =   ZERO)
      *        きざみ値計算
               MOVE    ZERO                TO  FLG-SATU-GEN
               MOVE    ZERO                TO  FLG-SATU
               PERFORM 1000132-TENSU-KEISAN-SEC
      *        撮影料の数量を保存
               MOVE    WRK-COM-SURYO (IDX-Z)   TO  WRK-SATUEI-SURYO
           ELSE
      *        撮影料
               COMPUTE WRK-TENSUKEI-9      =   WRK-SATU-TEN  + 0.5
               MOVE    WRK-TENSUKEI-9      TO  WRK-COM-KISOTEN(IDX-Z)
           END-IF
      *
           .
       1000131-SATUEI-KEI-EXT.
           EXIT.
      *
      *****************************************************************
      *     診断料その他処理
      *****************************************************************
       1000133-SINDAN-KEISAN-SEC          SECTION.
      *
           MOVE    WRK-COM-TEN  (IDX-Z)    TO  WRK-SATU-TEN
      *    乳幼児加算クリア
           MOVE    ZERO                TO  WRK-SATU-KASAN
      *
      *    間接撮影による逓減計算
           IF     (WRK-CNT-SATUEI      =   1   )  AND
                  (WRK-COM-TNSSRYSYUKBN(IDX-Z)(1:2)  =  "71" )
               PERFORM VARYING     IDX-Z2  FROM    IDX-STR BY  1
                   UNTIL      (IDX-Z2      >   SPA-MAX-LINE  )  OR
                              (WRK-GMN-INPUTCD(IDX-Z2)  =   SPACE ) OR
                              (WRK-COM-AUTOKBN(IDX-Z2)  =   "K"   )
                   IF     (WRK-COM-TENSIKIBETU (IDX-Z2) =   6 )  AND
                          (WRK-COM-KIJUNTEIGENKBN(IDX-Z2)
                                                       =   ZERO ) AND
                          (WRK-COM-AUTOKBN    (IDX-Z2) =   SPACE )
      *                間接撮影による逓減計算（診断料）
                       COMPUTE WRK-GENTEN      =   WRK-SATU-TEN
                                           *  (WRK-COM-TEN  (IDX-Z2)
                                           /   100        )
                       COMPUTE WRK-SATU-TEN    =   WRK-SATU-TEN
                                               -   WRK-GENTEN
                       MOVE    1               TO  WRK-COM-SANFLG
                                                          (IDX-Z2)
                       MOVE    SPA-MAX-LINE    TO  IDX-Z2
                   END-IF
               END-PERFORM
           END-IF
      *
      *    同時撮影による逓減
           IF    ((WRK-CNT-SATUEI      >   1   )  AND
                  (WRK-COM-TNSSRYSYUKBN(IDX-Z) =  "711"  ))
      ***                                      OR "710"  )
              OR  (WRK-COM-TNSSRYSYUKBN(IDX-Z) =  "710"  )
               IF      WRK-COM-TNSSRYSYUKBN(IDX-Z) =  "711"
                   COMPUTE WRK-SATU-TEN        =   WRK-SATU-TEN
                                               /   2
               END-IF
               MOVE    1                   TO  FLG-SATU-GEN
           ELSE
               MOVE    ZERO                TO  FLG-SATU-GEN
           END-IF
      *
      *    きざみ値判定
           IF     (WRK-COM-INPUTCD (IDX-Z)(1:1)    =   "1" ) AND
                  (WRK-COM-DATAKBN (IDX-Z)         =   "1" ) AND
                  (WRK-COM-KZMCOMPSIKIBETU (IDX-Z) NOT =   ZERO)
      *        撮影料の数量を数量へ
               IF      WRK-SATUEI-SURYO    >   ZERO
      ************ MOVE    "1"             TO  WRK-COM-SURFLG (IDX-Z)
                   MOVE    WRK-SATUEI-SURYO    TO  WRK-COM-SURYO (IDX-Z)
                   MOVE    WRK-SATUEI-SURYO    TO  WRK-KEIMAI
                   MOVE    1                   TO  FLG-SATU
               ELSE
                   MOVE    ZERO                TO  FLG-SATU
               END-IF
      *        きざみ値計算
               PERFORM 1000132-TENSU-KEISAN-SEC
           ELSE
               COMPUTE WRK-TENSUKEI-9      =   WRK-SATU-TEN  + 0.5
               MOVE    WRK-TENSUKEI-9      TO  WRK-COM-KISOTEN(IDX-Z)
           END-IF
      *
      *
           .
       1000133-SINDAN-KEISAN-EXT.
           EXIT.
      *
      *****************************************************************
      *     きざみ値計算処理
      *****************************************************************
       1000132-TENSU-KEISAN-SEC          SECTION.
      *
           MOVE    WRK-COM-INPUTCD (IDX-Z)
                                       TO  TNS-SRYCD
           PERFORM 910-TENSU-READ-SEC
      *
      *    きざみ値計算
           MOVE    WRK-COM-SURYO (IDX-Z)   TO  WRK-SURYO
           MOVE    ZERO                    TO  WRK-KEI-TEN2
      *    きざみ点数を算定点数の１／２とする
           IF      WRK-SATU-TEN        =   ZERO
               MOVE    TNS-KZMTEN          TO  WRK-KZMTEN
      *        乳幼児加算計算
               IF      WRK-SATU-KASAN      >   ZERO
                   COMPUTE WRK-KZMTEN      =   (TNS-KZMTEN
                                           *    WRK-SATU-KASAN )
                                           +    TNS-KZMTEN
               END-IF
           ELSE
               IF      FLG-SATU-GEN        =   ZERO
                   COMPUTE WRK-KZMTEN          =   WRK-SATU-TEN   /  2
               ELSE
                   MOVE    WRK-SATU-TEN        TO  WRK-KZMTEN
               END-IF
           END-IF
      *    きざみ値判定
           EVALUATE    TRUE
      *            データ≦（下限値−きざみ値）
               WHEN    WRK-SURYO       <=  (TNS-KZMKGN  -  TNS-KZM)
                   IF      TNS-KZMERR          =   2  OR  3
                       MOVE    "0021"          TO  SPA-ERRCD
                   END-IF
      *
      *            下限値＜データ≦上限値
               WHEN     (TNS-KZMKGN        <   WRK-SURYO )   AND
                        (TNS-KZMJGN        >=  WRK-SURYO )
      *            点数計算式１
                   COMPUTE WRK-KEI-TEN1    =  (WRK-SURYO -  TNS-KZMKGN)
                                           /   TNS-KZM
                                           +   0.999
                   MOVE    WRK-KEI-TEN1    TO  WRK-KEI-TEN2
      *            点数がゼロの時、この点数とする
                   COMPUTE WRK-KEI-TEN     =   WRK-SATU-TEN
                                               + ( WRK-KEI-TEN2
                                               *   WRK-KZMTEN )
                   COMPUTE WRK-KEI-TEN2    =   WRK-KEI-TEN    +  0.5
      *
      *            上限値＜データ
               WHEN    TNS-KZMJGN        <   WRK-SURYO
                   IF      TNS-KZMERR        =   0  OR  2
      *            点数計算式１をせず、エラーとする
      *************    MOVE    "0022"        TO  SPA-ERRCD
      *            警告エラーとする
                       MOVE    "K030"            TO  SPA-ERRCD
      *            点数計算式１
                       COMPUTE WRK-KEI-TEN1    =  (WRK-SURYO
                                               -  TNS-KZMKGN)
                                               /   TNS-KZM
                                               +   0.999
                       MOVE    WRK-KEI-TEN1    TO  WRK-KEI-TEN2
                       COMPUTE WRK-KEI-TEN     =   TNS-TEN
                                               + ( WRK-KEI-TEN2
                                               *   TNS-KZMTEN )
                       COMPUTE WRK-KEI-TEN2    =   WRK-KEI-TEN    +  0.5
                   ELSE
      *            点数計算式２
                       COMPUTE WRK-KEI-TEN1    =  (TNS-KZMJGN
                                               -   TNS-KZMKGN)
                                               /   TNS-KZM
                                               +   0.999
                       MOVE    WRK-KEI-TEN1    TO  WRK-KEI-TEN2
                       COMPUTE WRK-KEI-TEN     =   WRK-SATU-TEN
                                                   + ( WRK-KEI-TEN2
                                                   *   WRK-KZMTEN )
                       COMPUTE WRK-KEI-TEN2    =   WRK-KEI-TEN  +  0.5
                   END-IF
           END-EVALUATE
      *
           IF      SPA-ERRCD           =   SPACE
               IF      WRK-KEI-TEN2        >   ZERO
                   MOVE    WRK-KEI-TEN2    TO  WRK-COM-KISOTEN  (IDX-Z)
               ELSE
                   COMPUTE WRK-TENSUKEI-9  =   WRK-SATU-TEN  + 0.5
                   MOVE    WRK-TENSUKEI-9  TO  WRK-COM-KISOTEN(IDX-Z)
               END-IF
           END-IF
      *
           .
       1000132-TENSU-KEISAN-EXT.
           EXIT.
      *
      *****************************************************************
      *    手技料の計算処理
      *****************************************************************
       10021-TENSUKEI-SEC             SECTION.
      *
      *
           MOVE    IDX-Z               TO  HZN-IDX
           PERFORM 1006-KASAN-SET-SEC
           EVALUATE    FLG-SINDAN
               WHEN    1
      *            Ｘ線撮影
                   COMPUTE LNK-SC90-TENSU  =   WRK-TENSUKEI1
                                           +   WRK-TENSUKEI2
                                           +   WRK-TENSUKEI3
                                           +   WRK-TENSUKEI
               WHEN    OTHER
                   COMPUTE LNK-SC90-TENSU  =  (WRK-SUM-TENSUKEI
                                           +   WRK-SUM-TENGAKEI )
                                           +   0.5
           END-EVALUATE
      *
           .
       10021-TENSUKEI-EXT.
           EXIT.
      *
      *****************************************************************
      *    割合加算計算処理
      *****************************************************************
       1006-KASAN-SET-SEC              SECTION.
      *
           MOVE    ZERO                TO  WRK-TENSUKEI-G
           MOVE    ZERO                TO  WRK-TENSUKEI-K
      *
      *    手技料の点数を所定点数とする
           PERFORM VARYING     IDX     FROM    IDX-SYU  BY  1
                   UNTIL      (IDX         >=  HZN-IDX     )   OR
                              (WRK-GMN-INPUTCD (IDX)   =   SPACE )
      *      間接撮影逓減は対象外
             IF       (WRK-COM-TENSIKIBETU (IDX)   =   6 )  AND
                      (WRK-COM-KIJUNTEIGENKBN(IDX)
                                                   =   ZERO ) AND
                      (WRK-COM-AUTOKBN    (IDX)    =   SPACE ) AND
                      (WRK-COM-SANFLG     (IDX)    =   1    )
               CONTINUE
             ELSE
      *        ％加算
               IF     (WRK-COM-INPUTCD(IDX)(1:1)   =   "1" )  AND
                      (WRK-COM-TENSIKIBETU(IDX)    =   5   )  AND
                      (WRK-COM-SANFLG     (IDX)    =   ZERO  OR  2 )
                   MOVE    ZERO                TO  WRK-TENSUKEI
                   COMPUTE WRK-TENSUKEI        =   WRK-COM-KISOTEN
                                                               (IDX-SYU)
                                               *   WRK-COM-KISOTEN(IDX)
                                               /   100
                                               +   0.5
                   MOVE    WRK-TENSUKEI        TO  WRK-TENSUKEI-9
                   ADD     WRK-TENSUKEI-9      TO  WRK-TENSUKEI-K
                   MOVE    ZERO                TO  WRK-TENSUKEI
               END-IF
      *        ％減算
               IF     (WRK-COM-INPUTCD(IDX)(1:1)   =   "1" )  AND
                      (WRK-COM-TENSIKIBETU(IDX)    =   6   )  AND
                      (WRK-COM-SANFLG     (IDX)    =   ZERO OR 2 )
                   MOVE    ZERO                TO  WRK-TENSUKEI
                   COMPUTE WRK-TENSUKEI        =   WRK-COM-KISOTEN
                                                               (IDX-SYU)
                                               *   WRK-COM-KISOTEN(IDX)
                                               /   100
                   MOVE    WRK-TENSUKEI        TO  WRK-TENSUKEI-9
                   ADD     WRK-TENSUKEI-9      TO  WRK-TENSUKEI-G
                   MOVE    ZERO                TO  WRK-TENSUKEI
               END-IF
             END-IF
           END-PERFORM
      *
           COMPUTE WRK-SUM-TENSUKEI    =   WRK-SUM-TENSUKEI
                                       +   WRK-TENSUKEI-K
                                       -   WRK-TENSUKEI-G
      *
           .
       1006-KASAN-SET-EXT.
           EXIT.
      *
      *****************************************************************
      *    手技毎の計算処理
      *****************************************************************
       1007-TENSU-SUM-SEC            SECTION.
      *
      *    手技毎の計算処理
           MOVE    "1"                 TO  LNK-SC90-KBN
           MOVE    IDX-SYU             TO  LNK-SC90-IDX
           MOVE    IDX-STR             TO  LNK-SC90-IDX-STR
           COMPUTE LNK-SC90-IDX-END    =   IDX-Z   -   1
           CALL    "ORCSC90TEN"        USING
                                       ORCSC90AREA
                                       WRK-SCR-REC
      *
           MOVE    IDX-Z               TO  IDX-STR
           INITIALIZE                  WRK-TENSU-KEISAN
           MOVE    ZERO                TO  LNK-SC90-TENSU
      *
           .
       1007-TENSU-SUM-EXT.
           EXIT.
      *
      *****************************************************************
      *    全診療行為チェック処理
      *****************************************************************
       200-ALL-SYORI-SEC                SECTION.
      *
      *    包括まとめ入力は対象外
           IF      SPA-HKNCOMBINUM     =   "9999"
               GO      TO      200-ALL-SYORI-EXT
           END-IF
      *
      *
           MOVE    ZERO                TO  FLG-JIKASAN
           MOVE    ZERO                TO  IDX-GAI
           MOVE    ZERO                TO  IDX-CT
      *
           MOVE    ZERO                TO  FLG-TENTEKI
      *    ＣＴ、ＭＲＩチェック
      *        労災の時、変更なし
           IF      SPA-HKNKBN          =   1
      *****  AND  (SPA-RSI-HKNKBN  NOT =   "5" )
             AND  (SPA-RSI-JUNKYO  NOT =   "1" )
               INITIALIZE                  WRK-CTMRI-CNT-G
           ELSE
               PERFORM 2002-CTMRI-CNT-SEC
           END-IF
      *
           MOVE    ZERO                TO  FLG-TENTEKI
      *
           PERFORM VARYING     IDX-Z   FROM    1   BY  1
                   UNTIL      (IDX-Z       >   SPA-MAX-LINE     )   OR
                              (SPA-ERRCD           NOT =   SPACE )  OR
                              (WRK-GMN-INPUTCD (IDX-Z) =   SPACE )
             IF   (WRK-COM-HKNCOMBI (IDX-Z) NOT =   "9999")
      *        時間外緊急院内画像診断加算
               IF      WRK-COM-INPUTCD(IDX-Z)  =   "170016010"
                   MOVE    IDX-Z           TO  IDX-GAI
               END-IF
      *
      *        時間外加算チェック（診察、加算、時間加算のもの）
               IF     (WRK-COM-SRYKBN  (IDX-Z)     =   "11"  OR
                                                       "12"     )
                   IF     (WRK-COM-DATAKBN   (IDX-Z)   =   "2"  ) AND
                          (WRK-COM-TIMEKSNKBN(IDX-Z)   NOT =   ZERO)
                       MOVE    1               TO  FLG-JIKASAN
                   END-IF
                   IF     (WRK-COM-DATAKBN (IDX-Z)   =   "1"  ) AND
                          (WRK-COM-TIMEFLG (IDX-Z)   NOT =   ZERO )
                       MOVE    1               TO  FLG-JIKASAN
                   END-IF
               END-IF
      *
      *        ＣＴ．ＭＲＩの算定チェック
               IF      WRK-COM-KNSJISGRPKBN (IDX-Z)    =  32
                   PERFORM 2001-CTMRI-CHK-SEC
               END-IF
      *        点滴は１回のみとする
      *        外来の時のみ（入院は点滴でチェック）
               IF     (SPA-NYUGAIKBN               =   "2"   )  AND
                      (WRK-COM-SRYSYUKBN(IDX-Z) NOT =  "950"  AND
                                                       "960") AND
                      (WRK-COM-INPUTCD  (IDX-Z)    =   "130009310" OR
                                                       "130003710" OR
                                                       "130003810")
                   IF      FLG-TENTEKI         =   1
                       MOVE    "0057"          TO  SPA-ERRCD
                       MOVE    "1"             TO  WRK-COM-CUR(IDX-Z)
                   END-IF
                   MOVE    1               TO  FLG-TENTEKI
               END-IF
      *
             END-IF
           END-PERFORM
      *
           IF      SPA-ERRCD       NOT =   SPACE
               GO      TO      200-ALL-SYORI-EXT
           END-IF
      *
      *    時間外緊急院内画像診断加算有り
           IF      IDX-GAI             >   ZERO
               MOVE    IDX-GAI             TO  IDX-Z
               IF     (FLG-JIKASAN         =   ZERO         )  AND
                      (WRK-COM-KZMFLG (IDX-Z)  =   SPACE    )  AND
                      (LNK-ORCSC-TOROKU    NOT =   "2"   )
      *            外来の場合のみ
                 AND  (SPA-NYUGAIKBN       =   "2"       )
                   MOVE    "K001"              TO  SPA-ERRCD
                   MOVE    "2"                 TO  WRK-COM-CUR (IDX-Z)
                   MOVE    "1"                 TO  WRK-COM-KZMFLG
                                                              (IDX-Z)
                   GO      TO      200-ALL-SYORI-EXT
               END-IF
      *        時間入力追加
               MOVE    "840000071"             TO  WRK-SRYCD
               IF      WRK-COM-INPUTCD(IDX-Z + 1)  =   WRK-SRYCD
                                                   OR  CONS-JIKAN
                   CONTINUE
               ELSE
                   MOVE    ZERO                TO  FLG-SATU
                   MOVE    IDX-Z               TO  IDX-S
                   MOVE    SPACE               TO  WRK-SRYCD-2
                   MOVE    ZERO                TO  FLG-NENREI
                   PERFORM 1003-JIDOU-CHK-SEC
                   PERFORM 1002-JIDOUSET-SEC
                   IF      FLG-ADD             =   1
                       MOVE    "1"             TO  WRK-COM-IN  (IDX-S)
                       MOVE    "2"             TO  WRK-COM-IN2 (IDX-S)
                   END-IF
               END-IF
           END-IF
      *
           .
       200-ALL-SYORI-EXT.
           EXIT.
      *
      *****************************************************************
      *    ＣＴ．ＭＲＩの算定回数編集処理
      *****************************************************************
       2002-CTMRI-CNT-SEC                SECTION.
      *
           INITIALIZE                      WRK-CTMRI-CNT-G
      *    頭部
           MOVE    "099700101"         TO  WRK-SRYCD
           PERFORM 20021-CTMRI-SANTEI-SEC
           MOVE    LNK-SC91-CNT        TO  WRK-CTMRI-CNT (1)
           MOVE    LNK-SC91-DAYCNT     TO  WRK-CTMRI-DCNT(1)
      *    躯幹
           MOVE    "099700102"         TO  WRK-SRYCD
           PERFORM 20021-CTMRI-SANTEI-SEC
           MOVE    LNK-SC91-CNT        TO  WRK-CTMRI-CNT (2)
           MOVE    LNK-SC91-DAYCNT     TO  WRK-CTMRI-DCNT (2)
      *        四肢
           MOVE    "099700103"         TO  WRK-SRYCD
           PERFORM 20021-CTMRI-SANTEI-SEC
           MOVE    LNK-SC91-CNT        TO  WRK-CTMRI-CNT (3)
           MOVE    LNK-SC91-DAYCNT     TO  WRK-CTMRI-DCNT (3)
      *
           .
       2002-CTMRI-CNT-EXT.
           EXIT.
      *
      *****************************************************************
      *    ＣＴ．ＭＲＩの算定チェック処理
      *****************************************************************
       20021-CTMRI-SANTEI-SEC                SECTION.
      *
      *    算定履歴チェック
           INITIALIZE                      ORCSC91AREA
      *****MOVE    "3"                 TO  LNK-SC91-KBN
           MOVE    "4"                 TO  LNK-SC91-KBN
           MOVE    LNK-ORCSC-SYORIFLG  TO  LNK-SC91-SYORIFLG
           MOVE    WRK-SRYCD           TO  LNK-SC91-SRYCD
           MOVE    ZERO                TO  LNK-SC91-GMN-IDX
           MOVE    LNK-ORCSC-TEISEI-AREA   TO  LNK-SC91-TEISEI-AREA
           CALL    "ORCSC91"           USING
                                       MCPAREA
                                       ORCSC91AREA
                                       WRK-SCR-REC
                                       SPA-AREA
                                       TNS-TENSU-REC
      *
      *    当月算定なし
           IF      LNK-SC91-ATO-CNT    <=  LNK-SC91-CNT
               COMPUTE LNK-SC91-CNT    =   LNK-SC91-CNT
                                       -   LNK-SC91-ATO-CNT
           ELSE
               MOVE    ZERO            TO  LNK-SC91-CNT
           END-IF
      *    訂正時
           IF     LNK-ORCSC-SYORIFLG   =   "1"
               IF      LNK-SC91-DAYCNT     <=  LNK-SC91-CNT
                   COMPUTE LNK-SC91-CNT    =   LNK-SC91-CNT
                                           -   LNK-SC91-DAYCNT
               END-IF
           ELSE
               MOVE    ZERO                TO  LNK-SC91-DAYCNT
           END-IF
      *
           .
       20021-CTMRI-SANTEI-EXT.
           EXIT.
      *
      *****************************************************************
      *    ＣＴ．ＭＲＩの算定チェック処理
      *****************************************************************
       2001-CTMRI-CHK-SEC                SECTION.
      *
           MOVE    SPACE               TO  WRK-KIDCD
           MOVE    SPACE               TO  WRK-SRYCD
      *
      *    労災の時、警告チェック
           IF      SPA-HKNKBN          =   1
      *****  AND  (SPA-RSI-HKNKBN  NOT =   "5" )
             AND  (SPA-RSI-JUNKYO  NOT =   "1" )
               PERFORM 20012-RSI-CTMRI-CHK-SEC
               GO      TO      2001-CTMRI-CHK-EXT
           END-IF
      *
           IF      SPA-NYUGAIKBN       =   "2"
               MOVE    WRK-COM-KAISU (IDX-Z)   TO  WRK-CTMRI-KAI
           ELSE
               MOVE    WRK-COM-NYU-KAI(IDX-Z)  TO  WRK-CTMRI-KAI
           END-IF
      *
      *    包括逓減区分判定
           EVALUATE    WRK-COM-HOUKATUTEIGENKBN (IDX-Z)
      *        頭部
               WHEN    "201"
                   IF      WRK-COM-INPUTCD (IDX-Z) =   "170022290"
                       IF     (WRK-CTMRI-CNT (1)   =   ZERO )  AND
                              (WRK-CTMRI-DCNT(1)   =   ZERO )
                            MOVE    "K003"             TO  WRK-KIDCD
                       END-IF
                   ELSE
                       IF      WRK-CTMRI-CNT (1)   >   ZERO
                           MOVE    "170022290"     TO  WRK-SRYCD
                       END-IF
                   END-IF
                   ADD     WRK-CTMRI-KAI       TO  WRK-CTMRI-CNT (1)
      *        躯幹
               WHEN    "202"
                   IF      WRK-COM-INPUTCD (IDX-Z) =   "170022390"
                       IF     (WRK-CTMRI-CNT (2)   =   ZERO )  AND
                              (WRK-CTMRI-DCNT(2)   =   ZERO )
                            MOVE    "K003"              TO  WRK-KIDCD
                       END-IF
                   ELSE
                       IF      WRK-CTMRI-CNT (2)   >   ZERO
                           MOVE    "170022390"     TO  WRK-SRYCD
                       END-IF
                   END-IF
                   ADD     WRK-CTMRI-KAI       TO  WRK-CTMRI-CNT (2)
      *        四肢
               WHEN    "203"
                   IF      WRK-COM-INPUTCD (IDX-Z) =   "170022490"
                       IF     (WRK-CTMRI-CNT (3)   =   ZERO )  AND
                              (WRK-CTMRI-DCNT (3)   =   ZERO )
                            MOVE    "K003"              TO  WRK-KIDCD
                       END-IF
                   ELSE
                       IF      WRK-CTMRI-CNT (3)   >   ZERO
                           MOVE    "170022490"     TO  WRK-SRYCD
                       END-IF
                   END-IF
                   ADD     WRK-CTMRI-KAI       TO  WRK-CTMRI-CNT (3)
           END-EVALUATE
      *
           IF      WRK-SRYCD       NOT =   SPACE
      *        診療コード変更
               MOVE    WRK-COM-CHK-SRYCD(IDX-Z)
                                           TO  WRK-HEN-SRYCD
               MOVE    1                   TO  FLG-ARI
               MOVE    IDX-Z               TO  IDX-T
               PERFORM 1002-JIDOUSET-SEC
               MOVE    SPACE               TO  WRK-COM-AUTOKBN (IDX-Z)
               IF      FLG-ADD             =   1
      *            変更の診療コードをセット
                   MOVE    WRK-HEN-SRYCD   TO  WRK-COM-CHK-SRYCD(IDX-S)
               END-IF
           END-IF
      *
           IF      WRK-KIDCD       NOT =  SPACE
      *        ＣＴ．ＭＲＩの警告チェック
               IF      WRK-COM-KZMFLG (IDX-Z)  =   SPACE
                   MOVE    "K003"              TO  SPA-ERRCD
                   MOVE    "2"                 TO  WRK-COM-CUR
                                                               (IDX-Z)
                   MOVE    "1"                 TO  WRK-COM-KZMFLG
                                                               (IDX-Z)
               END-IF
           END-IF
           .
      *
       2001-CTMRI-CHK-EXT.
           EXIT.
      *
      *****************************************************************
      *   労災の時、警告チェック処理
      *****************************************************************
       20012-RSI-CTMRI-CHK-SEC                SECTION.
      *
      *    包括逓減区分判定
           EVALUATE    WRK-COM-HOUKATUTEIGENKBN (IDX-Z)
      *        頭部
               WHEN    "201"
                   IF      WRK-COM-INPUTCD (IDX-Z) =   "170022290"
                        MOVE    "K003"              TO  WRK-KIDCD
                   END-IF
      *        躯幹
               WHEN    "202"
                   IF      WRK-COM-INPUTCD (IDX-Z) =   "170022390"
                        MOVE    "K003"              TO  WRK-KIDCD
                   END-IF
      *        四肢
               WHEN    "203"
                   IF      WRK-COM-INPUTCD (IDX-Z) =   "170022490"
                        MOVE    "K003"              TO  WRK-KIDCD
                   END-IF
           END-EVALUATE
           IF      WRK-KIDCD       NOT =  SPACE
      *        ＣＴ．ＭＲＩの警告チェック
               IF      WRK-COM-KZMFLG (IDX-Z)  =   SPACE
                   MOVE    "K003"              TO  SPA-ERRCD
                   MOVE    "2"                 TO  WRK-COM-CUR
                                                               (IDX-Z)
                   MOVE    "1"                 TO  WRK-COM-KZMFLG
                                                               (IDX-Z)
               END-IF
           END-IF
      *
           .
      *
       20012-RSI-CTMRI-CHK-EXT.
           EXIT.
      *
      *****************************************************************
      *    自動削除処理
      *****************************************************************
       10023-JIDOU-DEL-SEC                SECTION.
      *
           MOVE    WRK-SCR-REC         TO  HOZ-SCR-REC
           INITIALIZE                      WRK-SCR-REC
           MOVE    ZERO                TO  IDX-D2
           PERFORM VARYING     IDX-D1  FROM    1   BY  1
                   UNTIL       IDX-D1      >   SPA-MAX-LINE
               IF      IDX-T           =   IDX-D1
                   CONTINUE
               ELSE
                   ADD     1               TO  IDX-D2
                   MOVE    HOZ-GMN-TBL (IDX-D1)
                                           TO  WRK-GMN-TBL(IDX-D2)
               END-IF
           END-PERFORM
           .
      *
       10023-JIDOU-DEL-EXT.
           EXIT.
      *
      *****************************************************************
      *    自動算定処理
      *****************************************************************
       300-AUTO-SYORI-SEC                SECTION.
      *
      *    包括まとめ入力は対象外
           IF      SPA-HKNCOMBINUM     =   "9999"
               GO      TO      300-AUTO-SYORI-EXT
           END-IF
      *
           IF      LNK-ORCSC-PG        =   "K02"
               MOVE    SPA-WORK            TO  SPA-K01KYOUTU
           END-IF
      *    訂正で自動発生なしの場合
           IF     (LNK-ORCSC-SYORIFLG  =   1  )  AND
                  (SPA-TEISEI-FLG      =   "0")
               GO      TO      300-AUTO-SYORI-EXT
           END-IF
      *
           INITIALIZE                  FLG-AREA
           INITIALIZE                  IDX-AREA
           MOVE    ZERO                TO  FLG-CTMRI
           MOVE    ZERO                TO  FLG-XSEN
           MOVE    ZERO                TO  FLG-KAKUI
           MOVE    ZERO                TO  FLG-ENKALL
           MOVE    ZERO                TO  FLG-ENK1
           MOVE    ZERO                TO  FLG-ENK2
           MOVE    ZERO                TO  FLG-ENK3
           MOVE    ZERO                TO  FLG-XSEN-ARI
           MOVE    ZERO                TO  FLG-CTMRI-ARI
           MOVE    ZERO                TO  FLG-KAKUI-ARI
           INITIALIZE                  WRK-NYUINDAY-AREA
      *
           PERFORM VARYING     IDX-Z   FROM    1   BY  1
                   UNTIL      (IDX-Z       >   SPA-MAX-LINE     )   OR
                              (SPA-ERRCD           NOT =   SPACE )  OR
                              (WRK-GMN-INPUTCD (IDX-Z) =   SPACE )
             IF   (WRK-COM-HKNCOMBI (IDX-Z) NOT =   "9999")
               IF     (WRK-COM-SRYKBN (IDX-Z)  =   "70")  AND
                      (WRK-COM-INPUTCD(IDX-Z)(1:1)
                                               =   "1"  )  AND
                      (WRK-COM-DATAKBN(IDX-Z)  =   "1"  )
      *
                   EVALUATE    WRK-COM-TNSSRYSYUKBN(IDX-Z)
      *                Ｘ線有り
                       WHEN    "721"
                       WHEN    "720"
                           MOVE    1                   TO  FLG-XSEN
                           MOVE    1                   TO  FLG-XSEN-ARI
                           IF      IDX-XSEN            =   ZERO
                               MOVE    IDX-Z           TO  IDX-XSEN
                           END-IF
                   END-EVALUATE
      *
      *            検査等実施判定グループ
                   EVALUATE    WRK-COM-KNSJISGRPKBN (IDX-Z)
      *                核医学
                       WHEN    31
                           MOVE    1                   TO  FLG-KAKUI
                           MOVE    1                   TO  FLG-KAKUI-ARI
                           IF      IDX-KAKUI           =   ZERO
                               MOVE    IDX-Z           TO  IDX-KAKUI
                           END-IF
      *                コンピュータ
                       WHEN    32
                           MOVE    1                   TO  FLG-CTMRI
                           MOVE    1                   TO  FLG-CTMRI-ARI
                           IF      IDX-CTMRI           =   ZERO
                               MOVE    IDX-Z           TO  IDX-CTMRI
                           END-IF
                   END-EVALUATE
      *
      *            遠隔画像診断管理加算チェック
                   EVALUATE    WRK-COM-INPUTCD (IDX-Z)
                       WHEN    "170024910"
                       WHEN    "170025010"
                           MOVE    1                   TO  FLG-ENKALL
                       WHEN    "170025910"
      *                    Ｘ線
                           MOVE    1                   TO  FLG-ENK1
                       WHEN    "170026100"
                       WHEN    "170026210"
      *                    核医学
                           MOVE    1                   TO  FLG-ENK2
                       WHEN    "170026110"
                       WHEN    "170026310"
      *                    コンピュータ
                           MOVE    1                   TO  FLG-ENK3
                   END-EVALUATE
               END-IF
      *
      *        画像診断で入院回数の時、回数集計
               IF     (SPA-NYUGAIKBN               =   "1" )  AND
                      (WRK-COM-SRYKBN (IDX-Z)(1:1) =   "7" )  AND
                     ((WRK-GMN-INPUTCD(IDX-Z)(1:1) =   "*" ) OR
                      (WRK-COM-KAIFLG (IDX-Z)      =   "1" ) OR
                      (WRK-COM-ZAIEND (IDX-Z)      =   "1" ))
                   PERFORM 3009-NYUINKAISU-SEC
               END-IF
               IF      WRK-COM-ZAIEND (IDX-Z)  =   "1"
                   MOVE    ZERO                TO  FLG-XSEN-ARI
                   MOVE    ZERO                TO  FLG-CTMRI-ARI
                   MOVE    ZERO                TO  FLG-KAKUI-ARI
               END-IF
      *
             END-IF
           END-PERFORM
      *    画像診断管理加算追加
           PERFORM 3001-GAZOUKSN-SET-SEC
      *
           .
      *
       300-AUTO-SYORI-EXT.
           EXIT.
      *
      *****************************************************************
      *    画像診断管理加算追加処理
      *****************************************************************
       3001-GAZOUKSN-SET-SEC             SECTION.
      *
           MOVE    ZERO                TO  IDXS
      *
      *    画像診断管理加算追加（写真診断）
           IF     (FLG-XSEN            =   1    )  AND
                  (FLG-ENKALL          =   ZERO )  AND
                  (FLG-ENK1            =   ZERO )
      *        システム管理の区分判定
             AND  (SPA-GAZOUKSN1       =   1   )
      *        画像診断管理加算１
               MOVE    "170025210"         TO  WRK-SRYCD
               MOVE    SPACE               TO  WRK-SRYCD-2
               MOVE    WRK-XSEN-DAY        TO  WRK-NYU-DAY
               MOVE    SPACE               TO  WRK-SRYKA
               MOVE    SPACE               TO  WRK-HKNCOMBI
               IF      IDX-XSEN            >   ZERO
                   MOVE    WRK-COM-SRYKA (IDX-XSEN)    TO  WRK-SRYKA
                   MOVE    WRK-COM-HKNCOMBI(IDX-XSEN)  TO  WRK-HKNCOMBI
               END-IF
               PERFORM 3002-KASAN-SYORI-SEC
           END-IF
      *
      *    核医学診断料加算有り
           IF      FLG-KAKUI           =   1
      *        核医学診断料加算
               MOVE    "170015310"         TO  WRK-SRYCD
               MOVE    SPACE               TO  WRK-SRYCD-2
               MOVE    WRK-KAKUI-DAY       TO  WRK-NYU-DAY
               MOVE    SPACE               TO  WRK-SRYKA
               MOVE    SPACE               TO  WRK-HKNCOMBI
               IF      IDX-KAKUI           >   ZERO
                   MOVE    WRK-COM-SRYKA (IDX-KAKUI)   TO  WRK-SRYKA
                   MOVE    WRK-COM-HKNCOMBI(IDX-KAKUI) TO  WRK-HKNCOMBI
               END-IF
               PERFORM 3002-KASAN-SYORI-SEC
               IF     (FLG-ERR             =   ZERO )  AND
                      (FLG-ENKALL          =   ZERO )  AND
                      (FLG-ENK2            =   ZERO )
      *        システム管理の区分判定
                 AND  (SPA-GAZOUKSN1       =   1   )
      *            画像診断管理加算２（核医学）
                   MOVE    "170025610"         TO  WRK-SRYCD
                   MOVE    SPACE               TO  WRK-SRYCD-2
                   MOVE    WRK-KAKUI-DAY       TO  WRK-NYU-DAY
                   PERFORM 3002-KASAN-SYORI-SEC
                   IF      FLG-ERR             =   ZERO
                       CONTINUE
                   ELSE
      *                画像診断管理加算１（核医学）
                       IF      FLG-ARI         =   ZERO
                           MOVE    "170025410"         TO  WRK-SRYCD
                           MOVE    SPACE               TO  WRK-SRYCD-2
                           MOVE    WRK-KAKUI-DAY       TO  WRK-NYU-DAY
                           PERFORM 3002-KASAN-SYORI-SEC
                       END-IF
                   END-IF
               END-IF
           END-IF
      *
      *    コンピュータ診断料加算有り
           IF      FLG-CTMRI           =   1
      *        コンピュータ断層診断
               MOVE    "170015410"         TO  WRK-SRYCD
               MOVE    SPACE               TO  WRK-SRYCD-2
               MOVE    WRK-CTMRI-DAY       TO  WRK-NYU-DAY
               MOVE    SPACE               TO  WRK-SRYKA
               MOVE    SPACE               TO  WRK-HKNCOMBI
               IF      IDX-CTMRI           >   ZERO
                   MOVE    WRK-COM-SRYKA (IDX-CTMRI)   TO  WRK-SRYKA
                   MOVE    WRK-COM-HKNCOMBI(IDX-CTMRI) TO  WRK-HKNCOMBI
               END-IF
               PERFORM 3002-KASAN-SYORI-SEC
               IF     (FLG-ERR             =   ZERO )  AND
                      (FLG-ENKALL          =   ZERO )  AND
                      (FLG-ENK3            =   ZERO )
      *            システム管理の区分判定
                 AND  (SPA-GAZOUKSN1       =   1   )
      *            画像診断管理加算２（コンピュータ断層診断）
                   MOVE    "170025710"         TO  WRK-SRYCD
                   MOVE    SPACE               TO  WRK-SRYCD-2
                   MOVE    WRK-CTMRI-DAY       TO  WRK-NYU-DAY
                   PERFORM 3002-KASAN-SYORI-SEC
                   IF      FLG-ERR             =   ZERO
                       CONTINUE
                   ELSE
      *                画像診断管理加算１（コンピュータ断層診断）
                       IF      FLG-ARI         =   ZERO
                           MOVE    "170025510"         TO  WRK-SRYCD
                           MOVE    SPACE               TO  WRK-SRYCD-2
                           MOVE    WRK-CTMRI-DAY       TO  WRK-NYU-DAY
                           PERFORM 3002-KASAN-SYORI-SEC
                       END-IF
                   END-IF
               END-IF
           END-IF
      *
           .
      *
       3001-GAZOUKSN-SET-EXT.
           EXIT.
      *
      *****************************************************************
      *    入院日処理
      *****************************************************************
       3009-NYUINKAISU-SEC             SECTION.
      *
           MOVE    ZERO                TO  IDN1
           IF      WRK-GMN-INPUTCD(IDX-Z)(1:1)  NOT =   "*"
               MOVE    SPA-SRYYMD (7:2)    TO  IDN1
           ELSE
               PERFORM VARYING     IDN2    FROM    1   BY  1
                       UNTIL       IDN2    >   31
                   IF      WRK-COM-NYUINDAY (IDX-Z IDN2) >   ZERO
                       MOVE    IDN2            TO  IDN1
                       MOVE    31              TO  IDN2
                   END-IF
               END-PERFORM
           END-IF
      *    Ｘ線
           IF      FLG-XSEN-ARI        =   1
      *        入院時の初日
               IF     (WRK-XSEN-DAY    =   ZERO )  OR
                      (WRK-XSEN-DAY    >   IDN1 )
                   MOVE    IDN1            TO  WRK-XSEN-DAY
               END-IF
           END-IF
      *    核医学
           IF      FLG-KAKUI-ARI       =   1
      *        入院時の初日
               IF     (WRK-KAKUI-DAY   =   ZERO )  OR
                      (WRK-KAKUI-DAY   >   IDN1 )
                   MOVE    IDN1            TO  WRK-KAKUI-DAY
               END-IF
           END-IF
      *    コンピュータ
           IF      FLG-CTMRI-ARI       =   1
      *        入院時の初日
               IF     (WRK-CTMRI-DAY   =   ZERO )  OR
                      (WRK-CTMRI-DAY   >   IDN1 )
                   MOVE    IDN1            TO  WRK-CTMRI-DAY
               END-IF
           END-IF
      *
           .
       3009-NYUINKAISU-EXT.
           EXIT.
      *
      *****************************************************************
      *    加算料算定処理
      *****************************************************************
       3002-KASAN-SYORI-SEC         SECTION.
      *
           MOVE    ZERO                TO  FLG-ERR
      *
      *    今回入力にないこと
           PERFORM VARYING     IDX     FROM    1   BY  1
                   UNTIL      (IDX     >   SPA-MAX-LINE  )  OR
                              (FLG-ERR              =   1    )   OR
                              (WRK-GMN-INPUTCD(IDX) =   SPACE)
               IF      WRK-COM-INPUTCD(IDX)    =   WRK-SRYCD
                   MOVE    1                   TO  FLG-ERR
               END-IF
           END-PERFORM
           IF      FLG-ERR             =   1
               GO      TO      3002-KASAN-SYORI-EXT
           END-IF
           IF      WRK-SRYKA           =   SPACE
               MOVE    SPA-SRYKA           TO  WRK-SRYKA
           END-IF
           IF      WRK-HKNCOMBI        =   SPACE
               MOVE    SPA-HKNCOMBINUM     TO  WRK-HKNCOMBI
           END-IF
      *    追加のチェック
           PERFORM 3001-SANTEI-CHK-SEC
           IF      FLG-ERR             =   ZERO
      *
               MOVE    WRK-SRYCD       TO  TNS-SRYCD
               PERFORM 910-TENSU-READ-SEC
               IF      FLG-TENSU       =   ZERO
      *
                   ADD     1               TO  IDXS
                   MOVE    WRK-SRYCD       TO  LNK-ORCSC-SRYCD
                                                       (08 IDXS)
                   MOVE    TNS-TEN         TO  LNK-ORCSC-TENSU
                                                       (08 IDXS)
                   MOVE    WRK-HKNCOMBI    TO  LNK-ORCSC-HKNCOMBI
                                                       (08 IDXS)
                   MOVE    WRK-SRYKA       TO  LNK-ORCSC-SRYKA
                                                       (08 IDXS)
                   MOVE    WRK-SRYCD       TO  LNK-ORCSC-SRYCD
                                                       (08 IDXS)
                   MOVE    "700"           TO  LNK-ORCSC-SRYSYUKBN
                                                        (08 IDXS)
                   MOVE    "70"            TO  LNK-ORCSC-SRYKBN
                                                        (08 IDXS)
      *            自動発生分としない
                   MOVE    "4"             TO  LNK-ORCSC-AUTOKBN
                                                         (08 IDXS)
      *            入院回数
                   IF      WRK-NYU-DAY     >   ZERO
                       MOVE    "*/"           TO  LNK-ORCSC-INPUTCD
                                                    (08 IDXS 01)
                       MOVE    WRK-NYU-DAY    TO  LNK-ORCSC-INPUTCD
                                                    (08 IDXS 01)(3:2)
                   END-IF
               END-IF
           END-IF
      *
           .
       3002-KASAN-SYORI-EXT.
           EXIT.
      *
      *****************************************************************
      *    算定履歴チェック処理
      *****************************************************************
       3001-SANTEI-CHK-SEC         SECTION.
      *
           MOVE    ZERO                TO  FLG-ERR
           MOVE    ZERO                TO  FLG-ARI
      *
           MOVE    WRK-SCR-REC         TO  HOZ-SCR-REC
           MOVE    WRK-GMN-MAX         TO  WRK-IDX
           ADD     1                   TO  WRK-IDX
           IF      WRK-IDX             >   SPA-MAX-LINE
               MOVE    SPA-MAX-LINE         TO  WRK-IDX
           END-IF
      *    点数マスタ編集・基本チェック
           INITIALIZE                  ORCSC92AREA
           MOVE    "3"                 TO  LNK-SC92-KBN
           MOVE    LNK-ORCSC-SYORIFLG  TO  LNK-SC92-SYORIFLG
           MOVE    LNK-ORCSC-PG        TO  LNK-SC92-PG
           MOVE    WRK-IDX             TO  LNK-SC92-GMN-IDX
           MOVE    WRK-SRYCD           TO  LNK-SC92-SRYCD
           MOVE    LNK-ORCSC-NENREI    TO  LNK-SC92-NENREI
           MOVE    LNK-ORCSC-ROUJIN    TO  LNK-SC92-ROUJIN
           MOVE    SPACE               TO  LNK-SC92-LSTSRYKA
           MOVE    SPACE               TO  LNK-SC92-LSTSRYKAMEI
           MOVE    LNK-ORCSC-TEISEI-AREA   TO  LNK-SC92-TEISEI-AREA
           CALL    "ORCSC92"           USING
                                       MCPAREA
                                       ORCSC92AREA
                                       HOZ-SCR-REC
                                       SPA-AREA
      *
      *    エラーの時、追加なし
           IF     (SPA-ERRCD           NOT =   SPACE )  OR
                  (LNK-SC92-ERRAREA    NOT =   SPACE )
      *        算定履歴ありの時
               IF      LNK-SC92-ERRCD3 NOT =   SPACE
                   MOVE    1               TO  FLG-ARI
               END-IF
               MOVE    1                   TO  FLG-ERR
           END-IF
      *
           MOVE    SPACE               TO  SPA-ERRCD
      *
           .
       3001-SANTEI-CHK-EXT.
           EXIT.
      *
      *****************************************************************
      *    点数マスター読込
      *****************************************************************
       910-TENSU-READ-SEC         SECTION.
      *
           MOVE    SPA-SRYYMD          TO  TNS-YUKOSTYMD
           MOVE    SPA-SRYYMD          TO  TNS-YUKOEDYMD
      *
           MOVE    SPA-HOSPNUM         TO  TNS-HOSPNUM
           MOVE    TNS-TENSU-REC       TO  MCPDATA-REC
           MOVE    "tbl_tensu"         TO  MCP-TABLE
           MOVE    "key"               TO  MCP-PATHNAME
           PERFORM 910-DBSELECT-SEC
           IF      MCP-RC              =   ZERO
               MOVE    "tbl_tensu"         TO  MCP-TABLE
               MOVE    "key"               TO  MCP-PATHNAME
               PERFORM 920-DBFETCH-SEC
               IF      MCP-RC              =   ZERO
                   MOVE    MCPDATA-REC         TO  TNS-TENSU-REC
                   MOVE    ZERO                TO  FLG-TENSU
               ELSE
                   INITIALIZE                  TNS-TENSU-REC
                   MOVE    1                   TO  FLG-TENSU
               END-IF
           ELSE
               INITIALIZE                  TNS-TENSU-REC
               MOVE    1                   TO  FLG-TENSU
           END-IF
      *
           MOVE    "tbl_tensu"         TO  MCP-TABLE
           MOVE    "key"               TO  MCP-PATHNAME
           PERFORM 990-DBCLOSE-SEC
      *
           .
       910-TENSU-READ-EXT.
           EXIT.
      *
      *****************************************************************
      *    システム管理マスタ読み込み処理
      *****************************************************************
       900-KANRIMST-READ-SEC         SECTION.
      *
           PERFORM 920-DBFETCH-SEC
           IF      MCP-RC              =   ZERO
               MOVE    ZERO            TO  FLG-SYSKANRI
           ELSE
               MOVE    1               TO  FLG-SYSKANRI
           END-IF
      *
           .
       900-KANRIMST-READ-EXT.
           EXIT.
      *****************************************************************
      *    テーブル検索処理
      *****************************************************************
       910-DBSELECT-SEC                SECTION.
      *
           MOVE    "DBSELECT"      TO  MCP-FUNC
grpsys     CALL    "ORCDBMAIN"         USING   MCPAREA
                                               MCPDATA-REC
                                               SPA-AREA
      *
           .
       910-DBSELECT-EXT.
           EXIT.
      *****************************************************************
      *    ＤＢ読み込み処理
      *****************************************************************
       920-DBFETCH-SEC                 SECTION.
      *
           MOVE    "DBFETCH"       TO  MCP-FUNC
grpsys     CALL    "ORCDBMAIN"         USING   MCPAREA
                                               MCPDATA-REC
                                               SPA-AREA
      *
           .
      *
       920-DBFETCH-EXT.
           EXIT.
      *
      *****************************************************************
      *    ＤＢクロース処理
      *****************************************************************
       990-DBCLOSE-SEC           SECTION.
      *
           MOVE    "DBCLOSECURSOR"     TO  MCP-FUNC
grpsys     CALL    "ORCDBMAIN"         USING   MCPAREA
                                               MCPDATA-REC
                                               SPA-AREA
      *
           .
       990-DBCLOSE-EXT.
           EXIT.
      *
