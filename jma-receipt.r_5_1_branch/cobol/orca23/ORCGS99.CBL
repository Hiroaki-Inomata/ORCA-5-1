      *******************************************************************
      * Project code name "ORCA"
      * 日医標準レセプトソフト（JMA standard receipt software）
      * Copyright(C) 2002 JMA (Japan Medical Association)
      *
      * This program is part of "JMA standard receipt software".
      *
      *     This program is distributed in the hope that it will be useful
      * for further advancement in medical care, according to JMA Open
      * Source License, but WITHOUT ANY WARRANTY.
      *     Everyone is granted permission to use, copy, modify and
      * redistribute this program, but only under the conditions described
      * in the JMA Open Source License. You should have received a copy of
      * this licence along with this program. If not, stop using this
      * program and contact JMA, 2-28-16 Honkomagome, Bunkyo-ku, Tokyo,
      * 113-8621, Japan.
      ********************************************************************
       IDENTIFICATION              DIVISION.
       PROGRAM-ID.                 ORCGS99.
      *****************************************************************
      *  システム名        : ＯＲＣＡ
      *  サブシステム名    : 収納
      *  コンポーネント名  : 処理結果
      *  管理者            : 
      *  作成日付    作業者        記述
      *  04/11/25    NACL-太田     新規作成
      *****************************************************************
      *  プログラム修正履歴
      * Maj/Min/Rev  修正者        日付      内容
      *  03.04.01    NACL-森脇     06/12/19  プレビュー追加
      *  03.05.00    NACL-太田     07/05/20  グループ診療対応
      *****************************************************************
      *
       ENVIRONMENT                 DIVISION.
       CONFIGURATION               SECTION.
       INPUT-OUTPUT                SECTION.
       FILE-CONTROL.
      *
       DATA                        DIVISION.
      *FILE                        SECTION.
      *
      *
       WORKING-STORAGE             SECTION.
      *
      *    スパ領域
           COPY    "COMMON-SPA".
      *----(03.04.01)--ADD-START---
           COPY    "S01COMMON-SPA".
      *
           COPY    "ENUM-VALUE".
      *----(03.04.01)--ADD-END-----
      *
      *    フラグ領域
       01  FLG-AREA.
           03  FLG-END             PIC 9(01).
           03  FLG-SYSKANRI        PIC 9(01).
      *
      *    添字領域
       01  IDX-AREA.
           03  IDX                 PIC 9(04).
      *
       01  WRK-AREA.
           03  WRK-PAGE            PIC ZZZZZZZZZZ.
      *----(03.04.01)--ADD-START---
           03  WRK-PRT-PAGE        PIC 9(10).
      *----(03.04.01)--ADD-END-----
      *     
       01  WRK-HEN-AREA.
           03  WRK-HENTIME.
               05  WRK-HH          PIC X(02).
               05  FILLER          PIC X(01)   VALUE   ".".
               05  WRK-MM          PIC X(02).
               05  FILLER          PIC X(01)   VALUE   ".".
               05  WRK-SS          PIC X(02).
      *
       01  WRK-CONS-AREA.
      *    システム管理ＤＢ（３００３）のキー値  
           03  WRK-CONS-3003-KBNCD PIC X(08)   VALUE   "023".
      *    印刷管理ＤＢのキー値  
           03  WRK-CONS-PRTKANRI-TBL-KEY
                                   PIC X(08)   VALUE   "ORCBS01".
           03  WRK-CONS-PRTKANRI-TBL-GROUP
                                   PIC X(14)   VALUE   SPACE.
      *    ジョブ管理ＤＢのキー値  
           03  WRK-CONS-JOB-SHELLID
                                   PIC X(08)   VALUE   "ORCBS01".
           03  WRK-CONS-JOB-JOBID  PIC 9(07)   VALUE   ZERO.
      *
      *****************************************************************
      *    ファイルレイアウト
      *****************************************************************
      *
           COPY    "CPSK3003.INC".
      *
      *    ジョブ管理マスタ
       01  JOBKANRI-REC.
           COPY    "CPJOBKANRI.INC".
      *
      *****************************************************************
      *    サブプロ用　領域
      *****************************************************************
      *
      *    ＤＢ検索
           COPY    "MCPDATA.INC".
      *     
      *     COPY    "CPORCMCP.INC".  
      *
      *   ジョブ管理ＤＢ制御サブ
           COPY    "CPORCSJOBKANRI.INC".
      *
      *****************************************************************
      *    連絡　領域
      *****************************************************************
        LINKAGE                    SECTION.
      *
           COPY    "MCPAREA".
           COPY    "ORCA-SPA".
      *
           COPY    "LINKAREA".
      *
       01  SCRAREA.
           COPY    "ORCA23SCRAREA.INC".
      *
       PROCEDURE                   DIVISION    USING
           MCPAREA
           SPAAREA
           LINKAREA
           SCRAREA.
      *
      *****************************************************************
      *    主　　処理
      *****************************************************************
       000-PROC-SEC                SECTION.
      *
           MOVE    SPA-COMMON      TO  SPA-AREA
      *----(03.04.01)--ADD-START---
           MOVE    SPA-WORK        TO  SPA-S01KYOUTU
      *----(03.04.01)--ADD-END-----
      * 
           EVALUATE    MCP-STATUS  ALSO    MCP-EVENT
               WHEN    "LINK"      ALSO    ANY
                   PERFORM 100-INIT-SEC
      *    画面遷移
               WHEN    OTHER
                   PERFORM 200-GMNSENI
           END-EVALUATE
      *
      *----(03.04.01)--ADD-START---
           MOVE    SPA-S01KYOUTU   TO  SPA-WORK
      *----(03.04.01)--ADD-END----
           MOVE    SPA-AREA        TO  SPA-COMMON
      *
           .
           EXIT    PROGRAM
           .
      *****************************************************************
      *    初期　処理
      *****************************************************************
       100-INIT-SEC                SECTION.
      * 
           .
       100-INIT-EXT.
           EXIT.
      *
      *****************************************************************
      *    画面遷移処理
      *****************************************************************
       200-GMNSENI                 SECTION.
      *
           EVALUATE    MCP-EVENT   ALSO    MCP-WIDGET
      *    戻る
               WHEN    "CLICKED"   ALSO    "B01"
                   MOVE    SPACE           TO  SPA-S99-FLG
                   PERFORM 210-BACK
      *    情報削除
               WHEN    "CLICKED"   ALSO    "B01S"
                   MOVE    "DEL"           TO  SPA-S99-FLG
                   PERFORM 210-BACK
      *    処理状態
               WHEN    "CLICKED"   ALSO    "pandatimer1"
               WHEN    "CLICKED"   ALSO    "B11"
                   PERFORM 490-KAKUNIN-SEC
      *    プレビュー画面へ
               WHEN    "CLICKED"   ALSO    "B12"
                   MOVE    "OK"            TO  SPA-S99-FLG
                   PERFORM 210-BACK
           END-EVALUATE
      *
           .
       200-GMNSENI-EXT.
           EXIT.
      *****************************************************************
      *    戻る　処理
      *****************************************************************
       210-BACK                    SECTION.
      *
           MOVE    SPA-MOTOPG      TO  SPA-SAKIPG
           MOVE    "S99"           TO  SPA-MOTOPG
      *
           MOVE    "JOIN"          TO  MCP-PUTTYPE
           MOVE    SPA-SAKIPG      TO  MCP-WINDOW
      *
           PERFORM 900-PUT-WINDOW
      *
           MOVE    1               TO  FLG-END
      *
           .
       210-BACK-EXT.
           EXIT.
      *
      *****************************************************************
      *    処理確認処理
      *****************************************************************
       490-KAKUNIN-SEC             SECTION.
      *     
           MOVE    SPACE               TO  S99
           INITIALIZE                      S99
           MOVE    ZERO                TO  S99-COUNT
      *----(03.04.01)--ADD-START---
           MOVE    WIDGET-INSENSITIVE  TO  S99-B12-STATE
           MOVE    ZERO                TO  WRK-PRT-PAGE
      *----(03.04.01)--ADD-END----
      *
      *----(03.04.01)--UPD-START---
           IF      SPA-MOTOPG          =   "S02"
               MOVE    SPACE               TO  SYS-3003-REC 
               INITIALIZE                      SYS-3003-REC
               MOVE    SPA-HOSPNUM         TO  SYS-3003-HOSPNUM
               MOVE    "3003"              TO  SYS-3003-KANRICD
               MOVE    WRK-CONS-3003-KBNCD TO  SYS-3003-KBNCD
               MOVE    SPA-SYSYMD          TO  SYS-3003-STYUKYMD
                                               SYS-3003-EDYUKYMD
               MOVE    SYS-3003-REC        TO  MCPDATA-REC
               PERFORM 900-SYSKANRI-INV-SEC
               IF      FLG-SYSKANRI        =   ZERO
                   MOVE    MCPDATA-REC         TO  SYS-3003-REC 
                   PERFORM VARYING IDX FROM    1   BY  1
                           UNTIL   IDX >       SYS-3003-JOBID
      *                ジョブ管理チェック処理
                       MOVE    "CHK"           TO  SJOBKANRI-MODE
                       INITIALIZE                  JOBKANRI-REC
                       MOVE    SPA-HOSPNUM     TO  JOB-HOSPNUM
                       MOVE    IDX             TO  JOB-JOBID
                       MOVE    WRK-CONS-JOB-SHELLID
                                               TO  JOB-SHELLID
                       CALL    "ORCSJOB"       USING
                                               ORCSJOBKANRIAREA  
                                               JOBKANRI-REC 
                                               SPA-AREA
                       IF      SJOBKANRI-RETURN    =   ZERO
                           PERFORM 4901-KAKUNIN-HYOUJI-SEC
                       END-IF
                   END-PERFORM
               END-IF
           ELSE
      *        ジョブ管理チェック処理
               MOVE    "CHK"           TO  SJOBKANRI-MODE
               INITIALIZE                  JOBKANRI-REC
               MOVE    SPA-HOSPNUM     TO  JOB-HOSPNUM
               MOVE    WRK-CONS-JOB-JOBID
                                       TO  JOB-JOBID
               MOVE    WRK-CONS-JOB-SHELLID
                                       TO  JOB-SHELLID
               CALL    "ORCSJOB"       USING
                                       ORCSJOBKANRIAREA
                                       JOBKANRI-REC
                                       SPA-AREA
               MOVE    1               TO  IDX 
               IF      SJOBKANRI-RETURN    =   ZERO
                   PERFORM 4901-KAKUNIN-HYOUJI-SEC
               END-IF
           END-IF
      *
           MOVE    "B01"                TO  MCP-WIDGET
           IF    ( S99-MSG              =   SPACE )
               IF    ( S99-COUNT        >   ZERO  )
                   MOVE    "処理は正常に終了しました"
                                                TO  S99-MSG
               ELSE
                   MOVE    "該当する処理はありません"
                                                TO  S99-MSG
               END-IF 
           END-IF                                 
      *
           MOVE    SPACE               TO  LINKAREA
      *----(03.04.01)--UPD-END-----        
      *
      *----(03.04.01)--ADD-START---
           IF    ( SPA-S01-SHR-ORDER   =   "PV" )
               MOVE    "F12:プレビュー"    TO  S99-B12-LABEL
               IF    ( WRK-PRT-PAGE    >   ZERO )
                       MOVE    WIDGET-NORMAL    TO  S99-B12-STATE
                       MOVE    "B12"            TO  MCP-WIDGET
               END-IF
           END-IF
      *----(03.04.01)--ADD-END-----
      *
           MOVE    "CURRENT"           TO  MCP-PUTTYPE
           MOVE    "S99    "           TO  MCP-WINDOW
      *
           PERFORM 900-PUT-WINDOW
           .
       490-KAKUNIN-EXT.
           EXIT.
      *
      *----(03.04.01)--ADD-START---
      *****************************************************************
      *    処理確認処理
      *****************************************************************
       4901-KAKUNIN-HYOUJI-SEC     SECTION.
      *
           MOVE    JOB-JOBID       TO  S99-RENNUM   (IDX)
           MOVE    JOB-SHELLMSG    TO  S99-SHORINM  (IDX)
           MOVE    JOB-STARTTIME(1:2)
                                   TO  WRK-HH
           MOVE    JOB-STARTTIME(3:2)
                                   TO  WRK-MM
           MOVE    JOB-STARTTIME(5:2)
                                   TO  WRK-SS
           MOVE    WRK-HENTIME     TO  S99-STARTTIME(IDX)
           MOVE    JOB-ENDTIME(1:2)
                                   TO  WRK-HH
           MOVE    JOB-ENDTIME(3:2)
                                   TO  WRK-MM
           MOVE    JOB-ENDTIME(5:2)
                                   TO  WRK-SS
           MOVE    WRK-HENTIME     TO  S99-ENDTIME  (IDX)
           MOVE    JOB-UPDCNT      TO  WRK-PAGE
           MOVE    WRK-PAGE        TO  S99-PAGE     (IDX)
           MOVE    JOB-YOBI        TO  S99-ERRMSG   (IDX)
           IF      JOB-ENDYMD      =   SPACE
               MOVE    "処理中です"    TO  S99-MSG
           ELSE
               IF      JOB-ERRCD       =   SPACE
                   CONTINUE
               ELSE
                   INITIALIZE          S99-MSG
                   STRING  JOB-YOBI    DELIMITED  BY  SPACE
                           "【"        DELIMITED  BY  SIZE
                          JOB-SHELLMSG DELIMITED  BY  SPACE
                          "】"         DELIMITED  BY  SIZE      
                                            INTO  S99-MSG
                   END-STRING
               END-IF                          
           END-IF  
          MOVE    IDX                     TO  S99-COUNT    
          IF    ( JOB-JOBID   =   SPA-S02-JOB-JOBID )
              MOVE    JOB-UPDCNT  TO  WRK-PRT-PAGE
          END-IF  
           .
       4901-KAKUNIN-HYOUJI-EXT.
           EXIT.
      *
      *****************************************************************
      *    エラーメッセージセット処理
      *****************************************************************
       4901-JOB-ERRSET-SEC         SECTION.
      *
           EVALUATE    JOB-ERRCD
               WHEN    "0001"
                   MOVE    "【ファイルがありません】" 
                                               TO  S99-MSG
               WHEN    "0002"
                   MOVE    "【ｆｄｄが起動されていません】" 
                                               TO  S99-MSG
               WHEN    "0003"
               WHEN    "0004"
                   MOVE    "【ｆｄｄで処理が失敗しました】" 
                                               TO  S99-MSG
               WHEN    "0011"
                   MOVE    "【媒体がマウントできませんでした】" 
                                               TO  S99-MSG
               WHEN    "0012"
                   MOVE    "【媒体がアンマウントできませんでした】" 
                                               TO  S99-MSG
               WHEN    "0021"
                   MOVE    "【ファイルコピーでエラーが発生しました】" 
                                               TO  S99-MSG
               WHEN    "0127"
                   MOVE    "【スクリプトが実行できませんでした】" 
                                               TO  S99-MSG
           END-EVALUATE
      *
           .
       4901-JOB-ERRSET-EXT.
           EXIT.
      *
      *----(03.04.01)--ADD-END-----
      *****************************************************************
      *    管理マスター読込（キー）
      *****************************************************************
       900-SYSKANRI-INV-SEC        SECTION.
      *
           MOVE    "DBSELECT"      TO  MCP-FUNC
           MOVE    "tbl_syskanri"  TO  MCP-TABLE
           MOVE    "key10"         TO  MCP-PATHNAME
           CALL    "ORCDBMAIN"         USING
                                       MCPAREA
                                       MCPDATA-REC
                                       SPA-AREA
           IF      MCP-RC              =   ZERO
               MOVE    "DBFETCH"       TO  MCP-FUNC
               MOVE    "tbl_syskanri"  TO  MCP-TABLE
               MOVE    "key10"         TO  MCP-PATHNAME
               CALL    "ORCDBMAIN"         USING
                                           MCPAREA
                                           MCPDATA-REC
                                           SPA-AREA
               IF      MCP-RC              =   ZERO
                   MOVE    ZERO            TO  FLG-SYSKANRI
               ELSE
                   MOVE    1               TO  FLG-SYSKANRI
               END-IF
           ELSE
               MOVE    1               TO  FLG-SYSKANRI
           END-IF
      *
           MOVE    "DBCLOSECURSOR" TO  MCP-FUNC
           MOVE    "tbl_syskanri"  TO  MCP-TABLE
           MOVE    "key10"         TO  MCP-PATHNAME
           CALL    "ORCDBMAIN"         USING
                                       MCPAREA
                                       MCPDATA-REC
                                       SPA-AREA
      *
           .
       900-SYSKANRI-INV-EXT.
           EXIT.
      *
      *****************************************************************
      *    ＰＵＴ　処理
      *****************************************************************
       900-PUT-WINDOW              SECTION.
      *
           MOVE    "PUTWINDOW"     TO  MCP-FUNC.
           CALL    "ORCDBMAIN"         USING
                                       MCPAREA
                                       MCPDATA-REC
                                       SPA-AREA
      *
           .
       900-PUT-WINDOW-EXT.
           EXIT.
      *
